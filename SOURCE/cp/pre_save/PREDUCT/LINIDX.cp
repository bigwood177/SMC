;  LINIDX / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		CREATES TEMPORARY INDEX OF LINE ITEM FILE
;		FOR PRINTING PICKING TICKETS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD ORDLIN
		.INCLUDE 'DEF:RD045A.DEF'
RECORD LINIDX
		.INCLUDE 'DEF:RD048A.DEF'
RECORD ITMMAS
		.INCLUDE 'DEF:RD041B.DEF'
RECORD SNDMSG
		,A3,'CP:'
		,A6,'ALNINV'
		,A10
RECORD NXTMSG
		,A3,'CP:'
		,A6,'PIKTIK'
RECORD
		.INCLUDE 'DEF:RD048S.DEF'
RECORD
	BLANKS	,A6
	DUMSIZ	,D5,	00500	;ARBITRARY RECORD COUNT FOR CREATING LINIDX
	ENDDAT	,D6
	WRKDAT	,D6
	TODAY	,D6
	BRACKS	,A14,	']]]]]]]]]]]]]]'
	CNGCTL	,D1
	WHATNO	,D1
	ENTRY	,A6
	INXCTL	,D1
	BLKSIZ	,D6
	LOCATN	,A2
	DEFALT	,A2
	RITTEN	,D1
	SWITCH	,D1
	ENDLIN	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	SWITCH = 5
	XCALL RDATE (TODAY)
	XCALL FILES (1,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GOTO END1
	LOKCTL = 1
	XCALL IO (1,ITMMAS,1,READ,LOKCTL)
	DEFALT = DFLTLO
	CLOSE 1
NOLOC,
	SWITCH = 1
	CALL OPENS
	XCALL OUTPT (1,1,2,'PRINT PICKING TICKETS',V)
DISPLA,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (6,17,0,'PLEASE INPUT LOCATION DESIRED',V)
	XCALL INPUT (6,48,2,0,'AE',ENTRY,INXCTL,V)
	GO TO (DISPLA,END3), INXCTL
	IF (ENTRY.EQ.BLANKS) ENTRY = DEFALT
	LOCATN = ENTRY
	IF (LOCATN.EQ.BLANKS) GOTO DISPLA
	XCALL OUTPT (6,48,0,LOCATN,V)
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (DISPLA), CNGCTL
DATE,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (6,15,0,'PLEASE INPUT CUTOFF DATE',V)
	XCALL INPUT (6,41,6,0,'DE',ENTRY,INXCTL,V)
	GO TO (DISPLA,END3), INXCTL
	IF (ENTRY.EQ.BLANKS) ENTRY = TODAY
	WRKDAT = ENTRY
	XCALL DSPLY (6,6,41,WRKDAT,2,V)
	ENDDAT (1,2) = WRKDAT (5,6)
	ENDDAT (3,6) = WRKDAT (1,4)
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (DATE), CNGCTL
	GO TO BLDIDX
;
OPENS,
	SWITCH = 3
	XCALL FILES (8,'O',48,SWITCH)		;FILE # 48 -- LINIDX FILE
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 1
	XCALL FILES (5,'SI',45,SWITCH)
	IF (SWITCH.EQ.9) GO TO END2
	BLKSIZ = (((DUMSIZ+1)*(SIZ048+2))/512) + 1
	XCALL OFILE (8,48,BLKSIZ,SIZ048,SWITCH)
	IF (SWITCH.EQ.1) GO TO NOROOM
	RETURN
ENDX,
	XCALL MESAG ('NO ITEMS ON ORDER AT THIS LOCATION / CUTOFF DATE',1)
	XCALL FILES (5,'SI',45,4)		;TO REPOSITION IOS READ
	SWITCH = 1				;  POINTER AT BEGINNING
	XCALL FILES (5,'SI',45,SWITCH)		;  OF ORDLIN FILE.
	ENDLIN = 0
	GO TO DISPLA
END3,
	XCALL WATE(3,V)
	XCALL FILES (5,'SI',45,4)
END2,
	XCALL FILES (8,'O',48,4)
END1,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:CPMENU',1)
NOROOM,
	XCALL MESAG
&	('CANNOT RUN SELECTION: NEED MORE ROOM ON DEVICE FOR LINIDX FILE',2)
	GO TO END3
BLDIDX,
	XCALL WATE(4,V)
	XCALL OUTPT (2,1,1,'BUILD INDEX',1)
NXTLIN,
	CALL GETLIN
	IF (RITTEN.EQ.0.AND.ENDLIN) GOTO ENDX
	IF (ENDLIN) GO TO END
	CALL WRTIDX
	GO TO NXTLIN
GETLIN,
	LOKCTL = 1
	XCALL IOS (5,ORDLIN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFLIN
	IF (LLOC.NE.LOCATN) GOTO GETLIN
	WRKDAT = LPRMDT
	IF (WRKDAT.EQ.0) WRKDAT = LEXSDT
	IF (WRKDAT.GT.ENDDAT) GO TO GETLIN
	RETURN
EOFLIN,
	ENDLIN = 1
	RETURN
WRTIDX,
	RITTEN = 1
	LXORDN = LORDNO
	LXPIKS = LPICSQ
	LXLSEQ = LINSEQ
	LOKCTL = 1
	XCALL IOS (8,LINIDX,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO NOROOM
	RETURN
END,
	LOKCTL = 1
	XCALL IOS (8,BRACKS(1,SIZ048),WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO NOROOM
	XCALL FILES (5,'SI',45,4)
	CLOSE 8
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (NXTMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (LOCATN,SWITCH)		;Send the Location to PIKTIK
						;This is for Component Items
	XCALL PGCHN ('CP:SRTLIX',0)
END
