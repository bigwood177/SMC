;  SHTMNT / COP
;
;
;
;		PROGRAM TO MAINTAIN THE SHIP-TO CODES FILE IN CUSTOMER ORDER PROCESSING
;
RECORD SHIPTO
		.INCLUDE 'DEF:RD171A.DEF'
RECORD ,X
		.INCLUDE 'DEF:RD171C.DEF'
RECORD SHPCTL	
		.INCLUDE 'DEF:RD171B.DEF'
RECORD CUSCTL
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD ARTCDE
		.INCLUDE 'DEF:RD169A.DEF'
RECORD ARTCTL
		.INCLUDE 'DEF:RD169B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SRTMSG
	PRGNAM	,A9,	'CP:SHTCNT'
	RCNT	,D5
	OCNT	,D5
RECORD MSGES
	TXCDNF	,A20,  	'TAX CODE NOT ON FILE'
	MSGCTL	,D1
RECORD KEY
	XCUSNO	,D6
	SHPNO	,D4
RECORD
	NXTPGM	,A9
	SRTPGM	,A9,	'CP:SRTSHT'
	ORGPGM	,A9,	'CP:ORGSHT'
	PRTPGM	,A9,	'CP:SHTPRT'
RECORD
	ALPHA   ,A2
	BS171   ,D5
	BSEND	,D5
	BSMID	,D5
	CKEY	,A6
	CNGCTL	,D1
	ENTRY	,A30
	INXCTL	,D1
	LOKCTL	,D1
	OPNFLG  ,D1
	PAYDEP  ,D2
	READ	,D1	,0
	RECCNT  ,D5
	SELECT  ,D1
	SORT    ,A1
	SRCCTL	,D1
	SWITCH	,D1
	TMPCOD	,A1
	V       ,D1
	WHATNO  ,D2
	WRITE	,D1	,1
	WRKDEP  ,A2
	WRKSAV  ,D5
PROC
	XCALL TERID(V)
MENU,
	XCALL OUTPT (1,1,2,'SHIP-TO ADDRESS FILE MAINTENANCE',V)
	XCALL OUTPT (4,20,0,'PLEASE SELECT APPLICATION',V)
	XCALL OUTPT (5,25,0,'1. ADD SHIP-TO ADDRESSES',V)
	XCALL OUTPT (6,25,0,'2. CHANGE/INQUIRE SHIP-TO ADDRESSES',V)
	XCALL OUTPT (7,25,0,'3. DELETE SHIP-TO ADDRESSES',V)
	XCALL OUTPT (8,25,0,'4. PRINT SHIP-TO ADDRESSES LIST',V)
	CTL = '08,47,01,01,#E'
	CALL INPUT
	GO TO (MENU,ENDOFF), INXCTL
	SELECT = ENTRY
	IF (SELECT.LT.1 .OR. SELECT.GT.4) GO TO MENU
	IF (SELECT.EQ.4) GO TO ASKSRT
	IF (OPNFLG.EQ.0) GO TO OPEN
	IF (SELECT.EQ.1 .OR. SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DISPLA
OPEN,
	XCALL WATE (3,V)
	SWITCH = 3
	XCALL FILES (1,'U',171,SWITCH)		;FILE # 171 -- SHIPTO FILE
	IF (SWITCH.EQ.9) GO TO INU171
	SWITCH = 5
	XCALL FILES (1,'U',171,SWITCH)		;FILE # 171 -- SHIPTO FILE
	IF (SWITCH.EQ.9) GO TO CLO171
	SWITCH = 1
	XCALL FILES (3,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO CLO171
	SWITCH = 1
	XCALL FILES (2,'I',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO CLO002
	SWITCH = 1
	XCALL FILES (4,'I',169,SWITCH)		;FILE # 169 -- ARTCDE FILE
	IF (SWITCH.EQ.9) GO TO CLO003
	LOKCTL = 0
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	RECCNT = REC171
	BS171 = ORG171
	UNLOCK 1
	LOKCTL = 1
	XCALL IO (2,CUSCTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (4,ARTCTL,1,READ,LOKCTL)
	OPNFLG = 1
	GO TO DISPLA
;**********************************************************************
CLO003,
	XCALL FILES (4,'I',169,4)
CLO002,
	XCALL FILES (3,'I',02,4)
CLO171,
	XCALL FILES (1,'U',171,4)
INU171,
	XCALL PGCHN ('CP:SPCFUN',1)
;**********************************************************************
ASKSRT,
	XCALL OUTPT (1,1,2,'SHIP-TO FILE MAINTENANCE',1)
	XCALL OUTPT (2,1,0,'PRINT SHIP-TO LIST',1)
	XCALL OUTPT (12,20,0,'SORT BEFORE PRINTING ?',1)
SRTQRY,
	CTL = '12,44,01,00,A '
	CALL INPUT
	IF (ENTRY.NE.'Y' .AND. ENTRY.NE.'N') ENTRY = 'N'
	XCALL OUTPT (6,44,0,ENTRY,V)
	SORT = ENTRY
ENDOFF,
	XCALL WATE(4,V)
	XCALL FILES (1,'U',171,4)
	XCALL FILES (4,'I',169,4)
	XCALL FILES (3,'I',02,4)
	XCALL FILES (2,'I',01,4)
	IF (SORT.EQ.'Y') GO TO ORGNIZ
	IF (DEL171.GE.95.AND.SELECT.NE.4) GO TO ORGNIZ
	IF (REC171-ORG171.GE.50.AND.SELECT.NE.4) GO TO ORGNIZ
PRTEND,
	IF (SELECT.EQ.4) XCALL PGCHN ('CP:SHTPRT',1)
	XCALL PGCHN ('CP:SPCFUN',1)
ORGNIZ,
	NXTPGM = 'CP:SPCFUN'
	IF (SELECT.EQ.4) NXTPGM = 'CP:SHTPRT'
	SWITCH = 2
	XCALL FILES (1,'U',171,SWITCH)
	IF (SWITCH.EQ.9) XCALL PGCHN (NXTPGM,0)
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	UNLOCK 1
	CLOSE 1
	RCNT = REC171
	OCNT = ORG171
	SWITCH = 5
	XCALL SNMSG (' ',SWITCH)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL SNMSG (SRTMSG,2)
	IF (DEL171.GE.95) XCALL SNMSG (ORGPGM,2)
	IF (SELECT.EQ.4) XCALL SNMSG (PRTPGM,2)
	IF ((RCNT-OCNT.GE.50) .OR. SORT.EQ.'Y') XCALL PGCHN (SRTPGM,0)
	XCALL PGCHN (ORGPGM,0)
;**********************************************************************
DISPLA,
	UNLOCK 1
	CNGCTL =
	XCALL OUTPT (1,1,2,'SHIP-TO FILE MAINTENANCE',1)
	IF (SELECT.EQ.1) XCALL OUTPT (2,1,1,'ADD',1)
	IF (SELECT.EQ.1 .AND. REC171.GE.MAX171) GO TO FULL
	IF (SELECT.EQ.2) XCALL OUTPT (2,1,1,'CHANGE/INQUIRE',1)
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) XCALL OUTPT (8,13,1,'*',1)
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) XCALL OUTPT (10,13,1,'*',1)
	IF (SELECT.EQ.3) XCALL OUTPT (2,1,1,'DELETE',1)
	XCALL OUTPT (8,17,0,'1. CUSTOMER NO',1)
	XCALL OUTPT (10,17,0,'2. SHIP-TO NO',1)
	XCALL OUTPT (12,17,0,'3. SHIP-TO NAME',1)
	XCALL OUTPT (14,17,0,'4. SHIP-TO ADDR-1',1)
	XCALL OUTPT (16,17,0,'5. SHIP-TO ADDR-2',1)
	XCALL OUTPT (18,17,0,'6. SHIP-TO ADDR-3',1)	
	XCALL OUTPT (20,17,0,'7. SHIP-TO TAX CODE',1)
WORK,
	XCALL OUTPT (8,38,1,'\',1)
	XCALL OUTPT (10,38,1,'\',1)
	IF (CNGCTL.EQ.1 .AND. SELECT.NE.1) GO TO BADCNG
	CTL = '08,38,06,01,#E'
	CALL INPUT
	GO TO (DISPLA,MENU), INXCTL
	XCUSNO = ENTRY
	IF (XCUSNO.EQ.0) GO TO WORK
	XCALL DSPLY (6,8,38,XCUSNO,1,1)
	CKEY = XCUSNO,'XXXXXX'
	BSEND = ORG001
	XCALL SERCH (3,CUSIDX,CKEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	GO TO (BADCUS), SRCCTL 
	GO TO (ANYCNG), CNGCTL
	LOKCTL = 1
	XCALL IO (2,CUSMAS,IRC001,READ,LOKCTL)
	XCALL OUTPT (8,46,0,NAME,1)
nocus,
	CTL = '10,38,04,01,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHPNO = ENTRY
	XCALL DSPLY (4,10,38,SHPNO,1,1)
	BSEND = BS171
	SRCCTL = 2
	XCALL SERCH (1,SHIPTO,KEY,1,10,BSEND,BSMID,SRCCTL,1,11,16,0,0,0,0)
	IF (SRCCTL.EQ.0.AND.SELECT.EQ.1) XCALL MESAG ('RECORD ALREADY EXISTS',1)
	IF (SRCCTL.EQ.0 .AND. SELECT.EQ.1) GO TO WORK
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) XCALL MESAG ('RECORD NOT FOUND',1)
	IF (SRCCTL.EQ.1 .AND. SELECT.NE.1) GO TO WORK
	WRKSAV = BSMID
	IF (SELECT.EQ.2 .OR. SELECT.EQ.3) GO TO DSPREC
SHNAME,
	SHCSNO = CUSNO
	SHTONO = SHPNO
	CTL = '12,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTONA = ENTRY
	XCALL OUTPT (12,38,0,SHTONA,1)
	GO TO (ANYCNG), CNGCTL
SHADD1,
	CTL = '14,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOAD(1) = ENTRY
	XCALL OUTPT (14,38,0,SHTOAD(1),1)
	GO TO (ANYCNG), CNGCTL
SHADD2,
	CTL = '16,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOAD(2) = ENTRY
	XCALL OUTPT (16,38,0,SHTOAD(2),1)
	GO TO (ANYCNG), CNGCTL
SHADD3,
	CTL = '18,38,30,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOAD(3) = ENTRY
	XCALL OUTPT (18,38,0,SHTOAD(3),1)
	GO TO (ANYCNG), CNGCTL
SHTOTX,
	XCALL OUTPT (20,44,0,'                              ',1)
	CTL = '20,38,03,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	SHTOTC = ENTRY
	CALL GTTXDS
	IF (SRCCTL.EQ.0) GO TO DSPTAX
	MSGCTL = 1
	XCALL MESAG(TXCDNF,MSGCTL)
	GO TO SHTOTX
DSPTAX,
	XCALL OUTPT (20,38,0,SHTOTC,1)
	XCALL OUTPT (20,44,0,ARTDSC,1)
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (ADD,CNGBR,DISPLA), CNGCTL + 1
CNGBR,
	GO TO (SHNAME,SHADD1,SHADD2,SHADD3,SHTOTX), WHATNO-2
	CNGCTL = 3
	GO TO ANYCNG
BADCNG,
	XCALL MESAG ('CAN''T CHANGE KEY FIELD',1)
	GO TO ANYCNG
BADCUS,
	XCALL MESAG ('CUSTOMER # NOT FOUND IN CUSTOMER MASTER FILE',1)
	if (select .eq. 3) goto nocus
	GO TO DISPLA
;**********************************************************************
DSPREC,
	LOKCTL = 1
	XCALL IO (1,SHIPTO,WRKSAV,READ,LOKCTL)
	CKEY = XCUSNO,'XXXXXX'
	BSEND = ORG001
	XCALL SERCH (3,CUSIDX,CKEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	XCALL DSPLY (6,8,38,SHCSNO,1,1)
	if (srcctl.eq.0 .and. irc001.gt.0)
	then	begin
		LOKCTL = 1
		XCALL IO (2,CUSMAS,IRC001,READ,LOKCTL)
		end
	else	name = '** not on file **'

	XCALL OUTPT (8,46,0,NAME,1)
	XCALL DSPLY (4,10,38,SHTONO,1,1)
	XCALL OUTPT (12,38,0,SHTONA,1)
	XCALL OUTPT (14,38,0,SHTOAD(1),1)
	XCALL OUTPT (16,38,0,SHTOAD(2),1)
	XCALL OUTPT (18,38,0,SHTOAD(3),1)
	CALL GTTXDS
	XCALL OUTPT (20,38,0,SHTOTC,1)
	XCALL OUTPT (20,43,0,ARTDSC,1)
	IF (SELECT.EQ.2) GO TO ANYCNG
	GO TO DELETE
ADD,
	IF (SELECT.EQ.2) GO TO CHANGE
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	INCR REC171
	RECCNT = REC171
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,SHIPTO,RECCNT,WRITE,LOKCTL)
	SHIPTO =
	GO TO DISPLA
	
FULL,
	UNLOCK 1
	XCALL OUTPT (23,1,0,'SHIPTO FILE IS NOW FULL. USE EXPAND',1)
	XCALL MESAG ('OPTION IN SYSTEM FUNCTIONS',2)
	GO TO MENU
CHANGE,
	IF (SELECT.EQ.3) GO TO DELETE
	LOKCTL = 1
	XCALL IO (1,SHIPTO,WRKSAV,WRITE,LOKCTL)
	GO TO DISPLA
DELETE,
	XCALL OUTPT (24,1,1,'OKAY TO DELETE  ?',1)
	CTL = '24,20,01,01,YN'
	CALL INPUT
	IF (INXCTL.NE.1) GO TO DISPLA
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	INCR DEL171
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,WRITE,LOKCTL)
	DFL171 = ']]]DEL'
	SHIPTO(17,102) =
	LOKCTL = 1
	XCALL IO (1,SHIPTO,WRKSAV,WRITE,LOKCTL)
	XCALL MESAG ('RECORD DELETED',2)
	IF (DEL171.GE.95) GO TO PURGE
	GO TO DISPLA
PURGE,
	XCALL MESAG ('DELETED RECORDS MUST BE PURGED', 1)
	XCALL WATE(4,V)
	GO TO ENDOFF
;**********************************************************************
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
;********************************************************************************
GTTXDS,
	IF (SHTOTC.NE.'   ') GO TO FINDTX
	SHTOTC = TAXFLG
FINDTX,
	BSEND = ORG169
	SRCCTL = 2
	XCALL SERCH (4,ARTCDE,SHTOTC,1,3,BSEND,BSMID,SRCCTL,1,4,9,0,0,0,0)
	IF (SRCCTL.NE.0) ARTDSC = 'TAX CODE NOT ON FILE'
	RETURN
;********************************************************************************
END
