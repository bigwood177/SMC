;  CLRMSS / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		CLEARS SLSSUM FILE MTD BUCKETS FOR BEGINNING OF A NEW MONTH
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD
	RECCNT	,D5
	ENTRY	,A2
	INXCTL	,D1
	DSPPER	,A2
	LSTPER	,D2
	BLANKS	,A10
	N	,D2
	SWITCH	,D1
	V	,D1
	COL2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SHIFT AND CLEAR MTD SALES SUMMARY FIELDS',V)
	SWITCH = 3
	XCALL FILES(1,'U',58,SWITCH)		;FILE # 58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO END
	SWITCH = 5
	XCALL FILES(1,'U',58,SWITCH)
	IF (SWITCH.EQ.9) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (1,SLSDUM,1,READ,LOKCTL)
	IF (SSPER.EQ.0) GO TO NOPERS
	CALL FNDLST
	XCALL OUTPT (3,10,0,'BEFORE CONTINUING, PLEASE REVIEW THE FOLLOWING:',V)
	XCALL OUTPT (4,15,0,
&	'1. IF YOU WISH TO SAVE A COPY OF THE SALES SUMMARY FILE,',V)
	XCALL OUTPT (5,18,0,'DO SO NOW, BEFORE CONTINUING.',V)
	IF (SSPER.GE.LSTPER)
&		XCALL MESAG ('END OF LAST DEFINED PERIOD, MUST USE YTD SHIFT',1)
	IF (SSPER.GE.LSTPER) GOTO ENDOFF
	DSPPER = SSPER,'ZX'
	XCALL OUTPT (6,15,0,'2. YOU ARE ABOUT TO CLOSE OUT PERIOD ',V)
	XCALL OUTPT (0,0,0,DSPPER,V)
	XCALL OUTPT (0,0,0,' (',V)
	XCALL OUTPT (0,0,0,SSDESC(SSPER),V)
	XCALL OUTPT (0,0,0,')',V)
	DSPPER = (SSPER + 1),'ZX'
	XCALL OUTPT (7,33,0,'AND OPEN UP PERIOD ',V)
	XCALL OUTPT (0,0,0,DSPPER,V)
	XCALL OUTPT (0,0,0,' (',V)
	XCALL OUTPT (0,0,0,SSDESC(SSPER+1),V)
	XCALL OUTPT (0,0,0,').',V)
	XCALL OUTPT (8,18,0,
&	'HAVE YOU PRINTED ALL REPORTS FOR THE PERIOD BEING CLOSED ?',V)
	XCALL OUTPT (10,10,0,'DO YOU WANT TO CONTINUE ?',V)
	XCALL INPUT (10,37,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (ENDOFF), INXCTL-1
	XCALL WATE(4,V)
	RECCNT = 1
;********************************************************************************
;		ADD LAST PERIOD SALES, UNITS, COST TO YTD TOTALS;
;		THIS PERIOD SALES, UNITS, COST BECOME LAST PERIOD
;		SALES, UNITS, COST; THIS PERIOD SALES, UNITS, COST
;		INITIALIZED
READ1,
	INCR RECCNT
	IF (RECCNT.GT.MAX058) GOTO EOF
	LOKCTL = 1
	XCALL IO (1,SLSSUM,RECCNT,READ,LOKCTL)
	IF (SLSSUM(1,6).EQ.']]]]]]') GOTO EOF
	IF (SSPER.EQ.2) SSSYTD(SSPER - 1) = SSLSTS
	IF (SSPER.EQ.2) SSUYTD(SSPER - 1) = SSLSTU
	IF (SSPER.EQ.2) SSCYTD(SSPER - 1) = SSLSTC
	IF (SSPER.GT.2) SSSYTD(SSPER - 1) = SSSYTD(SSPER - 2) + SSLSTS
	IF (SSPER.GT.2) SSUYTD(SSPER - 1) = SSUYTD(SSPER - 2) + SSLSTU
	IF (SSPER.GT.2) SSCYTD(SSPER - 1) = SSCYTD(SSPER - 2) + SSLSTC
	SSLSTS = SSSALE
	SSLSTU = SSUNIT
	SSLSTC = SSCOST
	SSSALE =
	SSUNIT =
	SSCOST =
	LOKCTL = 1
	XCALL IO (1,SLSSUM,RECCNT,WRITE,LOKCTL)
	GO TO READ1
;************************************************************************************
;		INCREMENT PERIOD
EOF,
	SSPER = SSPER + 1
	LOKCTL = 1
	XCALL IO (1,SLSDUM,1,WRITE,LOKCTL)
ENDOFF,
	XCALL FILES(1,'U',58,4)
END,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:SSMENU',1)
FNDLST,
	INCR N
	IF (N.GT.13) GO TO ENDFND
	IF (SSDESC(N).EQ.BLANKS) GO TO ENDFND
	GO TO FNDLST
ENDFND,
	LSTPER = N - 1
	RETURN
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO ENDOFF
	END
