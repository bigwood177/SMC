;  CRMCNC / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PGM TO CANCEL CREDIT MEMOS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CRMHDR		
		.INCLUDE 'DEF:RD046A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD046B.DEF'
RECORD CRMLIN		
		.INCLUDE 'DEF:RD047A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD047B.DEF'
RECORD CUSMAS	
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	OPTION	,D1
	ENTRY	,A7
	INXCTL  ,D1
	DECMAL	,D18
	MASK	,A6,'XXXXXX'
	KINVNO	,D6
	RECNO	,D5
	ORGCRM	,D5
	MAXCRM	,D5
	ORGLIN	,D5
	MAXLIN	,D5
	KEY   	,A6
	BSEND	,D5
	BS001	,D5
	BSMID	,D5
	BSM01	,D5
	SRCCTL	,D1
	BLANKS	,A25
	ALPHA	,A7
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	SWITCH = 1
	XCALL FILES(6,'U',46,SWITCH)		;FILE # 46 -- CRMHDR FILE
	IF (SWITCH.EQ.9) XCALL PGCHN ('CP:CRMENT',1)
	SWITCH = 1
	XCALL FILES(7,'U',47,SWITCH)		;FILE # 47 -- CRMLIN FILE
	IF (SWITCH.EQ.9) GO TO END2
	SWITCH = 1
	XCALL FILES(4,'I',002,SWITCH)		;FILE # 2  -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO END3
	SWITCH = 1
	XCALL FILES(3,'I',001,SWITCH)		;FILES # 1 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO END4
	SWITCH = 5
	XCALL FILES(13,'I',46,SWITCH)
	SWITCH = 5
	XCALL FILES(14,'I',47,SWITCH)
	LOKCTL = 1
	XCALL IO (13,CRMHDR,1,READ,LOKCTL)
	ORGCRM = ORG046
	MAXCRM = MAX046
	LOKCTL = 1
	XCALL IO (14,CRMLIN,1,READ,LOKCTL)
	ORGLIN = ORG047
	MAXLIN = MAX047
	LOKCTL = 1
	XCALL IO (3,CUSCTL,1,READ,LOKCTL)
DISPLA,
	XCALL CR4(V)
	XCALL OUTPT (2,1,0,'CANCEL',1)
CRMNUM,
	CTL = '02,35,06,01,#E'
	CALL INPUT
	GOTO (CRMNUM,END), INXCTL
	KINVNO = ENTRY(1,6)
	IF (KINVNO.EQ.0) GO TO CRMNUM
	KEY = KINVNO,MASK
	BSEND = ORGCRM
SERCH,
	XCALL SERCH (13,CRMHDR,KEY,1,6,BSEND,BSMID,SRCCTL,3,7,12,0,0,0,0)
	GO TO (NOFIND), SRCCTL
CONT,
	BS001 = ORG001
	KEY(1,6) = CCUSNO,'XXXXXX'
	XCALL SERCH (4,CUSIDX,KEY(1,6),1,6,BS001,BSM01,SRCCTL,4,7,11,0,0,0,0)
	GOTO (BADCUS), SRCCTL
	LOKCTL = 1
	XCALL IO (3,CUSMAS,IRC001,READ,LOKCTL)
CONTIN,
	CTL = '03,35'
	DECMAL = CAPLTO
	CALL DSPNUM
	IF (CAPLTO.EQ.-1) XCALL OUTPT (3,35,0,'BAL-FW',V)
	CTL = '04,35'
	DECMAL(1,6) = CCRMDT
	CALL DSPDTE
	CTL = '05,35,06'
	DECMAL = CCUSNO
	CALL DSPNUM
	XCALL OUTPT (5,43,0,NAME,V)
	CTL = '06,35,02'
	DECMAL = CSLMAN
	CALL DSPNUM
	XCALL OUTPT (7,35,0,CLOC,V)
	XCALL OUTPT (8,35,0,CCUSPO,V)
	CTL = '09,35,02'
	DECMAL = CDSCNT
	CALL DSPNUM
	XCALL OUTPT (10,35,0,CCOMNT(1),V)
	XCALL OUTPT (11,35,0,CCOMNT(2),V)
RIGHT1,
	XCALL OUTPT (12,1,1,'OK TO CANCEL ?',V)
	CTL = '12,17,01,01,YN'
	CALL INPUT
	GO TO (PROCES,DISPLA), INXCTL
NOFIND,
	XCALL MESAG('CREDIT MEMO NOT FOUND',1)
	GO TO DISPLA
PROCES,
	CCUSNM = ']]]CANCEL'
	CCRMDT =
WRERR1,
	LOKCTL = 1
	XCALL IO (6,CRMHDR,BSMID,WRITE,LOKCTL)
	BSEND = ORGLIN
	XCALL SERCH (14,CRMLIN,KEY,1,6,BSEND,BSMID,SRCCTL,3,54,57,0,0,0,0)
	GO TO (CANCLD), SRCCTL
	LOKCTL = 1
	XCALL IO (14,CRMLIN,BSMID,READ,LOKCTL)
	RECNO = BSMID - 1
NXTLIN,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (14,CRMLIN,RECNO,READ,LOKCTL)
	IF (CLDESC.EQ.']]]]]]') GO TO CANCLD
	IF (CLCRNO.NE.KINVNO) GO TO OVFLOW
	GO TO ADJUST
OVFLOW,
	IF (RECNO.LT.ORGLIN) RECNO = ORGLIN
	GO TO NXTLIN
CANCLD,
	XCALL MESAG('CREDIT MEMO CANCELLED',2)
	GO TO DISPLA
ADJUST,
	CLDESC = ']]]CANCEL'
	CLQTY = 0
WRERR2,
	LOKCTL = 1
	XCALL IO (7,CRMLIN,RECNO,WRITE,LOKCTL)
	GO TO NXTLIN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
DSPNUM,
	OPTION = 1
	GOTO CALDSP
DSPDTE,
	OPTION = 2
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
BADCUS,
	NAME = 
	GO TO CONTIN
END,
	XCALL WATE(3,V)
	XCALL FILES(3,'I',001,4)
END4,
	XCALL FILES(4,'I',002,4)
END3,
	XCALL FILES(7,'U',47,4)
END2,
	XCALL FILES(6,'I',46,4)
	XCALL PGCHN ('CP:CRMENT',1)
END
