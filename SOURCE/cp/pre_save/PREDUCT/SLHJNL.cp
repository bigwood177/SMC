;  SLHJNL / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;		MODIFIED: 17-JUL-85 MRF 973
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		COMPILE & LINK PER DOCUMENTATION INSTALLATION NOTES.
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD ORDHDR   	
		.INCLUDE 'DEF:RD044A.DEF'
RECORD ORDLIN  		
		.INCLUDE 'DEF:RD045A.DEF'
RECORD,X
		.INCLUDE 'DEF:RD045D.DEF'
RECORD SLSHST		
		.INCLUDE 'DEF:RD055A.DEF'
RECORD HSTCTL		
		.INCLUDE 'DEF:RD055B.DEF'
RECORD CUSMAS		
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL	,X	
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD
		.INCLUDE 'DEF:RD057S.DEF'
RECORD TITLE
		,A21	,'SALES HISTORY JOURNAL'
RECORD HDR1
		,A4
		,A7	,'INVOICE'
		,A3
		,A7	,'INVOICE'
		,A4
		,A5	,'ORDER'
		,A5
		,A4	,'CUST'
		,A3
		,A4	,'CUST'
		,A2
		,A2	,'SL'
		,A26
		,A3	,'PRD'
		,A8
		,A3	,'TAX'
		,A5
		,A3	,'QTY'
		,A8
		,A5	,'SALES'
		,A8
		,A4	,'COST'
		,A9
RECORD HDR2
		,A5
		,A4	,'DATE'
		,A7
		,A2	,'NO'
		,A7
		,A4	,'DATE'
		,A7
		,A2	,'NO'
		,A5
		,A3	,'TYP'
		,A2
		,A2	,'MN'
		,A3
		,A3	,'TER'
		,A4
		,A11	,'ITEM NUMBER'
		,A5
		,A3	,'CAT'
		,A2
		,A3	,'LOC'
		,A3
		,A3	,'FLG'
		,A5
		,A4	,'SOLD'
		,A6
		,A7	,'DOLLARS'
		,A6
		,A6	,'AMOUNT'
		,A8
RECORD SNDMSG
		,A3	,'CP:'
	PRGNAM	,A6	,'PSTSLH'
	RCNT	,D5
	OCNT	,D5
		,D3	,057
		,D3
RECORD NXTMSG
		,A3	,'CP:'
	MSGPRG	,A6	,'CLRLIN'
RECORD DASHES
		,A50,'--------------------------------------------------'
		,A50,'--------------------------------------------------'
		,A32,'--------------------------------'
RECORD
	MASK1	,A8	,'ZX/XX/XX'
	MASK2	,A7	,'ZZZZZX-'
	MASK3	,A7	,'ZZ,ZZX-'
	MASK4	,A11	,'ZZZ,ZZZ.XX-'
	MASK5	,A6	,'ZZZZZX'
	PLINE 	,A132
	V	,D1
	SWITCH	,D1
	CLROE7	,A13	,'057,CP:SLHJNL'
	KEY	,A6
	BSEND	,D5
	BSEND1	,D5
	BSMID	,D5
	SRCCTL	,D1
	PRTCTR	,D1
	FSTSWT	,D1	,1
	LSTSWT	,D1
	INVQTY	,D6
	INVPRC	,D9
	INVCST	,D9
	DATQTY	,D7
	DATPRC	,D10
	DATCST	,D10
	SAVDAT	,D6
	LINCNT	,D2	,60
	LINNUM	,D3
	PGCNT	,D3
	SAVLCT	,D5
	SAVOCT	,D5
	INVALO	,D1
	DECMAL	,D10
	PRNTSW	,D1	,1		;MODIFY TO CONTROL REPORT PRINTING
					;'1' MEANS PRINT THE REPORT
					;MODIFY TO CUSTOMER'S NEED TO PRINT
					;SALES HISTORY JOURNAL
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SPLFIL	,A14
	RPTNUM	,D3
	BLKSIZ	,D6
	RECCNT	,D6
	PRTTYP	,A1
	LPARG	,D1
	LPSW	,D2
	JUST	,D1
PROC
	XCALL TERID (V)
	ORG055 = 1
	REC055 = 1
	XCALL OUTPT (1,1,2,TITLE,V)
	XCALL WATE (4,V)
	SWITCH = 5
	XCALL FILES (10,'I',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	LOKCTL = 1
	XCALL IO (10,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	XCALL FILES (10,'I',41,4)		;PREVIOUSLY INCREMENTED
	SWITCH = 5
	XCALL FILES (2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
	SWITCH = 3
	XCALL FILES (7,'O',57,SWITCH)		;FILE # 57 -- SLHWRK FILE
	IF (SWITCH.EQ.9) GOTO INUSE1
	SWITCH = 5
	XCALL FILES (5,'SI',45,SWITCH)		;FILE # 45 -- ORDLIN FILE
	RECCNT = 0
CNTREC,
	LOKCTL = 1
	XCALL IOS (5,ORDLIN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO OPEN57
	IF (LFLAG.EQ.2) INCR RECCNT
	GO TO CNTREC
OPEN57,
	BLKSIZ = (((RECCNT+2)*(SIZ057+2))/512) + 1
	XCALL OFILE (7,057,BLKSIZ,SIZ057,SWITCH)
	IF (SWITCH.EQ.1) GO TO NOROOM
	SWITCH = 5
	XCALL FILES (1,'I',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	SWITCH = 5
	XCALL FILES (4,'SI',44,SWITCH)		;FILE # 44 -- ORDHDR FILE
	LOKCTL = 1
	XCALL IO (1,CUSCTL,1,READ,LOKCTL)
	BSEND = ORG001
	LOKCTL = 1
	XCALL IOS (7,HSTCTL,WRITE,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO NOROOM
	IF (PRNTSW.EQ.0) GO TO REDLIN
	SPLFIL (5,6) = 'EC'
	LPSW = 4	; MAY BE AUTO-SPOOLED
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) PRNTSW = 0
	IF (LPSW.EQ.0) XCALL MESAG ('POSTING CONTINUING',2)
	LPARG = 2
	IF (LPSW.EQ.2.OR.PRNTSW.EQ.0) LPARG = 4
	XCALL WATE (LPARG,V)
	LORDNO =
	LINSEQ =
	LOKCTL = 1
	XCALL ISIO (5,ORDLIN,ORDKEY,READ,LOKCTL)
	IF (LOKCTL.EQ.3) GO TO EOFLIN
	GO TO REDLN1
REDLIN,
	INVALO =
	LOKCTL = 1
	XCALL IOS (5,ORDLIN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFLIN
REDLN1,
	IF (LFLAG.NE.2) GOTO REDLIN
	IF (LITMNO.EQ.'???'.AND.LQTYOR.EQ.0) GOTO REDLIN
	IF (LQTYSH.EQ.0) GOTO REDLIN
	GO TO CHKORD
EOFLIN,
	LSTSWT = 1
;*************************************************************************************
;		CREATE SALES HISTORY RECORD FOR EACH INVOICED LINE ITEM
CHKORD,
	IF (LORDNO.NE.OORDNO.OR.FSTSWT.OR.LSTSWT) CALL NEWORD
	FSTSWT =
	IF (LSTSWT) GOTO CLOSE
	IF (INVALO) GOTO REDLIN
	INCR LINNUM
	HBOFLG =
	HTXFLG = LTXFLG
	HLOC = LLOC
	HITMNO = LITMNO
	HPRDCD = LPRDCD
	HPRDC2 =
	HQTY = LQTYSH
	DECMAL = (LQTYSH*LPRICE) - ((LDISC*LQTYSH*LPRICE)/100)
	HSALE = DECMAL - ((DECMAL*LODISC)/100)
	HCOST = LQTYSH * LCOST
	INCR REC055
	LOKCTL = 1
	XCALL IOS (7,SLSHST,WRITE,LOKCTL)	;WRITES RECORDS TO SLSHST FILE
	IF (LOKCTL.EQ.3) GO TO NOROOM
;*************************************************************************************
;		PRINT SALES HISTORY JOURNAL
	IF (LINNUM.GT.1) GO TO SKIP1
	PLINE(4,11) = HINVDT,MASK1
	PLINE(15,20) = HINVNO,MASK5
	PLINE(24,31) = HORDDT,MASK1
	PLINE(35,40) = HCUSNO,MASK5
	PLINE(44,45) = HCUSCD
	PLINE(49,50) = HSLSMN
	PLINE(54,55) = HTERR
SKIP1,
	PLINE(59,73) = HITMNO
	IF (JUST) XCALL LEFTJ (PLINE(59,73),15)
	PLINE(77,78) = HPRDCD
	PLINE(82,83) = HLOC
	PLINE (89,89) = 'N'
	IF (HTXFLG.EQ.'1') PLINE (89,89) = 'Y'
	PLINE(95,101) = HQTY,MASK3
	PLINE(104,114) = HSALE,MASK4
	PLINE(117,127) = HCOST,MASK4
	CALL PRINT
	INVQTY = INVQTY + HQTY
	INVPRC = INVPRC + HSALE
	INVCST = INVCST + HCOST
	GOTO REDLIN
;***************************************************************************************
;		PRINT INVOICE TOTALS
NEWORD,
	IF (FSTSWT) GOTO REDORD
	IF (PRNTSW.NE.0) XCALL LINFD (1)
	PLINE(75,89) = 'INVOICE TOTALS:'
	PLINE(94,101) = INVQTY,'ZZZ,ZZX-'
	PLINE(102,114) = INVPRC,'Z,ZZZ,ZZZ.XX-'
	PLINE(115,127) = INVCST,'Z,ZZZ,ZZZ.XX-'
	CALL PRINT
	PLINE = DASHES
	CALL PRINT
	IF (PRNTSW.NE.0) XCALL LINFD (1)
	LINCNT = LINCNT + 2
	DATQTY = DATQTY + INVQTY
	DATPRC = DATPRC + INVPRC
	DATCST = DATCST + INVCST
	INVQTY =
	INVPRC =
	INVCST =
	IF (LSTSWT) GOTO DATTOT
;*************************************************************************************
;		READ ORDER HEADER FILE
REDORD,
	LINNUM =
	LOKCTL = 1
	XCALL IOS (4,ORDHDR,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO FNDORD
	IF (OORDNO.LT.LORDNO) GOTO REDORD
	IF (OORDNO.GT.LORDNO) GOTO FNDORD
CHKDAT,
	IF (FSTSWT) SAVDAT = OINVDT
	IF (OINVDT.EQ.SAVDAT) GOTO GETCUS
;**************************************************************************************
;		PRINT DATE TOTALS
DATTOT,
;	PLINE(78,89) = 'DATE TOTALS:'
;	PLINE(93,101) = DATQTY,'Z,ZZZ,ZZX-'
;	PLINE(102,114) = DATPRC,'ZZZZZ,ZZZ.XX-'
;	PLINE(115,127) = DATCST,'ZZZZZ,ZZZ.XX-'
;	CALL PRINT
;	LINCNT = 60
	DATQTY =
	DATPRC =
	DATCST =
	IF (LSTSWT) RETURN
	SAVDAT = OINVDT
;************************************************************************************
;		SEARCH FOR ORDER HEADER RECORD
GETCUS,
	HCUSNO = OCUSNO
	HINVDT = OINVDT
	HINVNO = OINVNO
	HORDDT = OORDDT
	HSLSMN = OSLMAN
	KEY = OCUSNO,'XXXXXX'
	XCALL SERCH (2,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL) GOTO NTFOND
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	HCUSCD = CUSCD
	HCUSC2 = CUSCD2
	HTERR = TERR
;***********************************************************************************
NTFOND,
	IF (SRCCTL) HCUSCD =
	IF (SRCCTL) HCUSC2 =
	IF (SRCCTL) HTERR =
	RETURN
FNDORD,
	OORDNO = LORDNO
	LOKCTL = 1
	XCALL ISIO (4,ORDHDR,OORDNO,READ,LOKCTL)
	IF (LOKCTL) GO TO NOTFND
	GO TO CHKDAT
NOTFND,
	IF (PRNTSW.NE.0) XCALL LINFD (0)
	PLINE(1,42) = '*** NO ORDER HEADER RECORD FOUND FOR ORDER'
	PLINE(44,49) = LORDNO,'XXXXXX'
	PLINE(51,91) = '- LINE ITEM NOT ADDED TO HISTORY FILE ***'
	CALL PRINT
	IF (PRNTSW.NE.0) XCALL LINFD (0)
	LINCNT = LINCNT + 2
	INVALO = 1
	RETURN
PRINT,
	IF (PRNTSW.EQ.0) RETURN
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		'NO LEGEND',' ',' ',0,0,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN
INUSE2,
	CLOSE 5
INUSE1,
	SWITCH = 2
	XCALL SNMSG (CLROE7,SWITCH)
	XCALL FILES (2,'I',02,4)
	XCALL PGCHN ('UT:CLROF2',2)
NOROOM,
	XCALL MESAG
&	('CANNOT RUN SELECTION: NEED MORE ROOM ON DEVICE FOR SLHWRK FILE',2)
	GO TO INUSE2
;*************************************************************************************
;		CLOSE FILES
CLOSE,
	CLOSE 7
	ORG055 = REC055
	SWITCH = 5
	XCALL FILES (7,'U',57,SWITCH)
	LOKCTL = 1
	XCALL IO (7,HSTCTL,1,WRITE,LOKCTL)
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	CLOSE 2
	CLOSE 1
	CLOSE 4
	CLOSE 5
	CLOSE 7
	RCNT = REC055
	OCNT = 1
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (NXTMSG,SWITCH)
	XCALL PGCHN ('CP:SRTSLH',0)
END
