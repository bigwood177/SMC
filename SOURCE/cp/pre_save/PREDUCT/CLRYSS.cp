;  CLRYSS / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;		MODIFIED: 25-JUN-85 MRF 00968
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		RESETS SLSSUM FILE FOR BEGINNING OF A NEW YEAR
;		AND MARKS RECORDS WITH NO HISTORY FOR DELETION
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD
	KEY	,A21
	BSMID	,D5
	BSEND	,D5
	SRCCTL	,D1
	N	,D2
	M	,D2
	BLANKS	,A10
	RECCNT	,D5
	ENTRY	,A2
	INXCTL	,D1
	SWITCH	,D1
	PER	,D2
	V	,D1
	DSPYR	,A4
	DSPPER	,A2
	SAVESW	,D1
	LSTPER	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SHIFT AND CLEAR YTD SALES SUMMARY FIELDS',V)
	SWITCH = 3
	XCALL FILES(2,'U',59,SWITCH)		;FILE # 59 -- SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO END
	SWITCH = 5
	XCALL FILES(2,'U',59,SWITCH)
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 3
	XCALL FILES(1,'U',58,SWITCH)		;FILE # 58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 5
	XCALL FILES(1,'U',58,SWITCH)
	IF (SWITCH.EQ.9) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (1,SLSDUM,1,READ,LOKCTL)
	IF (SSPER.EQ.0) GO TO NOPERS
	CALL FNDLST
	XCALL OUTPT (3,10,0,
&	'BEFORE CONTINUING, PLEASE REVIEW THE FOLLOWING:',V)
	XCALL OUTPT (4,15,0,
&	'1. IF YOU WISH TO SAVE A COPY OF THE SALES SUMMARY FILE,',V)
	XCALL OUTPT (5,18,0,'DO SO NOW, BEFORE CONTINUING.',V)
	DSPYR = SSYEAR,'XXXX'
	DSPPER = SSPER,'ZX'
	XCALL OUTPT (6,15,0,'2. YOU ARE ABOUT TO CLOSE OUT PERIOD ',V)
	XCALL OUTPT (0,0,0,DSPPER,V)
	XCALL OUTPT (0,0,0,' (',V)
	XCALL OUTPT (0,0,0,SSDESC(SSPER),V)
	XCALL OUTPT (0,0,0,')  ',V)
	XCALL OUTPT (0,0,0,DSPYR,V)
	DSPYR = (SSYEAR + 1),'XXXX'
	DSPPER = ' 1'
	XCALL OUTPT (7,33,0,'AND OPEN UP PERIOD ',V)
	XCALL OUTPT (0,0,0,DSPPER,V)
	XCALL OUTPT (0,0,0,' (',V)
	XCALL OUTPT (0,0,0,SSDESC(1),V)
	XCALL OUTPT (0,0,0,')  ',V)
	XCALL OUTPT (0,0,0,DSPYR,V)
	XCALL OUTPT (0,0,0,'.',V)
	XCALL OUTPT (8,18,0,
&	'HAVE YOU PRINTED ALL REPORTS FOR THE PERIOD BEING CLOSED ?',V)
	IF (SSPER.LT.LSTPER) GO TO NOTLST
	XCALL OUTPT (10,10,0,'DO YOU WANT TO CONTINUE ?',V)
	XCALL INPUT (10,37,1,1,'YN',ENTRY,INXCTL,V)
	GO TO (ENDOFF), INXCTL-1
	XCALL WATE(4,V)
	BSEND = ORG058
	RECCNT = 1
	N = LSTPER
;***********************************************************************************
;		UPDATE YTD SALES FOR PERIODS 11 & 12 AND RESET YTD SALES
;		FOR NEW PERIOD 1
READ1,
	INCR RECCNT
	IF (RECCNT.GT.MAX058) GOTO EOF
	LOKCTL = 1
	XCALL IO (1,SLSSUM,RECCNT,READ,LOKCTL)
	IF (SLSSUM(1,6).EQ.']]]]]]') GOTO EOF
	SSSYTD (N) = SSSALE + SSLSTS + SSSYTD( N - 2 )
	SSUYTD (N) = SSUNIT + SSLSTU + SSUYTD( N - 2 )
	SSCYTD (N) = SSCOST + SSLSTC + SSCYTD( N - 2)
	SSSYTD ( N - 1) = SSLSTS + SSSYTD ( N - 2 )
	SSUYTD ( N - 1) = SSLSTU + SSUYTD ( N - 2 )
	SSCYTD ( N - 1) = SSLSTC + SSCYTD ( N - 2 )
	SSSALE =
	SSUNIT =
	SSCOST =
	SSLSTS =
	SSLSTU =
	SSLSTC =
	PER = 1
	SAVESW = 0
;****************************************
;		CLEAR SLSSUM RECORDS WITH NO SALES FOR YEAR
TEST,
	IF (SSSYTD(PER).NE.0) SAVESW = 1
	IF (SSUYTD(PER).NE.0) SAVESW = 1
	IF (SSCYTD(PER).NE.0) SAVESW = 1
	INCR PER
	IF (PER .LE. N) GO TO TEST
	IF (SAVESW.EQ.1) GO TO REWRIT
DELETE,
	KEY(1,6) = SSCUS, 'XXXXXX'
	KEY(7,21) = SSPROD
	SSPROD(1,6) = ']]]DEL'
	XCALL SERCH (2,SLSIDX,KEY,1,21,BSEND,BSMID,SRCCTL,4,38,42,0,0,0,0)
	IF (SRCCTL) GO TO REWRIT
	LOKCTL = 1
	XCALL IO (2,SLSIDX,BSMID,READ,LOKCTL)
	IRC058 =
	LOKCTL = 1
	XCALL IO (2,SLSIDX,BSMID,WRITE,LOKCTL)
REWRIT,
	LOKCTL = 1
	XCALL IO (1,SLSSUM,RECCNT,WRITE,LOKCTL)
	GOTO READ1
;************************************************************************************
EOF,
	LOKCTL = 1
	XCALL IO(1,SLSDUM,1,READ,LOKCTL)
	SSYEAR = SSYEAR + 1
	SSPER = 1
	LOKCTL = 1
	XCALL IO (1,SLSDUM,1,WRITE,LOKCTL)
	CLOSE 1		;CLOSE SLSSUM BUT LEAVE PROTECTED FOR REORG
	CLOSE 2		;CLOSE SLSIDX BUT LEAVE PROTECTED FOR REORG
	XCALL PGCHN ('CP:ORGSLS',1)
ENDOFF,
	XCALL FILES (1,'U',58,4)
END1,
	XCALL FILES (2,'U',59,4)
END,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:SSMENU',1)
FNDLST,
	INCR M
	IF (M.GT.13) GO TO ENDFND
	IF (SSDESC(M).EQ.BLANKS) GO TO ENDFND
	GO TO FNDLST
ENDFND,
	LSTPER = M - 1
	RETURN
NOTLST,
	XCALL MESAG('CURRENT PERIOD EARLIER THAN LAST PERIOD - CLEAR MTD FIRST',1)
	GO TO ENDOFF
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO ENDOFF
	END
