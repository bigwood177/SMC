;  PIKTIK / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;		MODIFIED: JUNE 4, 1985 EAV-889
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PGM TO PRINT PICKING TICKETS
;		WITH BILL OF MATERIAL CAPABILITY
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD ORDHDR
		.INCLUDE 'DEF:RD044A.DEF'
RECORD ORDLIN
		.INCLUDE 'DEF:RD045A.DEF'
RECORD,X
		.INCLUDE 'DEF:RD045D.DEF'
RECORD LINIDX
		.INCLUDE 'DEF:RD048A.DEF'
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL,X
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD PRDSTR
		.INCLUDE 'DEF:RD091A.DEF'
RECORD	,X
		.INCLUDE 'DEF:RD091B.DEF'
RECORD ITMMAS
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV, X
		.INCLUDE 'DEF:RD041B.DEF'
RECORD SHPVIA
		.INCLUDE 'DEF:RD172A.DEF'
RECORD SHPCTL
		.INCLUDE 'DEF:RD172B.DEF'
RECORD PBUF
	PLINE	,A85
RECORD SAVITM		;USED TO SAVE COUNTERS FOR SUBROUTINE FNDPS
	ITEMNM	,12A15
	ITEMN	,12D5
	QTYS	,12D7
	LEVEL	,D3
	BADBIL	,D1
RECORD SNDMSG
		,A3,	'CP:'
	PRGRAM	,A6,	'LINIDX'
RECORD
	LINCNT	,D2,	00
	SPLFIL	,A14
	DSPDAT	,D6
	PGCNT	,D3
	X	,D2
	PAGENO	,D2
	MASK	,A10,	'ZZZ,ZZZ.XX'
	MASK1	,A4,	'ZZZX'
	MASK2	,A6,	'XXXXXX'
	DATMSK	,A8,	'ZX/XX/XX'
	SOLDTO	,4A29
	SHIPTO	,4A29
	KEY1	,A6
	KEY   	,A15
	BSND1	,D5
	BSND2	,D5
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	RECRD	,D5
	RECNO	,D5
	LRECNO	,D5,	00001
	RPTNUM	,D3
	PRTTYP	,A1
	STRTNA	,A6
	ENDNA	,A6
	STRTNO	,D6
	ENDNO	,D6
	STXCTL	,D1
	BLANKS	,A25
	SWITCH	,D1
	TODAY	,D6
	PIKQTY	,D14
	PLEVEL	,A4,	'(  )'
	ANYMOR	,D1
	LOCTNS	,D5
	LPONSW	,D1
	SYSTEM	,D1
	MSG	,A50
	V	,D1
	DATWRK	,D6
	READ	,D1,	0
	WRITE	,D1,	1
	LOKCTL	,D1
	LPSW	,D2
	JUST	,D1
	COMPIK	,A3
	LOCATN	,A2
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,0,'PRINT PICKING TICKETS',V)
	XCALL OUTPT (2,1,1,'\',1)
	SWITCH = 1
	XCALL SNMSG (LOCATN,SWITCH)
	IF (SWITCH.EQ.9) GO TO CONT
	SWITCH = 3
	XCALL SNMSG (' ',SWITCH)
CONT,
	XCALL RDATE(TODAY)
	SWITCH = 5
	XCALL FILES(1,'I',1,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO END0
	LOKCTL = 1
	XCALL IO (1,CUSMAS,1,READ,LOKCTL)
	BSND1 = ORG001
BEGIN,
	XCALL WATE(3,V)
	LINIDX =
	SWITCH = 1
	XCALL FILES(2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO END0
	SWITCH = 1
	XCALL FILES (4,'SI',44,SWITCH)		;FILE # 44 -- ORDHDR FILE
	IF (SWITCH.EQ.9) GO TO END1
	SWITCH = 1
	XCALL FILES (5,'SI',45,SWITCH)		;FILE # 45 -- ORDLIN FILE
	IF (SWITCH.EQ.9) GO TO END2
	SWITCH = 1
	XCALL FILES (7,'I',41,SWITCH)		;FILE # 41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO END4
	SWITCH = 1
	XCALL FILES (9,'I',172,SWITCH)		;FILE # 172 -- SHPVIA
	IF (SWITCH.EQ.9) GO TO END5
	LOKCTL = 1
	XCALL IO (7,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	LOCTNS = NUMLOC
	SYSTEM = TYPSYS
	IF (SYSTEM.LT.3) GO TO NOPRST	;IF B/M CAPABILITY IS NOT NEEDED
	XCALL FILES (12,'I',91,SWITCH)		;FILE # 91 -- PRDSTR FILE
	IF (SWITCH.EQ.9) GO TO END6
	LOKCTL = 1
	XCALL IO (12,PRDSTR,1,READ,LOKCTL)
	BSEND = ORG091
NOPRST,
	LOKCTL = 1
	XCALL IO (9,SHPCTL,1,READ,LOKCTL)
	XCALL FILES (8,'I',48,5)		;FILE # 48 -- LINIDX FILE
	STXCTL = 1
	XCALL STENO (STRTNA,ENDNA,'ORDER #',6,STXCTL,V)
	GO TO (END3), STXCTL
	STRTNO = STRTNA
	IF (ENDNA.EQ.'[[[') ENDNO = 999999
	IF (ENDNA.NE.'[[[') ENDNO = ENDNA
FIRSTI,
	LOKCTL = 1
	XCALL IOS (8,LINIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO END3
	IF (LINIDX.EQ.']]]]]]') GO TO END3
	IF (LXORDN.LT.STRTNO) GO TO FIRSTI
	IF (LXORDN.GT.ENDNO) GO TO ENDHD1
RDHDR,
	LOKCTL = 1
	XCALL IOS (4,ORDHDR,READ,LOKCTL)
	IF (OORDNO.LT.LXORDN) GO TO RDHDR
	IF (OORDNO.LT.STRTNO) GO TO RDHDR
	IF (OORDNO.GT.ENDNO) GO TO ENDHD1
	SHIPTO (1) = OSHPNM
	SHIPTO (2) = OSHAD1
	SHIPTO (3) = OSHAD2
	SHIPTO (4) = OSHAD3
	KEY1 = OCUSNO, MASK2
	XCALL SERCH (2,CUSIDX,KEY1,1,6,BSND1,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL.EQ.1) CALL NOCUST
	LOKCTL = 1
	IF (SRCCTL.EQ.0) XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	SOLDTO (1) = OCUSNM
	SOLDTO (2) = ADD1
	SOLDTO (3) = ADD2
	SOLDTO (88,102) = CITY
	SOLDTO (104,105) = STATE
	SOLDTO (107,116) = ZIP
	IF (OCUSNO.NE.999999) GO TO PRNTHD
	OCUSNM = SHIPTO(1)
	SOLDTO(1) = SHIPTO(1)
	SOLDTO(2) = SHIPTO(2)
	SOLDTO(3) = SHIPTO(3)
	SOLDTO(4) = SHIPTO(4)
	SHIPTO(1,116) =
PRNTHD,
	PAGENO = 1
	CALL PRTHDR
	LINCNT =
ITEMS,
	IF (LXORDN.EQ.OORDNO) CALL PRTLIN
	IF (LXORDN.GT.OORDNO) GO TO ENDTIK
	LOKCTL = 1
	XCALL IOS (8,LINIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDTIK
	IF (LINIDX.EQ.']]]]]]') GO TO ENDTIK
	GO TO ITEMS
PRTLIN,
	LORDNO = LXORDN
	LINSEQ = LXLSEQ
	LOKCTL = 1
	XCALL ISIO (5,ORDLIN,ORDKEY,READ,LOKCTL)
	IF (LOKCTL) RETURN
	IF (LQTYOR.EQ.0.AND.(LQTYBO.EQ.0.OR.LSTATS.EQ.2)) RETURN
	IF (LINCNT.GE.30) CALL NXTPAG
	IF (SYSTEM.LT.3) GO TO NOSTR	;IF B/M CAPABILITY IS NOT NEEDED.
	IF (LSTOKT.EQ.'S') GO TO NOSTR
	KEY = LITMNO
	SRCCTL = 2
	XCALL SERCH(12,PRDSTR,KEY,1,15,BSEND,BSMID,SRCCTL,4,18,23,0,0,0,0)
	IF (SRCCTL) GO TO NOGOOD
	CALL NOSTR			;PRINT THE ITEM ORDERED FIRST
	SAVITM =
	ANYMOR = 0
	RECNO = BSMID - 1		;WILL INCR RECNO FIRST THING IN FNDPS
ANYMOR,
	XCALL FNDPS (ITMMAS,ANYMOR,LITMNO,RECNO,LQTYOR,LOCTNS,
&		SAVITM,PIKQTY,BSEND,COMPIK,LOCATN)
	IF (BADBIL.EQ.1) GO TO BILERR
	IF (ANYMOR.EQ.1) RETURN
	IF (LINCNT.GE.30) CALL NXTPAG
	PLINE (21,35) = ITEMNO
	IF (JUST) XCALL LEFTJ (PLINE(21,35),15)
	PLEVEL(2,3) = LEVEL,'XX'
	PLINE (45,48) = PLEVEL
	PLINE (53,54) = SUOFM
	PLINE (57,74) = PIKQTY,'ZZZZ,ZZZ,ZZZ.XXXX-'
	XCALL LEFTJ (PLINE(57,74),18)
	PLINE (78,80) = COMPIK
	CALL PRINT
	PLINE (21,50) = DESCR
	CALL PRINT
	GO TO ANYMOR
NOSTR,
	PLINE (3,6) = LQTYOR, MASK1
	IF (LSTATS.NE.2) PLINE (15,18) = LQTYBO, 'ZZZZ'
	PLINE (21,35) = LITMNO
	IF (JUST) XCALL LEFTJ (PLINE(21,35),15)
	PLINE (53,54) = LUOFM
	PLINE (78,80) = LXPIKS
	CALL PRINT
	PLINE (21,50) = LDESCR
	IF((LEXSDT.EQ.OORDDT.AND.LPRMDT.EQ.0).OR.LPRMDT.EQ.OORDDT) GO TO PRLIN
	DATWRK = LPRMDT
	IF (LPRMDT.EQ.0) DATWRK = LEXSDT
	DSPDAT (5,6) = DATWRK (1,2)
	DSPDAT (1,4) = DATWRK (3,6)
	IF (DSPDAT.EQ.OORDDT) GO TO PRLIN
	PLINE (57,57) = '('
	PLINE (58,65) = DSPDAT,'ZX/XX/XX'
	PLINE (66,66) = ')'
PRLIN,
	CALL PRINT
	RETURN
PRTHDR,
	PLINE =
	CALL PRINT
	XCALL LINFD (1)
	PLINE (81,82) = PAGENO
	CALL PRINT
	XCALL LINFD (2)
	PLINE (74,81) = TODAY, DATMSK
	CALL PRINT
	XCALL LINFD (8)
	IF (SOLDTO(2).NE.BLANKS) GO TO BUFSLD
	SOLDTO (2) = SOLDTO (3)
	SOLDTO (3) =
BUFSLD,
	IF (SOLDTO (3).NE.BLANKS) GO TO BUFSL2
	SOLDTO (3) = SOLDTO (4)
	SOLDTO (4) =
BUFSL2,
	IF (SHIPTO(2).NE.BLANKS) GO TO BUFSHP
	SHIPTO (2) = SHIPTO (3)
	SHIPTO (3) =
BUFSHP,
	IF (SHIPTO (3).NE.BLANKS) GO TO BUFSH2
	SHIPTO (3) = SHIPTO (4)
	SHIPTO (4) =
BUFSH2,
	PLINE (13,41) = SOLDTO(1)
	PLINE (51,79) = SHIPTO(1)
	CALL PRINT
	PLINE (13,41) = SOLDTO(2)
	PLINE (51,79) = SHIPTO(2)
	CALL PRINT
	PLINE (13,41) = SOLDTO(3)
	PLINE (51,79) = SHIPTO(3)
	CALL PRINT
	PLINE (13,41) = SOLDTO(4)
	PLINE (51,79) = SHIPTO(4)
	CALL PRINT
	XCALL LINFD (4)
	PLINE (3,8) = OORDNO
	PLINE (11,18) = OORDDT, DATMSK
	PLINE (21,26) = OCUSNO
	PLINE (29,30) = OLOC
	PLINE (33,34) = OSLMAN
	PLINE (37,46) = OPONO
	PLINE (49,58) = OJOBNO
	SRCCTL = 2
	BSND2 = ORG172
;------ COP33 -------------------------------------------------------------
;	XCALL SERCH (9,SHPVIA,OSHVIA,1,1,BSND2,BSMID,SRCCTL,1,2,7,0,0,0,0)
	XCALL SERCH (9,SHPVIA,OSHVIA,1,1,BSND2,BSMID,SRCCTL,4,2,7,0,0,0,0)
;---------------------------------------------------------------------------
	GO TO (SHVNFD), SRCCTL
	PLINE (61,75) = SHPVDS
	GO TO SHVOK
SHVNFD,
	PLINE (61,75) = 'CODE NOT FOUND '
SHVOK,
	IF (OCLPPD.EQ.'C') PLINE (78,80) = 'COL'
	IF (OCLPPD.EQ.'P') PLINE (78,80) = 'PPD'
	CALL PRINT
PIKTIC,
	PLINE (56,68) = 'COMPONENT QTY'
	PLINE (73,84) = 'BIN LOCATION'
	CALL PRINT
	PLINE (57,65) = 'XXXXXXXXX'
	PLINE (69,70) = 'XX'
	PLINE (76,81) = 'XXXXXX'
	CALL PRINT
	XCALL LINFD (1)
	RETURN
NXTPAG,
	INCR PAGENO
	XCALL LINFD (1)
	PLINE (72,84) = '**CONTINUED**'
	CALL PRINT
	PLINE (21,55) = OCOMNT(1)
	CALL PRINT
	PLINE (21,55) = OCOMNT(2)
	CALL PRINT
	XCALL LINFD (0)
	CALL PRTHDR
	LINCNT =
	RETURN
NOCUST,
	CUSMAS =
	RETURN
NOGOOD,
	MSG(1,13) = 'NON-STKD ITEM'
	MSG(15,29) = LITMNO
	MSG(31,44) = 'HAS NO PRD STR'
	XCALL MESAG(MSG,1)
BILERR,
	XCALL MESAG('BILL MUST BE CORRECTED',1)
	RETURN
;*************************************************
PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	XCALL LPOUT (LINCNT,PGCNT,PBUF,'NO TITLE','NO HDR',' ',' ',
&	'NO LEGEND',' ',' ',0,85,85,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	LPSW = 5		;SPECIAL FORMS (PICKING TICKETS)
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END3
	XCALL WATE (2,V)
	LPONSW = 1
	RETURN
;*************************************************
ENDTIK,
	X =  31 - LINCNT
	XCALL LINFD (X)
	CALL PRINT
	PLINE (21,55) = OCOMNT(1)
	CALL PRINT
	PLINE (21,55) = OCOMNT(2)
	CALL PRINT
	XCALL LINFD (0)
	IF (LINIDX.NE.']]]]]]') GO TO RDHDR
ENDHDR,
	IF (ENDNA.EQ.'[[[ ') GO TO END3
ENDHD1,
	IF (LPONSW.EQ.0) XCALL MESAG ('NO ORDERS MEET THESE PARAMETERS',2)
	CLOSE 8
	IF (SYSTEM.GE.3) XCALL FILES (12,'I',91,4)
	XCALL FILES (9,'I',172,4)
	XCALL FILES (7,'I',41,4)
	XCALL FILES (5,'U',45,4)
	XCALL FILES (4,'U',44,4)
	XCALL FILES (2,'I',02,4)
	IF (LPONSW) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	LPONSW = 0
	GO TO BEGIN
END3,
	XCALL WATE(3,V)
	XCALL FILES (8,'O',48,7)	;DELETE FILE
	IF (SYSTEM.GE.3) XCALL FILES (12,'I',91,4)
END6,
	XCALL FILES (9,'I',172,4)
END5,
	XCALL FILES (7,'I',41,4)
END4,
	XCALL FILES (5,'SI',45,4)
END2,
	XCALL FILES (4,'SI',44,4)
END1,
	XCALL FILES (2,'I',02,4)
END0,
	CLOSE 1
	IF (LPONSW) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	XCALL MOUNT ('REGULAR PAPER','PRINTER')
	SWITCH = 4
	XCALL SNMSG (SNDMSG,SWITCH)
	XCALL PGCHN ('CP:CPMENU',1)
END
