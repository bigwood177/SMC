SUBROUTINE CMSC1
;
;  CMSC1 / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		HANDLES FIRST SCREEN OF ADD MODE IN CRMEMO
;
	CRMHDR	,A
	PMAX	,D
	INXCTL	,D
	TXFLG	,A
	LLSTIN	,D
	CUSTP2	,A
	LINCNT	,D
	LSTDTE	,D
	FULL	,D
	ADCNT1	,D
	PCODES	,A
	PDISCS	,D
	SHPCTL	,A
	V	,D
RECORD CUSMAS  		
		.INCLUDE 'DEF:RD001A.DEF'
RECORD ,X		
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX  		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD TCRMHD		
		.INCLUDE 'DEF:RD046A.DEF'
RECORD DUMHDR		
		.INCLUDE 'DEF:RD046B.DEF'
RECORD COPCTL
		.INCLUDE 'DEF:RD060A.DEF'
RECORD DUMSHP
		.INCLUDE 'DEF:RD171B.DEF'
RECORD SHIPTO
		.INCLUDE 'DEF:RD171A.DEF'
RECORD TMPAD3
	TMPCTY	,A15
		,A1
	TMPSTA	,A2
		,A2
	TMPZIP	,A10
RECORD SKEY
	XCUSNO	,D6
	SHPNO	,D4
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	SAVBAL	,A1
	PCTR	,D2
	L	,2D1
	OPTION	,D1
	ENTRY	,A36
	CNGCTL	,D1
	WHATNO	,D2
	MEMO	,A6
	DECMAL	,D18
	ORGCUS	,D5
	KEY   	,A10
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	BLANKS	,A35
	DOTS	,A35,'...................................'
	ALPHA	,A8
	ABORT	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	DUMSHP = SHPCTL
	ABORT =
	INXCTL =
	LOKCTL = 1
	XCALL IO (3,CUSMAS,1,READ,LOKCTL)
	ORGCUS = ORG001
	PMAX = NUMDSC
	LOKCTL = 1
	XCALL IO (6,DUMHDR,1,READ,LOKCTL)
	IF (REC046.GE.MAX046) GO TO FULL
	UNLOCK 6
	LOKCTL = 1
	XCALL IO (8,COPCTL,1,READ,LOKCTL)
DISPLA,
	TCRMHD =
	IF (INXCTL.EQ.1) ABORT = 1
	CNGCTL =
	XCALL CR1 (DSTFLG,V)
APLTO,
	IF (CNGCTL.AND.SAVBAL.EQ.'B') GO TO ANYCNG
	CTL = '02,46,06,00,#E'
	CALL INPUT
	IF (INXCTL.EQ.1.AND.CNGCTL) GO TO DISPLA
	GO TO (APLTO,PROCES), INXCTL
	CAPLTO = ENTRY (1,6)
	GO TO (ANYCNG), CNGCTL
	IF (ABORT) GO TO SAMNUM
	LOKCTL = 1
	XCALL IO (8,COPCTL,1,READ,LOKCTL)
	LSTCRM = LSTCRM + 1
	LLSTIN = LSTCRM
	LOKCTL = 1
	XCALL IO (8,COPCTL,1,WRITE,LOKCTL)
SAMNUM,
	MEMO = LLSTIN,'XXXXXX'
	XCALL OUTPT (2,19,0,MEMO,V)
	CCRMNO = LLSTIN
	IF (ABORT) GO TO DATE
DATE,
	ABORT =
	CTL = '02,66,06,00,D '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CCRMDT = ENTRY
	IF (ENTRY.EQ.BLANKS) CCRMDT = LSTDTE
	IF (CCRMDT.EQ.0) GO TO DATE
	LSTDTE = CCRMDT
	DECMAL(1,6) = CCRMDT
	CALL DSPDTE
	GO TO (ANYCNG), CNGCTL
CUSNUM,
	XCALL OUTPT (3,20,1,'\',V)
	CTL = '03,19,06,01,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CCUSNO = ENTRY
	BSEND = ORGCUS
	KEY(1,6) = CCUSNO,'XXXXXX'
	XCALL SERCH (4,CUSIDX,KEY(1,6),1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	GO TO (BADCUS), SRCCTL
	LOKCTL = 1
	XCALL IO (3,CUSMAS,IRC001,READ,LOKCTL)
	PCTR =
SHIFT,
	INCR PCTR
	IF (PCTR.GT.PMAX.OR.PRDCD(PCTR).EQ.'  ') GO TO BLANK
	PCODES(PCTR) = PRDCD(PCTR)
	PDISCS(PCTR) = DISCNT(PCTR)
	GO TO SHIFT
BLANK,
	IF (PCTR.GT.PMAX) GO TO DSPNAM
	PCODES(PCTR) =
	PDISCS(PCTR) =
	INCR PCTR
	IF (PCTR.LE.PMAX) GO TO BLANK
DSPNAM,
	XCALL OUTPT (3,27,0,NAME,V)
	IF (BALMTH.EQ.'B') XCALL OUTPT (2,46,0,'BAL-FW',V)
	IF (BALMTH.EQ.'B') CAPLTO = -1
	CUSTP2 = CUSCD
	TXFLG = TAXFLG
	SAVBAL = BALMTH
	GO TO (APLTO), CNGCTL
	GO TO SALMAN
BADCUS,
	XCALL MESAG('CUSTOMER NOT ON FILE',1)
	GO TO CUSNUM
BADSHP,
	XCALL MESAG('SHIPPING ADDRESS NOT ON FILE',1)
	GO TO SHIPTO
FULL,
	FULL = 1
	UNLOCK 6
	RETURN
SALMAN,
	CTL = '04,19,02,00,N '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CSLMAN = ENTRY
	IF (ENTRY.EQ.BLANKS) CSLMAN = SLSMAN
	IF (CSLMAN.LT.1) GO TO SALMAN
	DECMAL = CSLMAN
	CALL DSPNUM
	GO TO (ANYCNG), CNGCTL
LOCATN,
	CTL = '04,47,02,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.NE.'  ') GO TO GDLOC
	CLOC = CUSLOC
	XCALL OUTPT (4,47,0,CLOC,V)
	GO TO (ANYCNG), CNGCTL
	GO TO PONUM
GDLOC,
	CLOC = ENTRY
	GO TO (ANYCNG), CNGCTL
PONUM,
	CTL = '05,19,10,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CCUSPO = ENTRY
	GO TO (ANYCNG), CNGCTL
DISC,
	CTL = '05,47,02,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CDSCNT = ENTRY
	GO TO (ANYCNG), CNGCTL
SHIPTO,
	XCALL OUTPT (13,12,1,'\',1)
	XCALL OUTPT (14,12,1,'\',1)
	XCALL OUTPT (15,12,1,'\',1)
	XCALL OUTPT (16,12,1,'\',1)
	CTL = '06,19,04,00,N '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CSHPTO = ENTRY
	IF (ENTRY.EQ.BLANKS) GO TO DFBLTO
	IF (CSHPTO.EQ.9999) GO TO SHIPNM
	SRCCTL = 2
	BSEND = ORG171
	XCUSNO = CCUSNO
	SHPNO = CSHPTO
	XCALL SERCH (9,SHIPTO,SKEY,1,10,BSEND,BSMID,SRCCTL,1,11,16,0,0,0,0)		
	GO TO (BADSHP), SRCCTL
	CCUSNM = SHTONA
	CADD1 = SHTOAD(1)
	CADD2 = SHTOAD(2)
	CADD3 = SHTOAD(3)
	CALL DSPSHP
	GO TO (ANYCNG), CNGCTL
	GO TO COMENT
DFBLTO,
	CCUSNM = NAME
	CADD1 = ADD1
	CADD2 = ADD2
	TMPCTY = CITY
	TMPSTA = STATE
	TMPZIP = ZIP
	CADD3 = TMPAD3
	CALL DSPSHP
	GO TO (ANYCNG), CNGCTL
	GO TO COMENT
SHIPNM,
	CTL = '13,12,30,00,A '
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	GO TO (DISPLA), INXCTL
	CCUSNM = ENTRY
	GO TO (ANYCNG), CNGCTL
SHIPA1,
	CTL = '14,12,30,00,A '
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	GO TO (DISPLA), INXCTL
	CADD1 = ENTRY
	GO TO (ANYCNG), CNGCTL
SHIPA2,
	CTL = '15,12,30,00,A '
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	GO TO (DISPLA), INXCTL
	CADD2 = ENTRY
	GO TO (ANYCNG), CNGCTL
SHIPA3,
	CTL = '16,12,30,00,A '
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	GO TO (DISPLA), INXCTL
	CADD3 = ENTRY
	GO TO (ANYCNG), CNGCTL
COMENT,
	XCALL OUTPT (10,19,0,DOTS,V)
	XCALL OUTPT (11,19,0,DOTS,V)
	CTL = '10,19,35,00,A '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY.EQ.BLANKS .AND. CNGCTL.EQ.0) GO TO DEFCOM
	CCOMNT(1) = ENTRY
	ROW = 11
	CALL INPUT
	GO TO (DISPLA), INXCTL
	CCOMNT(2) = ENTRY
	GO TO (ARACCT,ANYCNG), CNGCTL+1
	GO TO ARACCT
DEFCOM,
	CCOMNT(1) = ORDCOM(1)
	CCOMNT(2) = ORDCOM(2)
	XCALL OUTPT (10,19,0,CCOMNT(1),V)
	XCALL OUTPT (11,19,0,CCOMNT(2),V)
	GO TO (ANYCNG), CNGCTL
ARACCT,
	IF (DSTFLG.NE.'Y' .OR. MLARFG.NE.'Y') GO TO ANYCNG
	CARACT = DEFARA
	IF (CNGCTL.EQ.0) GO TO DSPACT
	XCALL GETAC (10,72,CARACT,INXCTL,V)
	GO TO (DISPLA), INXCTL
	GO TO ANYCNG
DSPACT,
	ALPHA = CARACT, 'XXXX-XXX'
	XCALL OUTPT (10,72,0,ALPHA,V)
	GO TO ANYCNG
DSPSHP,
	XCALL OUTPT (13,12,0,CCUSNM,1)
	XCALL OUTPT (14,12,0,CADD1,1)
	XCALL OUTPT (15,12,0,CADD2,1)
	XCALL OUTPT (16,12,0,CADD3,1)
	RETURN
CNGBR,
	GO TO (APLTO,DATE,CUSNUM,SALMAN,LOCATN,PONUM,DISC,SHIPTO,
&		SHIPNM,SHIPA1,SHIPA2,SHIPA3,COMENT,ARACCT), WHATNO
	XCALL MESAG (' ',6)
	GO TO ANYCNG
PROCES,
	XCALL WATE(3,V)
	IF (INXCTL.EQ.2) RETURN
	CALL GTTXCD
	CRMHDR = TCRMHD
	LOKCTL = 1
	XCALL IO (6,DUMHDR,1,READ,LOKCTL)
	IF (REC046.GE.MAX046) GO TO FULL
	REC046 = REC046 + 1
	ADCNT1 = REC046
	LOKCTL = 1
	XCALL IO (6,DUMHDR,1,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (6,TCRMHD,ADCNT1,WRITE,LOKCTL)
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR,DISPLA), CNGCTL+1
DSPNUM,
	OPTION = 1
	GO TO CALDSP
DSPDTE,
	OPTION = 2
	GO TO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
GTTXCD,
	IF (CSHPTO.EQ.0) GO TO REDCUS
	IF (CSHPTO.EQ.9999) GO TO REDCUS
	SRCCTL = 2
	BSEND = ORG171
	XCUSNO = CCUSNO
	SHPNO = CSHPTO
	XCALL SERCH (9,SHIPTO,SKEY,1,10,BSEND,BSMID,SRCCTL,1,11,16,0,0,0,0)		
	GO TO (INVSHP), SRCCTL
	TXFLG = SHTOTC
	RETURN
INVSHP,
	TXFLG = 
	RETURN
REDCUS,
	TXFLG = TAXFLG
	RETURN
END
