;  PDAMNT / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PRODUCT CATEGORY SALES ACCOUNT FILE MAINTENANCE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD PRDACT		
		.INCLUDE 'DEF:RD069A.DEF'
RECORD PDACTL		
		.INCLUDE 'DEF:RD069B.DEF'
RECORD ARACCT		
		.INCLUDE 'DEF:RD007A.DEF'
RECORD ARACTL		
		.INCLUDE 'DEF:RD007B.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD SNDMSG
		,A3,	'CP:'
	PRGNAM	,A6
	RCNT	,D5
	OCNT	,D5
		,D3,	069
		,A3
RECORD NXTMSG
		,A3,	'CP:'
	MSGPRG	,A6
RECORD
	ALPHA	,A8
	SRCHPD	,A39
	ORGPDA	,D5
	ORGARA	,D5
	ENTRY	,A5
	INXCTL  ,D1
	CNGCTL	,D1
	WHATNO	,D1
	SELECT	,D1
	KEY   	,A2
	KEY2	,A7
	BSEND	,D5
	BSMID	,D5
	SBSMID	,D5
	MAXCNT	,D5
	SRCCTL	,D1
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	SWITCH = 1
	XCALL FILES (7,'U',69,SWITCH)		;FILE # 69 -- PRDACT FILE
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL FILES(2,'I',07,SWITCH)		;FILE # 07 -- ARACCT FILE
	IF (SWITCH.EQ.9) GO TO ENDOFF
	SWITCH = 5
	XCALL FILES (13,'I',69,SWITCH)
UPDFLG,
	LOKCTL = 0
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	IF (LOKCTL.EQ.1) CLOSE 5
	IF (LOKCTL.EQ.1) GO TO ENDOFF
	ORGPDA = ORG069
	MAXCNT = MAX069
	DFL069 =
	SFL069 =
	LOKCTL = 0
	XCALL IO (7,PDACTL,1,WRITE,LOKCTL)
	IF (LOKCTL.EQ.1) CLOSE 5
	IF (LOKCTL.EQ.1) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (2,ARACTL,1,READ,LOKCTL)
	ORGARA = ORG007
BEGIN,
	IF (REC069.EQ.ORG069) SELECT = 6
	XCALL MMENU ('PRODUCT CATEGORY ACCOUNT MAINTENANCE','CATEGORIE',
&			SELECT,V)
	GO TO (DISPLA,DISPLA,DISPLA,LIST,LIST), SELECT
LOOP,
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	IF (DFL069.NE.1.AND.DEL069.GE.10) GO TO ORGPDA
	IF (SFL069.NE.1.AND.(REC069-ORG069).GE.50) GO TO SRTPDA
ENDOFF,
	XCALL WATE(3,V)
	CALL CLOSES
EXIT,
	XCALL PGCHN ('CP:CPMENU',1)
LIST,
	CALL CLOSES
	IF (SELECT.EQ.4) XCALL PGCHN ('CP:PDALST',1)
	SWITCH = 3
	XCALL FILES (1,'U',69,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT
	XCALL FILES (7,'I',69,5)
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	MSGPRG = 'PDALST'
SRTIX2,
	PRGNAM = 'PDACNT'
	OCNT = ORG069
	SWITCH = 2
	XCALL SNMSG (SNDMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (NXTMSG,SWITCH)
	RCNT = REC069
	XCALL PGCHN ('CP:SRTPDA',0)
ORGPDA,
	DFL069 = 1
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,WRITE,LOKCTL)
	XCALL WATE (4,V)
	CALL CLOSES
	XCALL PGCHN ('CP:ORGPDA',0)
SRTPDA,
	SFL069 = 1
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,WRITE,LOKCTL)
	XCALL WATE (4,V)
	CALL CLOSES
	SWITCH = 3
	XCALL FILES (1,'U',69,SWITCH)
	IF (SWITCH.EQ.9) GO TO EXIT
	MSGPRG = 'CPMENU'
	GO TO SRTIX2
CLOSES,
	XCALL FILES (7,'U',69,4)
	XCALL FILES (2,'I',07,4)
	RETURN
PURGE,
	XCALL MESAG ('DELETED RECORDS MUST BE PURGED',1)
	GO TO LOOP
DISPLA,
	UNLOCK 7
	CNGCTL =
	PRDACT =
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (5,20,0,'1. PRODUCT CATEGORY',V)
	XCALL OUTPT (6,20,0,'2. SALES ACCOUNT #',V)
	IF (SELECT.NE.1) XCALL OUTPT (5,19,0,'*',V)
CATCOD,
	IF (SELECT.NE.1.AND.CNGCTL) GO TO BADCNG
	IF (SELECT.EQ.1.AND.REC069.GE.MAX069) GO TO FULL
	IF (SELECT.EQ.3.AND.DEL069.GE.95) GO TO PURGE
	CTL = '05,41,02,01,AE'
	CALL INPUT
	GO TO (DISPLA,BEGIN), INXCTL
	PRDCAT = ENTRY (1,2)
	IF (PRDCAT.EQ.'  ') GO TO CATCOD
	KEY = PRDCAT
	BSEND = ORGPDA
	LOKCTL = 1
	XCALL IO (13,SRCHPD,MAXCNT,READ,LOKCTL)
	XCALL SERCH (13,SRCHPD,KEY,1,2,BSEND,BSMID,SRCCTL,2,10,15,0,0,0,0)
	SBSMID = BSMID
	GO TO (FNDREC,FNDREC), SELECT-1
	GO TO (BADPRD), SRCCTL+1
	GO TO (ANYCNG), CNGCTL
	GO TO ACCTNO
FULL,
	CALL CLOSES
	XCALL OUTPT (3,1,2,'\',1)
	XCALL OUTPT (3,0,2,'\',V)
	XCALL OUTPT (6,20,0,'THE PRODUCT CATEGORY ACCOUNT FILE IS FULL.',V)
	XCALL OUTPT (7,20,0,'RUN "XPAND" AND EXPAND THE "PRDACT" FILE',V)
	XCALL OUTPT (8,20,0,'THEN CONTINUE.',V)
	XCALL MESAG(' ',2)
	STOP
BADPRD,
	UNLOCK 7
	XCALL MESAG('PRODUCT CATEGORY ALREADY ASSIGNED',1)
	GO TO DISPLA
CLRSUB,
	XCALL OUTPT (6,45,1,'\',V)
	RETURN
ACCTNO,
	CALL CLRSUB
	CTL = '06,41,04,03,#E'
	CALL INPUT
	GO TO (DISPLA,BEGIN), INXCTL
	PACTNO (1,4) = ENTRY (1,4)
	XCALL OUTPT (6,45,0,'-',V)
	CTL = '06,46,03,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY(1,3).EQ.'   ') CALL CLRSUB
	PACTNO (5,7) = ENTRY (1,3)
	BSEND = ORGARA
	KEY2 = PACTNO, 'XXXXXXX'
	XCALL SERCH (2,ARACCT,KEY2,1,7,BSEND,BSMID,SRCCTL,2,32,37,0,0,0,0)
	GO TO (BADACT), SRCCTL
	PACTDS = ARACDS
	XCALL OUTPT (6,51,0,ARACDS,V)
	GO TO ANYCNG
BADACT,
	XCALL MESAG('ACCOUNT NOT ON FILE',1)
	GO TO ACCTNO
CNGBR,
	GO TO (CATCOD,ACCTNO), WHATNO
BADCNG,
	CNGCTL = 3
	GO TO ANYCNG
PROCES,
	GO TO (ADD,CHANGE), SELECT
FNDREC,
	GO TO (NOFIND), SRCCTL
LOKERR,
	LOKCTL = 1
	XCALL IO (7,PRDACT,BSMID,READ,LOKCTL)
	ALPHA = PACTNO, 'XXXX-XXX'
	XCALL OUTPT (6,41,0,ALPHA,V)
	XCALL OUTPT (6,51,0,PACTDS,V)
	IF (SELECT.NE.3) GO TO ANYCNG
	XCALL OUTPT (12,1,1,'OK TO DELETE ?',V)
	CTL = '12,17,01,00,YN'
	CALL INPUT
	GO TO (DELETE,DISPLA), INXCTL
ADD,
RDERR2,
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	BSMID = SBSMID
	BSMID = BSMID - 1
	LOKCTL = 1
	XCALL IO (13,SRCHPD,MAXCNT,READ,LOKCTL)
	BSEND = ORGPDA
	XCALL SERCH (13,SRCHPD,KEY,1,2,BSEND,BSMID,SRCCTL,5,10,15,0,0,0,0)
	GO TO (BADPRD), SRCCTL + 1
	XCALL WATE (3,V)
	INCR REC069
	IF (REC069.GT.MAX069) GO TO FULL
	ON ERROR WRTERR
WRTERR,
	LOKCTL = 1
	XCALL IO (7,PRDACT,REC069,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,WRITE,LOKCTL)
CLRBUF,
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (13,PDACTL,1,READ,LOKCTL)
	GO TO DISPLA
CHANGE,
	LOKCTL = 1
	XCALL IO (7,PRDACT,SBSMID,WRITE,LOKCTL)
	IF (SELECT.EQ.3) XCALL MESAG ('RECORD DELETED',2)
	GO TO CLRBUF
DELETE,
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,READ,LOKCTL)
	INCR DEL069
	LOKCTL = 1
	XCALL IO (7,PDACTL,1,WRITE,LOKCTL)
	PACTDS =
	PACTDS (1,6) = '000000'
	GO TO CHANGE
NOFIND,
	XCALL MESAG ('RECORD NOT FOUND',1)
	GO TO DISPLA
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR,DISPLA), CNGCTL+1
END
