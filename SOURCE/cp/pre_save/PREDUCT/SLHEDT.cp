; SLHEDT / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		PRINT OUT DETAIL SALES HISTORY RECORDS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SLSHST		
		.INCLUDE 'DEF:RD055A.DEF'
RECORD HSTCTL		
		.INCLUDE 'DEF:RD055B.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD TITLE
		,A30	,'DETAIL SALES HISTORY EDIT LIST'
RECORD HDR1
		,A4
		,A7	,'INV/CRM'
		,A3
		,A7	,'INV/CRM'
		,A4
		,A5	,'ORDER'
		,A5
		,A4	,'CUST'
		,A3
		,A4	,'CUST'
		,A2
		,A2	,'SL'
		,A26
		,A3	,'PRD'
		,A8
		,A3	,'TAX'
		,A5
		,A3	,'QTY'
		,A8
		,A4	,'SALE'
		,A9
		,A4	,'COST'
		,A9
RECORD HDR2
		,A5
		,A4	,'DATE'
		,A7
		,A2	,'NO'
		,A7
		,A4	,'DATE'
		,A7
		,A2	,'NO'
		,A5
		,A3	,'TYP'
		,A2
		,A2	,'MN'
		,A3
		,A3	,'TER'
		,A4
		,A11	,'ITEM NUMBER'
		,A5
		,A3	,'CAT'
		,A2
		,A3	,'LOC'
		,A3
		,A3	,'FLG'
		,A3
		,A8	,'SOLD/CRD'
		,A4
		,A6	,'AMOUNT'
		,A7
		,A6	,'AMOUNT'
		,A8
RECORD
	MASK1	,A8	,'ZX/XX/XX'
	MASK2	,A6	,'ZZZZZX'
	MASK3	,A7	,'ZZ,ZZX-'
	MASK4	,A11	,'ZZZ,ZZZ.XX-'
	PLINE 	,A132
	V	,D1
	LPSW	,D2
	SWITCH	,D1
	LINCNT	,D2	,60
	PGCNT	,D3
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	LPARG	,D1
	STRDTE	,D6
	ENDDTE	,D6
	STRTNO	,A6
	ENDNO	,A6
	BLANKS	,A6
	STXCTL	,D1
	PRINTD	,D1
	STRDAT	,A6
	ENDDAT	,A6
	WRKDAT	,A6
	ENTRY	,A7
	SAVCUS	,D6
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	RECIN	,D5
	LISTCT	,D5
	TQTY	,D6
	TSALE	,D9
	TCOST	,D9
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST	,D1
PROC
	XCALL TERID (V)
	SWITCH = 1
	XCALL FILES (1,'I',41,SWITCH)
	IF (SWITCH.EQ.9) XCALL PGCHN ('CP:UNPSLH',1)
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	XCALL FILES (1,'I',41,4)
	SWITCH = 5
	XCALL FILES (8,'I',55,SWITCH)		;FILE # 55 -- SLSHST FILE
BEGIN,
	XCALL OUTPT (1,1,0,'MANUAL ENTRY OF SALES HISTORY',V)
	XCALL OUTPT (2,1,2,'PRINT EDIT LIST',1)
	SAVCUS =
	STXCTL = 2
	XCALL STENO (STRTNO,ENDNO,'DOC DATE',6,STXCTL,V)
	IF (STXCTL.NE.0) GOTO EOF
	IF (STRTNO.EQ.BLANKS) STRDAT = '      '
	IF (STRTNO.EQ.BLANKS) ENDDAT = '999999'
	IF (STRTNO.EQ.BLANKS) GOTO RDYP

	STRDTE = STRTNO
	ENDDTE = ENDNO

	STRDAT(1,2) = STRDTE(5,6),'XX'
	STRDAT(3,6) = STRDTE(1,4),'XXXX'
	ENDDAT(1,2) = ENDDTE(5,6),'XX'
	ENDDAT(3,6) = ENDDTE(1,4),'XXXX'
RDYP,
	SPLFIL(5,6) = 'ET'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON(LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:UNPSLH',1)
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LINCNT = 60
	PGCNT = 0
	PRINTD = 1
	LOKCTL = 1
	XCALL IO (8,HSTCTL,1,READ,LOKCTL)
	RECIN = 1
REDHST,
	INCR RECIN
	IF (RECIN.GE.MAX055) GOTO EOF
	LOKCTL = 1
	XCALL IO (8,SLSHST,RECIN,READ,LOKCTL)
	IF (SLSHST(1,5).EQ.']]]]]') GOTO EOF
	IF (HINVDT.EQ.0) GO TO REDHST
	WRKDAT(1,2) = HINVDT(5,6),'XX'
	WRKDAT(3,6) = HINVDT(1,4),'XXXX'
	IF (WRKDAT.GE.STRDAT .AND. WRKDAT.LE.ENDDAT) CALL PRTEDT
	GOTO REDHST
EOF,
	IF (PRINTD) GO TO PRTTOT
	XCALL WATE(3,V)
	XCALL FILES (8,'I',55,4)
	XCALL PGCHN ('CP:BLDSLH',1)
;*******************************
PRTEDT,
	IF (HCUSNO.EQ.SAVCUS) GO TO SAMCUS
	SAVCUS = HCUSNO
	XCALL LINFD (1)
	INCR LINCNT
SAMCUS,
	PLINE(4,11) = HINVDT,MASK1
	PLINE(15,20) = HINVNO,MASK2
	IF (HORDDT.NE.0) PLINE(24,31) = HORDDT,MASK1
	PLINE(35,40) = HCUSNO,MASK2
	PLINE(44,45) = HCUSCD
	PLINE(49,50) = HSLSMN
	PLINE(54,55) = HTERR
	PLINE(59,73) = HITMNO
	IF (JUST) XCALL LEFTJ (PLINE(59,73),15)
	PLINE(77,78) = HPRDCD
	PLINE(82,83) = HLOC
	PLINE (89,89) = 'N'
	IF (HTXFLG.EQ.'1') PLINE (89,89) = 'Y'
	PLINE(95,101) = HQTY,MASK3
	PLINE(104,114) = HSALE,MASK4
	PLINE(117,127) = HCOST,MASK4
	CALL PRINT
	TQTY = TQTY + HQTY
	TSALE = TSALE + HSALE
	TCOST = TCOST + HCOST
	INCR LISTCT
	RETURN
;*******************************
PRTTOT,
	XCALL LINFD (3)
	PLINE(5,10) = LISTCT,'ZZ,ZZX'
	PLINE(12,26) = 'RECORDS LISTED '
	PLINE (86,92) = 'TOTALS:'
	PLINE(94,101) = TQTY,'ZZZ,ZZX-'
	PLINE(102,114) = TSALE,'Z,ZZZ,ZZZ.XX-'
	PLINE(115,127) = TCOST,'Z,ZZZ,ZZZ.XX-'
	CALL PRINT
NOFORM,
	TQTY =
	TSALE =
	TCOST =
	LISTCT =
	PRINTD =
	XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	IF (STRDTE.NE.0) GO TO BEGIN
	GO TO EOF
;**************************************************************************
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		'NO LEGEND',' ',' ',0,0,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;*********************************************************************************
	END
