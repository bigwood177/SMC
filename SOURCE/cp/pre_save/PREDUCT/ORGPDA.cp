; ORGPDA / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		REMOVES RECORDS MARKED FOR DELETION FROM PRDACT FILE
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD PRDACT		
		.INCLUDE 'DEF:RD069B.DEF'
RECORD ,X		
		.INCLUDE 'DEF:RD069A.DEF'
RECORD SNDMSG
		,A3,	'CP:'
	PRGNAM	,A6,	'PDACNT'
	RCNT	,D5
	OCNT	,D5
		,D3 ,069
RECORD
	CLRCP7	,A13,	'069,CP:ORGPDA'
	RDCNT	,D5,	00002
	TOTCNT	,D5
	WRTCNT	,D5
	ORGREC	,D5
	ORGDEL	,D5,	00000
	BRACKS	,A39,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
	SRTFLG	,D1
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID (V)
	SWITCH = 2
	XCALL FILES (7,'U',69,SWITCH)		;FILE # 69 -- PRDACT FILE
	IF (SWITCH.EQ.9) GO TO INUSE
	XCALL OUTPT (2,1,0,'REORG CATEGORY ACCT ACCOUNT FILE',1)
	LOKCTL = 1
	XCALL IO (7,PRDACT,1,READ,LOKCTL)
	TOTCNT = REC069
	ORGREC = ORG069
	WRTCNT = 1
READ,
	LOKCTL = 1
	XCALL IO (7,PRDACT,RDCNT,READ,LOKCTL)
	IF (PACTDS.EQ.BRACKS) GO TO EOF
	INCR RDCNT
	IF (PACTDS.EQ.'000000') GO TO DELETE
	INCR WRTCNT
	LOKCTL = 1
	XCALL IO (7,PRDACT,WRTCNT,WRITE,LOKCTL)
CONTIN,
	IF (RDCNT.GT.TOTCNT) GO TO EOF
	GO TO READ
DELETE,
	IF (RDCNT-1.LE.ORGREC) INCR ORGDEL
	GO TO CONTIN
EOF,
	LOKCTL = 1
	XCALL IO (7,PRDACT,1,READ,LOKCTL)
	ORG069 = ORG069-ORGDEL
	REC069 = WRTCNT
	DEL069 = 0
	LOKCTL = 1
	XCALL IO (7,PRDACT,1,WRITE,LOKCTL)
	IF (REC069.GT.ORG069) SRTFLG = 1
WRTBRK,
	IF (WRTCNT.EQ.TOTCNT) GO TO EOF2
	INCR WRTCNT
	LOKCTL = 1
	XCALL IO (7,BRACKS,WRTCNT,WRITE,LOKCTL)
	GO TO WRTBRK
EOF2,
	IF (SRTFLG) GO TO SORTIT
	XCALL FILES (7,'U',69,4)		;FILE # 69 -- PRDACT FILE
	XCALL PGCHN ('CP:CPMENU',1)
INUSE,
	SWITCH = 5
	XCALL SNMSG (CLRCP7,SWITCH)
	XCALL PGCHN ('UT:CLROF2',1)
SORTIT,
	CLOSE 7
	XCALL OUTPT (2,1,1,'\',1)
	RCNT = REC069
	OCNT = ORG069
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	XCALL PGCHN ('CP:SRTPDA',1)
	END
