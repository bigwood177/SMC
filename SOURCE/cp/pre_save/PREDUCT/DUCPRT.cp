;DUCPRT.DBL


RECORD	COPTBL
	.INCLUDE 'DEF:RD182A.DEF'

RECORD	PARAMS
	.INCLUDE 'DEF:PARAM.DEF'

RECORD	DPKEY
	K_CODE	,A2
	K_GAUGE	,D3
	K_TYPE	,D1
	K_CAT	,D1
	K_STYLE	,D1

RECORD	HD1
	,A*	,'GAUGE   TYPE               CAT                STYLE'
	,A*,	'                  PRICE'

RECORD	PRINT
	TITLE	,A*,	'DUCT PRICE TABLE'
	HD	,A6,	'NO HDR'
	LG	,A9,	'NO LEGEND'
	PLINE	,A80
	PRNTON	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3,080
	LPARG	,D1
	PRNTSW	,D1
	PRTCTR	,D1

RECORD	VARS
	PDESC	,A15
	ENTRY	,A30
	INXCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	STG	,D3
	ENG	,D3
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	SWITCH	,D1
	V	,D1

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT DUCT PRICE TABLE',1)
	SWITCH = 1
	XCALL FILES (1,'SI',182,SWITCH)
	IF (SWITCH .EQ. 9) XCALL PGCHN ('CP:TBLMNU',1)

	CLEAR PRNTON
DISPLA,
	CLEAR CNGCTL
	XCALL OUTPT (1,1,2,'PRINT DUCT PRICE TABLE',1)
	XCALL OUTPT (4,4,0,'1. START GAUGE:',1)
	XCALL OUTPT (6,4,0,'2. END   GAUGE:',1)
STG,
	XCALL INPUT (4,21,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	STG = ENTRY(1,3)
	IF (STG .EQ. 0)
		BEGIN
		ENG = 999
		XCALL OUTPT (4,21,1,'ALL',1)
		XCALL OUTPT (6,21,1,' ',1)
		GOTO ANYCNG
		END
	GOTO (ANYCNG),CNGCTL
ENG,
	XCALL INPUT (6,21,03,00,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF),INXCTL
	ENG = ENTRY(1,3)
	IF (ENG .EQ. 0)
		BEGIN
		ENG = STG
		ENTRY(1,3) = ENG,	'ZZX' [LEFT]
		XCALL OUTPT (6,21,1,ENTRY(1,3),1)
		END
ANYCNG,
	XCALL ANYCN (CNGCTL,WHATNO)
	GOTO (PROCES,CNGBR),CNGCTL+1
CNGBR,
	GOTO (STG,ENG),WHATNO
	GOTO ANYCNG
PROCES,
	CLEAR DPKEY
	K_CODE = 'DP'
	K_GAUGE = STG

	FIND (1,COPTBL,DPKEY) [ERR=LOOP]
LOOP,		
	XCALL IOS (1,COPTBL,READ,LOKCTL)
	IF (LOKCTL .NE. 0) GOTO EOF
	IF (TBLCOD .NE. 'DP') GOTO EOF
	IF (DP_GAUGE .GT. ENG) GOTO EOF

	PLINE (3,5) = DP_GAUGE,	'ZXX'
	PLINE (9,9) = DP_TYPE,	'X'
	IF (DP_TYPE .LE. 4)
	THEN	BEGIN
		PDESC = DTYPE(DP_TYPE)
		PLINE (11,25) = PDESC(3,15)
		END
	ELSE	PLINE (11,25) = '*OUT OF RANGE*'

	PLINE (28,28) = DP_CAT,	'X'
	IF (DP_CAT .LE. 4)
	THEN	BEGIN
		PDESC = DCAT(DP_CAT)
		PLINE (30,44) = PDESC(3,15)
		END
	ELSE	PLINE (30,44) = '*OUT OF RANGE*'

	PLINE (47,47) = DP_STYLE,	'X'
	IF (DP_STYLE .LE. 5)
	THEN	BEGIN
		PDESC = DSTY(DP_STYLE)
		PLINE (49,53) = PDESC(3,15)
		END
	ELSE	PLINE (49,53) = '*OUT OF RANGE*'

	PLINE (66,74) = DP_PRICE,	'Z,ZZX.XXX'
	CALL PRINT
	GOTO LOOP

EOF,
ENDOFF,
	IF (PRNTON.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	XCALL FILES (1,'SI',182,4)
	XCALL PGCHN ('CP:TBLMNU',1)

PRINT,
	IF (PRNTON .EQ. 0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HD1,HD,HD
&		,LG,LG,LG,0,080,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
;------------------------------------------------------------------------------

PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ENDOFF
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTON = 1
	RETURN
;------------------------------------------------------------------------------

END
;==============================================================================
;GAUGE   TYPE               CAT                STYLE                  PRICE
;  ZZX   X AAAAAAAAAAAAAAA  X AAAAAAAAAAAAAAA  X AAAAAAAAAAAAAAA  ZZ,ZZX.XX
;123456789012345678901234567890123456789012345678901234567890123456789012345678
;         1         2         3         4         5         6         7        



