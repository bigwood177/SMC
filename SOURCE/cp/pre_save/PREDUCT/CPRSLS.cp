;  CPRSLS / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SALES COMPARISON BY PRODUCT
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CUSMAS   	
		.INCLUDE 'DEF:RD001A.DEF'
RECORD DUMCUS ,X		
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX  		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD INVMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV ,X		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX  		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD TITLE
		,A40,'SALES COMPARISON BY CUSTOMER AND PRODUCT'
RECORD LEGEND
		,A48
		,A14,'************* '
	MONTH1	,A10
		,A13,'*************'
		,A9
		,A20,'********** YTD THRU '
	MONTH2	,A10
		,A7,' ******'
RECORD LGND2
		,A46
		,A7,'-------'
	LYR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	LYR2	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR2	,D4
		,A6,'------'
RECORD HDR1
		,A7,'CUSNO  '
		,A13,'CUSTOMER NAME'
		,A112
RECORD HDR2
		,A1
		,A9,'ITEM # / '
		,A1
		,A11,'DESCRIPTION'
		,A4
		,A8,'PROD CAT'
		,A23
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A1
RECORD PRDTOT
	PRDMLU	,D7
	PRDMU	,D7
	PRDYLU	,D7
	PRDYU	,D7
	PRDMLS	,D9
	PRDMS	,D9
	PRDYLS	,D9
	PRDYS	,D9
	PRDMLC	,D9
	PRDMC	,D9
	PRDYLC	,D9
	PRDYC	,D9
RECORD CUSTOT
	CUSMLU	,D7
	CUSMU	,D7
	CUSYLU	,D7
	CUSYU	,D7
	CUSMLS	,D9
	CUSMS	,D9
	CUSYLS	,D9
	CUSYS	,D9
	CUSMLC	,D9
	CUSMC	,D9
	CUSYLC	,D9
	CUSYC	,D9
RECORD GRDTOT
	GRDMLU	,D7
	GRDMU	,D7
	GRDYLU	,D7
	GRDYU	,D7
	GRDMLS	,D9
	GRDMS	,D9
	GRDYLS	,D9
	GRDYS	,D9
	GRDMLC	,D9
	GRDMC	,D9
	GRDYLC	,D9
	GRDYC	,D9
RECORD DASHES
		,A50,'--------------------------------------------------'
		,A50,'--------------------------------------------------'
		,A32,'--------------------------------'
RECORD
	RATIO1	,D5
	RATIO2	,D5
	RATIO3	,D5
	RATIO4	,D5
	PRATO1	,D5
	PRATO2	,D5
	PRATO3	,D5
	PRATO4	,D5
	GRATO1	,D5
	GRATO2	,D5
	GRATO3	,D5
	GRATO4	,D5
	OLDPRD	,A15
	OLDPCD	,A2
	OLDCUS	,D6
	LINCNT	,D2,60
	CNGCTL	,D1
	SPLFIL	,A14
	IDXCNT	,D5
	PGCNT	,D3
	PLINE	,A132
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	LSTSW	,D1
	FRSTSW	,D1
	KEY	,A15
	MASK	,A13,'Z,ZZZ,ZZZ.ZX-'
	MASK1	,A10,'Z,ZZZ,ZZX-'
	MASK2	,A7,'ZZZ.X%-'
	MASK3	,A14,'ZZ,ZZZ,ZZZ.ZX-'
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	SWITCH	,D1
	PRTCTL	,D3
	LPONSW	,D1
	ROW	,D2
	WHATNO	,D2
	V	,D1
	UNT	,A4,'UNT:'
	SLS	,A4,'SLS:'
	CST	,A4,'CST:'
	STRTNO	,D6
	ENDNO	,D6
	STRTNA	,A6
	ENDNA	,A6
	STXCTL	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	ORGCUS	,D5
	ORGINV	,D5
	JUST	,D1
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'PRINT SALES COMPARISON BY CUSTOMER AND PRODUCT',V)
	SWITCH = 1
	XCALL FILES(7,'I',59,SWITCH)		; FILE # 59 -- SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE
	XCALL FILES(8,'I',58,SWITCH)		; FILE # 58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	XCALL FILES(2,'I',42,SWITCH)		; FILE # 42 -- INVIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3
	XCALL FILES(1,'I',41,SWITCH)		; FILE # 41 -- INVMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE4
	XCALL FILES(3,'I',1,SWITCH)		; FILE # 1 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE5
	XCALL FILES(4,'I',2,SWITCH)		; FILE # 2 -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE6
	LOKCTL = 1
	XCALL IO(1,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	ORGINV = ORG041
	LOKCTL = 1
	XCALL IO(3,DUMCUS,1,READ,LOKCTL)
	ORGCUS = ORG001
	LOKCTL = 1
	XCALL IO(8,SLSDUM,1,READ,LOKCTL)
	IF (SSPER.EQ.0) GO TO NOPERS
	YR1 = SSYEAR
	YR2 = SSYEAR
	LYR1 = SSYEAR - 1
	LYR2 = SSYEAR - 1
	MONTH1 = SSDESC(SSPER)
	MONTH2 = SSDESC(SSPER)
;*****************************************************************************
;		SELECT CUSTOMER # RANGE
SELECT,
	STXCTL = 1
	XCALL STENO(STRTNA,ENDNA,'CUSTOMER #',6,STXCTL,V)
	GO TO (END),STXCTL
	IF (ENDNA.EQ.'[[[') STRTNA =
	IF (ENDNA.EQ.'[[[') ENDNA = '999999'
	STRTNO = STRTNA
	ENDNO = ENDNA
;*********************************************************************************
;		READ SORTED SLSIDX FILE
	XCALL WATE (3,V)
	IDXCNT = 1
READ,
	INCR IDXCNT
	LOKCTL = 1
	XCALL IO(7,SLSIDX,IDXCNT,READ,LOKCTL)
	IF(SIPROD.EQ.']]]]]') LSTSW = 1
	IF(SICUS.LT.STRTNO.AND.LSTSW.NE.1) GO TO READ
	IF(SICUS.GT.ENDNO.AND.LSTSW.NE.1) GO TO READ
	IF(IRC058.EQ.0.AND.LSTSW.NE.1) GO TO READ
	IF((SIPROD.NE.OLDPRD.OR.SICUS.NE.OLDCUS).AND.FRSTSW) CALL PRDTOT
	IF(ITEMNO.EQ.SIPROD.AND.SICUS.EQ.OLDCUS) GO TO SAMEP
	FRSTSW = 1
BINAR1,
	BSEND = ORGINV
	KEY = SIPROD
	XCALL SERCH (2,INVIDX,KEY,1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
	IF (SRCCTL.NE.0.OR.IRC041.EQ.0) GO TO NOINV
	LOKCTL = 1
	XCALL IO(1,INVMAS,IRC041,READ,LOKCTL)
BINAR2,
	BSEND = ORGCUS
	KEY(1,6) = SICUS,'XXXXXX'
	XCALL SERCH (4,CUSIDX,KEY(1,6),1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL.NE.0.OR.IRC001.EQ.0) GO TO NOCUS
	LOKCTL = 1
	XCALL IO(3,CUSMAS,IRC001,READ,LOKCTL)
SKPCUS,
	IF(SICUS.NE.OLDCUS) CALL CUSPRT
	IF(FRSTSW.NE.1) RETURN
	OLDPRD = SIPROD
	OLDPCD = SIPCD
	OLDCUS = SICUS
;**********************************************************************************
;		CALCULATE SAME PRODUCT TOTALS
SAMEP,
	IF(LSTSW.EQ.1) GO TO PRDTOT
	LOKCTL = 1
	XCALL IO (8,SLSSUM,IRC058,READ,LOKCTL)
	IF (SSPER.EQ.1) CALL FIRSTP
	IF (SSPER.EQ.2) CALL SECNDP
	IF (SSPER.GT.2) CALL OTHERP
	GO TO READ
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO END
FIRSTP,
	PRDMLU = PRDMLU + SSUYTD(1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(1)
	PRDMLC = PRDMLC + SSCYTD(1)
	PRDMC = PRDMC + SSCOST
	PRDMS = PRDMS + SSSALE
	PRDYLU = PRDYLU + SSUYTD(1)
	PRDYU = PRDYU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(1)
	PRDYS = PRDYS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(1)
	PRDYC = PRDYC + SSCOST
	RETURN
SECNDP,
	PRDMLU = PRDMLU + SSUYTD(2) - SSUYTD(1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(2) - SSSYTD(1)
	PRDMS = PRDMS + SSSALE
	PRDMLC = PRDMLC + SSCYTD(2) - SSCYTD(1)
	PRDMC = PRDMC + SSCOST
	PRDYLU = PRDYLU + SSUYTD(2)
	PRDYU = PRDYU + SSLSTU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(2)
	PRDYS = PRDYS + SSLSTS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(2)
	PRDYC = PRDYC + SSLSTC + SSCOST
	RETURN
OTHERP,
	PRDMLU = PRDMLU + SSUYTD(SSPER) - SSUYTD(SSPER - 1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(SSPER) - SSSYTD(SSPER - 1)
	PRDMS = PRDMS + SSSALE
	PRDMLC = PRDMLC + SSCYTD(SSPER) - SSCYTD(SSPER - 1)
	PRDMC = PRDMC + SSCOST
	PRDYLU = PRDYLU + SSUYTD(SSPER)
	PRDYU = PRDYU + SSUYTD(SSPER - 2) + SSLSTU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(SSPER)
	PRDYS = PRDYS + SSSYTD(SSPER - 2) + SSLSTS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(SSPER)
	PRDYC = PRDYC + SSCYTD(SSPER - 2) + SSLSTC + SSCOST
	RETURN
;********************************************************************************
;		PRINT CUSTOMER
CUSPRT,
	CALL PRINT
	PLINE = DASHES (1,33)
	CALL PRINT
	PLINE(1,6) = CUSNO
	PLINE(9,33) = NAME
	CALL PRINT
	PLINE = DASHES (1,33)
	CALL PRINT
	RETURN
;*****************************************************************************
;		PRINT SAME PRODUCT TOTALS
PRDTOT,
	CALL PRINT
	PLINE (6,20) = SSPROD
	IF (JUST) XCALL LEFTJ (PLINE(6,20),15)
	PLINE (30,31) = OLDPCD
	PLINE (38,41) = UNT
	PLINE (43,52) = PRDMLU,MASK1
	PLINE(63,75) = PRDMU,MASK1
	PLINE(88,98) = PRDYLU,MASK1
	PLINE(112,121) = PRDYU,MASK1
	CALL PRINT
	PLINE (7,36) = DESCR
	PLINE (38,41) = SLS
	PLINE (43,55) = PRDMLS,MASK
	PLINE (66,78) = PRDMS,MASK
	PLINE (89,101) = PRDYLS,MASK
	PLINE (112,124) = PRDYS,MASK
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = PRDMLC,MASK
	RATIO1 =
	IF (PRDMLS.NE.0) RATIO1 = (PRDMLS - PRDMLC) * 1000 / PRDMLS
	PLINE (57,63) = RATIO1,MASK2
	PLINE (66,78) = PRDMC,MASK
	RATIO2 =
	IF (PRDMS.NE.0) RATIO2 = (PRDMS - PRDMC) * 1000 / PRDMS
	PLINE (80,86) = RATIO2,MASK2
	PLINE (89,101) = PRDYLC,MASK
	RATIO3 =
	IF (PRDYLS.NE.0) RATIO3 = (PRDYLS - PRDYLC) * 1000 / PRDYLS
	PLINE (103,109) = RATIO3,MASK2
	PLINE (112,124) = PRDYC,MASK
	RATIO4 =
	IF (PRDYS.NE.0) RATIO4 = (PRDYS - PRDYC) * 1000 / PRDYS
	PLINE (126,132) = RATIO4,MASK2
	CALL PRINT
;**********************************************************************************
;		CALCULATE CUSTOMER TOTALS
	CUSMLU = CUSMLU + PRDMLU
	CUSMU = CUSMU + PRDMU
	CUSYLU = CUSYLU + PRDYLU
	CUSYU	= CUSYU + PRDYU
	CUSMLS = CUSMLS + PRDMLS
	CUSMS = CUSMS + PRDMS
	CUSYLS = CUSYLS + PRDYLS
	CUSYS = CUSYS + PRDYS
	CUSMLC = CUSMLC + PRDMLC
	CUSMC = CUSMC + PRDMC
	CUSYLC = CUSYLC + PRDYLC
	CUSYC = CUSYC + PRDYC
	PRDTOT =
	IF(LSTSW.EQ.1) GO TO CUSTOT
	IF(SICUS.NE.OLDCUS) CALL CUSTOT
	RETURN
;**********************************************************************************
;		PRINT CUSTOMER TOTALS
CUSTOT,
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
	PLINE(18,25) = 'CUSTOMER'
	PLINE(27,34) = 'TOTALS:'
	PLINE(38,41) = UNT
	PLINE(43,52) = CUSMLU,MASK1
	PLINE(63,75) = CUSMU,MASK1
	PLINE(89,98) = CUSYLU,MASK1
	PLINE(112,121) = CUSYU,MASK1
	CALL PRINT
	PLINE(38,41) = SLS
	PLINE(43,55) = CUSMLS,MASK
	PLINE(66,78) = CUSMS,MASK
	PLINE(89,101) = CUSYLS,MASK
	PLINE(112,124) = CUSYS,MASK
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = CUSMLC,MASK
	PRATO1 =
	IF (CUSMLS.NE.0) PRATO1 = (CUSMLS - CUSMLC) * 1000 / CUSMLS
	PLINE (57,63) = PRATO1,MASK2
	PLINE (66,78) = CUSMC,MASK
	PRATO2 =
	IF (CUSMS.NE.0) PRATO2 = (CUSMS - CUSMC) * 1000 / CUSMS
	PLINE (80,86) = PRATO2,MASK2
	PLINE (89,101) = CUSYLC,MASK
	PRATO3 =
	IF (CUSYLS.NE.0) PRATO3 = (CUSYLS - CUSYLC) * 1000 / CUSYLS
	PLINE (103,109) = PRATO3,MASK2
	PLINE (112,124) = CUSYC,MASK
	PRATO4 =
	IF (CUSYS.NE.0) PRATO4 = (CUSYS - CUSYC) * 1000 / CUSYS
	PLINE (126,132) = PRATO4,MASK2
	CALL PRINT
	PLINE = DASHES
	CALL PRINT
;******************************************************************************
;		CALCULATE GRAND TOTALS
	GRDMLU = GRDMLU + CUSMLU
	GRDMU = GRDMU + CUSMU
	GRDYLU = GRDYLU + CUSYLU
	GRDYU = GRDYU + CUSYU
	GRDMLS = GRDMLS + CUSMLS
	GRDMS = GRDMS + CUSMS
	GRDYLS = GRDYLS + CUSYLS
	GRDYS = GRDYS + CUSYS
	GRDMLC = GRDMLC + CUSMLC
	GRDMC = GRDMC + CUSMC
	GRDYLC = GRDYLC + CUSYLC
	GRDYC = GRDYC + CUSYC
	CUSTOT =
	IF (LSTSW.EQ.1) GO TO GRDTOT
	RETURN
;****************************************************************************
;		PRINT GRAND TOTALS
GRDTOT,
	XCALL LINFD (3)
	LINCNT = LINCNT + 3
	PLINE(21,33) = 'GRAND TOTALS:'
	PLINE (38,41) = UNT
	PLINE(43,52) = GRDMLU,MASK1
	PLINE(66,75) = GRDMU,MASK1
	PLINE(89,98) = GRDYLU,MASK1
	PLINE(112,121) = GRDYU,MASK1
	CALL PRINT
	PLINE(38,41) = SLS
	PLINE(43,55) = GRDMLS,MASK
	PLINE(66,78) = GRDMS,MASK
	PLINE(89,101) = GRDYLS,MASK
	PLINE(112,124) = GRDYS,MASK3
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = GRDMLC,MASK
	GRATO1 =
	IF (GRDMLS.NE.0) GRATO1 = (GRDMLS - GRDMLC) * 1000 / GRDMLS
	PLINE (57,63) = GRATO1,MASK2
	PLINE (66,78) = GRDMC,MASK
	GRATO2 =
	IF (GRDMS.NE.0) GRATO2 = (GRDMS - GRDMC) * 1000 / GRDMS
	PLINE (80,86) = GRATO2,MASK2
	PLINE (89,101) = GRDYLC,MASK
	GRATO3 =
	IF (GRDYLS.NE.0) GRATO3 = (GRDYLS - GRDYLC) * 1000 / GRDYLS
	PLINE (103,109) = GRATO3,MASK2
	PLINE (112,124) = GRDYC,MASK
	GRATO4 =
	IF (GRDYS.NE.0) GRATO4 = (GRDYS - GRDYC) * 1000 / GRDYS
	PLINE (126,132) = GRATO4,MASK2
	CALL PRINT
	IF (LPONSW.EQ.1) XCALL LPOFF(LPSW,SPLFIL,PGCNT)
;********************************************************************************
END,
	XCALL WATE(3,V)
	XCALL FILES (1,'I',41,4)
	XCALL FILES (2,'I',42,4)
	XCALL FILES (8,'I',58,4)
	XCALL FILES (7,'I',59,4)
	XCALL FILES (3,'I',1,4)
	XCALL FILES (4,'I',2,4)
	XCALL PGCHN ('CP:SSMENU',1)
INUSE6,
	XCALL FILES (3,'I',1,4)
INUSE5,
	XCALL FILES (1,'I',41,4)
INUSE4,
	XCALL FILES(2,'I',42,4)
INUSE3,
	XCALL FILES(8,'I',58,4)
INUSE2,
	XCALL FILES(7,'I',59,4)
INUSE,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:SSMENU',1)
;***********************************************
PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',LEGEND,LGND2,
&		'NO LEGEND',0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6) = 'EQ'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE(LPARG,V)
	LPONSW = 1
	RETURN
;***********************************************
NOINV,
	ITEMNO = SIPROD
	DESCR = '* ITEM NOT ON FILE *'
	GO TO BINAR2
NOCUS,
	CUSNO = SICUS
	NAME = '* CUSTOMER NOT ON FILE *'
	GO TO SKPCUS
END
