; PRICES / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PRINTS PRICE LIST
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD INVMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX  		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD TITLE
		,A10, 'PRICE LIST'
RECORD HDR1
		,A49
		,A4, 'PROD'
		,A2
		,A4, 'CUST'
RECORD HDR2
		,A4
		,A7, 'ITEM NO'
		,A6
		,A16, 'ITEM DESCRIPTION'
		,A16
		,A3,'CAT'
		,A3
		,A4, 'TYPE'
		,A4
		,A5, 'PRICE'
RECORD
	MASK	,A9, 'ZZ,ZZZ.XX'
	ENTRY	,A2
	INXCTL	,D1
	LINCNT	,D2,60
	PGCNT	,D6
	PLINE	,A132
	LPSW	,D2
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	PRTCTL	,D3
	STRTNO	,A15
	ENDNO	,A15
	STXCTL	,D1
	CODCTR	,D2
	PRICES	,D2
	LPARG	,D1
	PRNTSW	,D1
	ENDFLG	,D1
	SWITCH	,D1
	OVRFLG	,D1
	V	,D1
	PRTCTR	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST	,D1
PROC
	XCALL TERID (V)
BEGIN,
	XCALL OUTPT (1,1,2,'PRINT PRICE LIST',V)
	SWITCH = 1
	XCALL FILES(2,'I',42,SWITCH)		;FILE # 42 -- INVIDX FILE
	IF (SWITCH.EQ.9) GO TO END
	SWITCH = 1
	XCALL FILES(1,'I',41,SWITCH)		;FILE # 41 -- INVMAS FILE
	IF (SWITCH.EQ.9) GOTO END0
	XCALL WATE(3,V)
	LOKCTL = 1
	XCALL IO (1,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	PRICES = NUMPRC		;GET NUMBER OF PRICES FROM INVMAS CONTROL RECORD
	OVRFLG =
	STXCTL =
	IF (JUST) STXCTL = 3
	XCALL STENO (STRTNO,ENDNO,'ITEM #',15,STXCTL,V)
	GO TO (END1), STXCTL
	LOKCTL = 1
	XCALL IOS (2,INVIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFIDX
RDIDX,
	LOKCTL = 1
	XCALL IOS (2,INVIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO EOFIDX
	IF (IITMNO.LT.STRTNO) GOTO RDIDX
	IF (IITMNO.EQ.']]]]]]') GOTO EOFIDX
	IF (IITMNO.GT.ENDNO.AND.OVRFLG.EQ.0) GO TO SETOVR
	IF (IITMNO.GT.ENDNO) GO TO RDIDX
	IF (IRC041.EQ.0.OR.IITMNO.EQ.'???') GO TO RDIDX
	IF (PRNTSW.NE.1) CALL PRNTON
	IF (ENDFLG.EQ.1) GO TO END1
	LOKCTL = 1
	XCALL IO (1,INVMAS,IRC041,READ,LOKCTL)
	XCALL LINFD (1)
	INCR LINCNT
	CODCTR = 1
	PLINE (1,15) = ITEMNO
	PLINE (18,47) = DESCR
	PLINE (51,52) = PRDCAT
	PLINE (57,58) = PRICCD(1)
	PLINE (62,70) = PRICE(1),MASK
	GOTO PRINT
NXCODE,
	PLINE (57,58) = PRICCD(CODCTR)
	PLINE (62,70) = PRICE(CODCTR),MASK
PRINT,
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR3'
&		,'NO LEGENDS',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	INCR CODCTR
	IF (CODCTR.GT.PRICES.OR.PRICCD(CODCTR).EQ.'  ') GOTO RDIDX
	GOTO NXCODE
SETOVR,
	OVRFLG = 1
	LOKCTL = 1
	XCALL IO (2,INVIDX,ORG041,READ,LOKCTL)
	GO TO RDIDX
EOFIDX,
	IF (ENDNO.EQ.'[[[') GO TO ENDOFF
	XCALL FILES(1,'I',41,4)
	XCALL FILES(2,'I',42,4)
	IF (PRNTSW.EQ.1) XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PRNTSW = 0
	LINCNT = 60
	PGCNT = 0
	GO TO BEGIN
ENDOFF,
	IF (PRNTSW.EQ.1)  XCALL LPOFF(LPSW,SPLFIL,PGCNT)
END1,
	XCALL WATE(3,V)
	XCALL FILES(1,'I',41,4)
END0,
	XCALL FILES(2,'I',42,4)
END,
	XCALL PGCHN ('CP:CPMENU',1)
PRNTON,
	SPLFIL (5,6) = 'EF'
	LPSW = 1		;PRINT,SPOOL, OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) ENDFLG = 1
	IF (LPSW.EQ.0) RETURN
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN
END
