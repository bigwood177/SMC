;  ORGSHT / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;	RE-ORGANIZATION OF THE SHPTO FILE.  THIS FILE HAS NO INDEX.
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SHIPTO
		.INCLUDE 'DEF:RD171A.DEF'

RECORD,X		
		.INCLUDE 'DEF:RD171C.DEF'

RECORD SHPCTL		
		.INCLUDE 'DEF:RD171B.DEF'

RECORD DELARY	
		.INCLUDE 'DEF:RD171D.DEF'

RECORD BRACKS
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A50,	']]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]'
		,A12,	']]]]]]]]]]]]'

RECORD SRTMSG
	PRGNAM	,A9,	'CP:SHTCNT'
	RCNT	,D5
	OCNT	,D5
RECORD
	CNT	,D3
	DELCNT  ,D3
	LOKCTL	,D1
	N	,D3
	NXTPGM	,A9
	ORGCNT  ,D5
	ORGDEL	,D3
	RDPNTR	,D5
	READ	,D1
	RECCNT  ,D5
	SWITCH	,D1
	V	,D1
	WRITE	,D1,	1
	WRPNTR  ,D5
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'REORGANIZE TERM CODES FILE',1)
	XCALL WATE (4,V)
SETUP,
	RDPNTR = 2
	WRPNTR = 2
	SWITCH = 5
	XCALL FILES (1,'U',171,SWITCH)		; FILE #171 - SHIPTO
	IF (SWITCH.EQ.9) GO TO NOMSG
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	ORGCNT = ORG171
	RECCNT = REC171
READ,
	LOKCTL = 1
	XCALL IO (1,SHIPTO,RDPNTR,READ,LOKCTL)
	IF (SHIPTO.EQ.BRACKS) GO TO EOF
	IF (DFL171.EQ.']]]DEL') GO TO DELREC
	INCR CNT
	DELARR(CNT) = SHIPTO
	IF (CNT.EQ.100) CALL WRITE
	INCR RDPNTR
	GO TO READ
DELREC,
	IF (RDPNTR.LE.ORGCNT) INCR ORGDEL
	INCR DELCNT
	INCR RDPNTR
	GO TO READ
WRITE,
	IF (N.EQ.CNT) GO TO OUT
	INCR N
	LOKCTL = 1
	XCALL IO (1,DELARR(N),WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO WRITE
OUT,
	CNT =
	N =
	RETURN
EOF,
	IF (CNT.NE.0) CALL WRITE
	IF (RDPNTR.EQ.WRPNTR) GO TO DONE
	SHIPTO = BRACKS
	LOKCTL = 0
	XCALL IO (1,SHIPTO,WRPNTR,WRITE,LOKCTL)
	INCR WRPNTR
	GO TO EOF
DONE,
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,READ,LOKCTL)
	ORG171 = ORG171  - ORGDEL
	REC171  = REC171  - DELCNT
	DEL171  =
	RCNT = REC171 
	OCNT = ORG171 
	LOKCTL = 1
	XCALL IO (1,SHPCTL,1,WRITE,LOKCTL)
END,
	SWITCH = 3
	XCALL SNMSG (' ',SWITCH)
	IF (OCNT.NE.RCNT) XCALL SNMSG (SRTMSG,2)
	IF (OCNT.NE.RCNT) XCALL PGCHN ('CP:SRTSHT',0)
	XCALL FILES (1,'U',171 ,4)
NOMSG,
	XCALL PGCHN ('CP:SPCFUN',1)
END

