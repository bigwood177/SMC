;  PRTCRM / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PGM TO PRINT CREDIT MEMOS
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CRMHDR		
		.INCLUDE 'DEF:RD046A.DEF'
RECORD CRMLIN		
		.INCLUDE 'DEF:RD047A.DEF'
RECORD ,X		
		.INCLUDE 'DEF:RD047B.DEF'
RECORD CUSMAS		
		.INCLUDE 'DEF:RD001A.DEF'
RECORD DUMCUS  ,X	
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD PBUF
	PLINE	,A85
RECORD
	SAVQTY	,D4
	ADCNT2	,D4
	DECMAL	,D10
	LINCNT	,D2,	00
	PGCNT	,D3
	RPTNUM	,D3
	PRTTYP	,A1
	SPLFIL	,A14
	X	,D3
	PAGENO	,D2
	MASK	,A10,'ZZZ,ZZZ.XX'
	MASK1	,A4,'ZZZX'
	MASK2	,A6,'XXXXXX'
	MASK3	,A12,'ZZZ,ZZZ.XXCR'
	ENTRY	,A7
	INXCTL  ,D1
	CNGCTL	,D1
	WHATNO	,D1
	RECNO	,D5
	LRECNO	,D5,00001
	INVDAT	,D6
	TOTSAL	,D8
	STRTNA	,A10
	ENDNA	,A10
	STRTNO	,D6
	ENDNO	,D6
	STXCTL	,D1
	TAX	,D6
	BLANKS	,A25
	ROW	,D2,02
	SWITCH	,D1
	V	,D1
	BSEND	,D5
	BSND2	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A6
	SOLDTO	,4A29
	SHIPTO	,4A29
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	LPSW	,D2
	PRNTSW	,D1
	JUST	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,1,'PRINT CREDIT MEMOS',V)
	XCALL OUTPT (2,1,1,'\',1)
	SWITCH = 5
	XCALL FILES(2,'I',02,SWITCH)			;FILE # 02 -- CUSIDX FILE
	SWITCH = 5
	XCALL FILES(6,'I',46,SWITCH)			;FILE # 46 -- CRMHDR FILE
	SWITCH = 5
	XCALL FILES(7,'I',47,SWITCH)			;FILE # 47 -- CRMLIN FILE
	SWITCH = 5
	XCALL FILES(1,'I',01,SWITCH)			;FILE # 01 -- CUSMAS FILE
	SWITCH = 5
	XCALL FILES (10,'I',41,SWITCH)
	LOKCTL = 1
	XCALL IO (10,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	CLOSE 10
	LOKCTL = 1
	XCALL IO (1,CUSMAS,1,READ,LOKCTL)
	BSEND = ORG001
	LOKCTL = 1
	XCALL IO (7,CRMLIN,1,READ,LOKCTL)
	ADCNT2 = REC047
	BSND2 = ORG047
DATE,
	XCALL OUTPT (6,20,2,'PLEASE ENTER CREDIT MEMO DATE:',V)
	XCALL INPUT (06,52,06,00,'DE',ENTRY,INXCTL,V)
	GO TO (DATE,END5), INXCTL
	INVDAT = ENTRY (1,6)
	IF (ENTRY.EQ.BLANKS) CALL DFLDAT
	CNGCTL = 2
	XCALL ANYCN (CNGCTL,WHATNO)
	GO TO (DATE), CNGCTL
BEGIN,
	STXCTL = 1
	XCALL STENO(STRTNA,ENDNA,'CR MEMO #',6,STXCTL,V)
	GO TO (END2), STXCTL
	STRTNO = STRTNA
	IF (ENDNA.EQ.'[[[') ENDNO = 999999
	IF (ENDNA.NE.'[[[') ENDNO = ENDNA
	CLOSE 6
	SWITCH = 5
	XCALL FILES (6,'I',46,SWITCH)
	RECNO = 1
	LRECNO = 1
	CRMLIN =
RDHDR,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (6,CRMHDR,RECNO,READ,LOKCTL)
	IF (CCUSNM.EQ.']]]]]]') GOTO ENDHDR
	IF (CCRMNO.LT.STRTNO) GO TO RDHDR
	IF (CCUSNM.EQ.']]]CANCEL'.OR.CCRMDT.EQ.0) GO TO RDHDR
	IF (CCRMNO.GT.ENDNO .AND. PRNTSW.EQ.0) XCALL MESAG
&	('NO MEMOS MATCH THESE PARAMETERS',1)
	IF (CCRMNO.GT.ENDNO .AND. PRNTSW) CALL LPOFF
	IF (CCRMNO.GT.ENDNO) GO TO BEGIN
PRNTHD,
	PAGENO = 01
	LINCNT =
	TOTSAL =
	KEY = CCUSNO, MASK2
	XCALL SERCH (2,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	GOTO (NOCUST), SRCCTL
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	SOLDTO(1) = NAME
	SOLDTO(2) = ADD1
	SOLDTO(3) = ADD2
	SOLDTO (88,102) = CITY
	SOLDTO (104,105) = STATE
	SOLDTO (107,116) = ZIP
BUFSLD,
	IF (SOLDTO(2).NE.BLANKS) GOTO BUFSL2
	SOLDTO (2) = SOLDTO (3)
	SOLDTO (3) =
BUFSL2,
	IF (SOLDTO (3).NE.BLANKS) GO TO BUFSHP
	SOLDTO (3) = SOLDTO (4)
	SOLDTO (4) =
BUFSHP,
	SHIPTO (1) = CCUSNM
	SHIPTO (2) = CADD1
	SHIPTO (3) = CADD2
	SHIPTO (4) = CADD3
	IF (SHIPTO(2).NE.BLANKS) GOTO BUFSH2
	SHIPTO (2) = SHIPTO (3)
	SHIPTO (3) =
BUFSH2,
	IF (SHIPTO (3).NE.BLANKS) GO TO BUFSH3
	SHIPTO (3) = SHIPTO (4)
	SHIPTO (4) =
BUFSH3,
	CALL PRTHDR
	LINCNT =
	GOTO FNDITM
DFLDAT,
	XCALL RDATE(INVDAT)
	STRTNA(1,8) = INVDAT,'XX/XX/XX'
	XCALL OUTPT (6,52,0,STRTNA(1,8),V)
	RETURN
NXTPAG,
	LINCNT =
	INCR PAGENO
	XCALL LINFD (1)
	PLINE (72,84) = '**CONTINUED**'
	CALL PRINT
	PLINE (21,55) = CCOMNT(1)
	CALL PRINT
	PLINE (21,55) = CCOMNT(2)
	CALL PRINT
	XCALL LINFD (0)
	CALL PRTHDR
	LINCNT =
	RETURN
PRTHDR,
	IF (PRNTSW.EQ.0) CALL LPON
	PLINE =
	CALL PRINT
	XCALL LINFD (1)
	PLINE (73,78) = CCRMNO, MASK2
	PLINE (81,82) = PAGENO
	CALL PRINT
	XCALL LINFD (2)
	PLINE (74,81) = INVDAT, 'XX/XX/XX'
	CALL PRINT
	XCALL LINFD (8)
	PLINE (13,41) = SOLDTO(1)
	PLINE (51,79) = SHIPTO (1)
	CALL PRINT
	PLINE (13,41) = SOLDTO(2)
	PLINE (51,79) = SHIPTO (2)
	CALL PRINT
	PLINE (13,41) = SOLDTO(3)
	PLINE (51,79) = SHIPTO (3)
	CALL PRINT
	PLINE (13,41) = SOLDTO(4)
	PLINE (51,79) = SHIPTO (4)
	CALL PRINT
	XCALL LINFD (4)
	PLINE (3,8) = CAPLTO, MASK2
	IF (CAPLTO.EQ.-1) PLINE (3,8) = 'BAL-FW'
	IF (CAPLTO.EQ.0) PLINE (3,8) = 'OPN-CR'
	PLINE (11,18) = CCRMDT, 'XX/XX/XX'
	PLINE (21,26) = CCUSNO
	PLINE (29,30) = CLOC
	PLINE (33,34) = CSLMAN, MASK
	PLINE (37,46) = CCUSPO
	CALL PRINT
	XCALL LINFD (3)
	RETURN
FNDITM,
	KEY = CCRMNO, MASK2
	XCALL SERCH (7,CRMLIN,KEY,1,6,BSND2,BSMID,SRCCTL,4,1,6,0,0,0,0)
	GO TO (ORDTOT), SRCCTL
	LRECNO = BSMID
	LOKCTL = 1
	XCALL IO (7,CRMLIN,LRECNO,READ,LOKCTL)
ITEMS,
	IF (CLITEM.EQ.']]]]]]') GOTO ORDTOT
	IF (CLCRNO.EQ.CCRMNO) CALL PRTLIN
	IF (CLCRNO.GT.CCRMNO) GOTO ORDTOT
	INCR LRECNO
	LOKCTL = 1
	XCALL IO (7,CRMLIN,LRECNO,READ,LOKCTL)
	GOTO ITEMS
PRTLIN,
	IF (CLQTY.EQ.0) RETURN
	IF (CLDESC .EQ. ']]]CANCEL') RETURN
	SAVQTY = CLQTY
	IF (CLQTY.LT.0) SAVQTY = -SAVQTY
	IF (LINCNT.EQ.28) CALL NXTPAG
	PLINE (3,6) = SAVQTY, MASK1
	PLINE (21,35) = CLITEM
	IF (JUST) XCALL LEFTJ (PLINE(21,35),15)
	PLINE (42,50) = CLPRCE, MASK
	PLINE (53,54) = CLUOFM
	DECMAL = (SAVQTY*CLPRCE)
	PLINE (57,66) = DECMAL, MASK
	PLINE (69,70) = CLDISC
	DECMAL = DECMAL - ((DECMAL*CLDISC)/100)
	DECMAL = DECMAL - ((DECMAL*CDSCNT)/100)
	PLINE (72,84) = DECMAL, MASK3
	TOTSAL = TOTSAL + DECMAL
	CALL PRINT
	PLINE (21,50) = CLDESC
	CALL PRINT
	RETURN
PRINT,
	IF (PRNTSW.EQ.0) CALL LPON
	XCALL LPOUT (LINCNT,PGCNT,PBUF,'NO TITLE','NO HDR',' ',' ',
&	'NO LEGEND',' ',' ',0,85,85,0,LPSW,RPTNUM,PRTTYP)
	RETURN
LPON,
	LPSW = 5	;SPECIAL FORMS
	XCALL LPON(LPSW,SPLFIL)
	IF (LPSW.EQ.0) XCALL PGCHN ('CP:UNPRCM',1)
	XCALL WATE (2,V)
	PRNTSW = 1
	RETURN
LPOFF,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
	PGCNT =
	LINCNT =
	PRNTSW =
	RETURN
ORDTOT,
	X = 29 - LINCNT
	XCALL LINFD (X)
	IF (CDSCNT.EQ.0) GO TO NOCDSC
	PLINE (21,41) ='MEMO DISCOUNT PERCENT'
	PLINE (69,70) = CDSCNT
	CALL PRINT
NOCDSC,
	IF (CDSCNT.EQ.0) XCALL LINFD (1)
	XCALL LINFD (1)
	PLINE (72,84) = TOTSAL, MASK3
	CALL PRINT
	PLINE (21,55) = CCOMNT(1)
	CALL PRINT
	PLINE (21,55) = CCOMNT(2)
	PLINE (74,84) = CMISC, MASK3
	CALL PRINT
	PLINE (74,84) = CFRGHT, MASK3
	CALL PRINT
	TAX = CTAX(1) + CTAX(2) + CTAX(3)
	PLINE (73,84) = TAX, MASK3
	CALL PRINT
	XCALL LINFD (1)
	DECMAL = TOTSAL + CMISC + TAX + CFRGHT
	PLINE (72,84) = DECMAL, MASK3
	CALL PRINT
	XCALL LINFD (0)
	GO TO RDHDR
NOCUST,
	SOLDTO =
	SOLDTO (1) = 'CUSTOMER # NOT FOUND'
	GOTO BUFSHP
ENDHDR,
	IF (PRNTSW.EQ.0) XCALL MESAG
&	('NO MEMOS MATCH THESE PARAMETERS',1)
	IF (PRNTSW.EQ.0) GO TO BEGIN
	IF (PRNTSW) CALL LPOFF
	IF (ENDNA.NE.'[[[ ') GO TO BEGIN
END2,
	XCALL OUTPT (6,20,2,'ARE ALL CREDIT MEMOS OK ?',V)
	XCALL INPUT (06,47,01,01,'YN',ENTRY,INXCTL,V)
	GO TO (END3,END4), INXCTL
END3,
	CLOSE 7
	CLOSE 6
	XCALL FILES (2,'I',02,4)
	XCALL MOUNT ('REGULAR PAPER','PRINTER')
	XCALL WATE (3,V)
	XCALL PGCHN ('CP:CRMAR',1)
END4,
	XCALL OUTPT (7,20,0,'END OFF FOR FURTHER CHANGES ?',V)
	XCALL INPUT (07,52,01,01,'YN',ENTRY,INXCTL,V)
	GOTO (END5,BEGIN), INXCTL
END5,
	XCALL MOUNT('REGULAR PAPER','PRINTER')
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:UNPRCM',1)
END
