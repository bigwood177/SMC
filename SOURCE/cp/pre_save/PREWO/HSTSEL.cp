; HSTSEL / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SELECT DATES FOR POSTING AND/OR PURGING DETAIL SALES HISTORY
;		AND THEN MARK RECORDS TO BE POSTED
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SLSHST		
		.INCLUDE 'DEF:RD055A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD HSTCTL		
		.INCLUDE 'DEF:RD055B.DEF'
RECORD
	V	,D1
	DSPPER	,A2
	SSYRA	,A4
	SWITCH	,D1,1
	PSTDTA	,A6
	PSTDTD	,D6
	PRGDTA	,A6
	PRGDTD	,D6
	WRKDAT	,A6
	ENTRY	,A7
	INXCTL	,D1
	CNGCTL	,D1
	WHATNO	,D1
	RECIN	,D5	,00001
	COL2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	XCALL TERID(V)
	SWITCH = 1
	XCALL FILES (9,'I',58,SWITCH)
	IF (SWITCH.EQ.9) GO TO ENDOFF
	LOKCTL = 1
	XCALL IO (9,SLSDUM,1,READ,LOKCTL)
	XCALL FILES (9,'I',58,4)
DISPLA,
	XCALL OUTPT (1,1,2,'POST AND/OR PURGE SALES HISTORY',V)
	IF (SSPER.EQ.0) GO TO NODISP
	SSYRA = SSYEAR
	XCALL OUTPT (1,48,0,'CURRENT PERIOD: ',V)
	XCALL OUTPT (0,0,0,SSYRA,V)
	XCALL OUTPT (0,0,0,'-',V)
	XCALL OUTPT (0,0,0,SSDESC(SSPER),V)
NODISP,
	XCALL OUTPT (4,05,0,'ENTER THE INVOICE DATE THROUGH',V)
	XCALL OUTPT (0,0,0,' WHICH RECORDS WILL BE POSTED',V)
	XCALL OUTPT (5,10,0,'(PRESSING THE RETURN KEY MEANS DO NOT POST)',V)
	XCALL OUTPT (6,5,0,'ENTER THE INVOICE DATE THROUGH',V)
	XCALL OUTPT (0,0,0,' WHICH RECORDS WILL BE PURGED',V)
	XCALL OUTPT (7,10,0,'(PRESSING THE RETURN KEY MEANS DO NOT PURGE)',V)
PSTDAT,
	XCALL INPUT (04,66,06,00,'DE',ENTRY,INXCTL,V)
	GOTO (DISPLA,ENDOFF), INXCTL
	PSTDTD = ENTRY(1,6)
	PSTDTA(1,2) = PSTDTD(5,6),'XX'
	PSTDTA(3,6) = PSTDTD(1,4),'XXXX'
PRGDAT,
	XCALL INPUT (06,66,06,00,'D ',ENTRY,INXCTL,V)
	GO TO (DISPLA), INXCTL
	PRGDTD = ENTRY(1,6)
	PRGDTA(1,2) = PRGDTD(5,6),'XX'
	PRGDTA(3,6) = PRGDTD(1,4),'XXXX'
DSPERD,
	IF (PSTDTD.EQ.0) GO TO ANYCNG
	IF (SSPER.EQ.0) GO TO NOPERS
	DSPPER = SSPER,'ZX'
	XCALL OUTPT (9,05,0,'DATA WILL BE POSTED TO PERIOD ',V)
	XCALL OUTPT (0,0,0,DSPPER,V)
	XCALL OUTPT (0,0,0,'  (',V)
	XCALL OUTPT (0,0,0,SSDESC(SSPER),V)
	XCALL OUTPT (0,0,0,')',V)
ANYCNG,
	XCALL OUTPT (12,1,1,'ARE THE DATES OK ?',V)
	XCALL INPUT(12,21,01,01,'YN',ENTRY,INXCTL,V)
	XCALL OUTPT (12,1,1,'\',V)
	GO TO (DISPLA), INXCTL - 1
;******************************************************************************
	IF (PSTDTD.EQ.0 .AND. PRGDTD.EQ.0) GOTO ENDOFF
	IF (PRGDTD.NE.0) GO TO OPEN10
OPEN55,
	SWITCH = 3
	XCALL FILES (8,'U',55,SWITCH)		;FILE # 55 -- SLSHST FILE
	IF (SWITCH.EQ.9.AND.PRGDTD.NE.0) XCALL FILES (10,'I',41,4)
	IF (SWITCH.EQ.9) GOTO ENDOFF
	XCALL WATE(4,V)
	IF (PSTDTD.EQ.0) GOTO PURGE
	SWITCH = 5
	XCALL FILES(8,'U',55,SWITCH)
	LOKCTL = 1
	XCALL IO (8,HSTCTL,1,READ,LOKCTL)
	RECIN = 1
REDHST,
	INCR RECIN
	IF (RECIN.GT.REC055) GOTO CLOSE
	LOKCTL = 1
	XCALL IO (8,SLSHST,RECIN,READ,LOKCTL)
	IF (SLSHST(1,5).EQ.']]]]]') GOTO CLOSE
	IF (POSTED.NE.0) GO TO REDHST
	IF (HINVDT.EQ.0) GO TO REDHST
	WRKDAT(1,2) = HINVDT(5,6),'XX'
	WRKDAT(3,6) = HINVDT(1,4),'XXXX'
	IF (WRKDAT.GT.PSTDTA) GOTO REDHST
	POSTED = 1
	LOKCTL = 1
	XCALL IO(8,SLSHST,RECIN,WRITE,LOKCTL)
	GOTO REDHST
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO ENDOFF
CLOSE,
	SWITCH = 2
	IF (PRGDTD.NE.0) XCALL SNMSG (PRGDTA,SWITCH)
	CLOSE 8			;CLOSE FILE BUT LEAVE PROTECTED FOR POSTING PGM
	XCALL PGCHN ('CP:SSUPD ' ,1)
ENDOFF,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:SSMENU',1)
PURGE,
	XCALL WATE(3,V)
	SWITCH = 2
	XCALL SNMSG (PRGDTA,SWITCH)
	CLOSE 8			;CLOSE FILE BUT LEAVE PROTECTED FOR PURGE PGM
	XCALL PGCHN ('CP:PRGSLH',1)
OPEN10,
	SWITCH = 1
	XCALL FILES (10,'I',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO ENDOFF
	CLOSE 10
	GO TO OPEN55
	END
