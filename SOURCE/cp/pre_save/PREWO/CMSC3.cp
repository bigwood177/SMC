SUBROUTINE CMSC3
;
;  CMSC3 / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;		HANDLES THIRD SCREEN IN ADD MODE OF CRMEMO
;
	CRMHDR	,A
	CDETDS	,A
	CMLMSC	,A
	CDEFMS	,D
	CMLTAX	,A
	CDEFTX	,D
	CMLFRT	,A
	CDEFRT	,D
	TAXTOT	,D
	TXFLG	,A
	ARTCTL	,A
	V	,D
RECORD TCRMHD   	
		.INCLUDE 'DEF:RD046A.DEF'
RECORD ARACCT		
		.INCLUDE 'DEF:RD007A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD007B.DEF'
RECORD DUMCTL
		.INCLUDE 'DEF:RD169B.DEF'
RECORD ARTCDE
		.INCLUDE 'DEF:RD169A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	OPTION	,D1
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	KEY	,A7
	MASK	,A7,'XXXXXXX'
	MASK2	,A8,'XXXX-XXX'
	MASK3	,A6,'ZZZZZX'
	DCHAR	,D3
	TCHAR	,D3
	SRCHFL	,D1
	DEFACT	,D7
	ENTRY	,A10
	INXCTL  ,D1
	CNGCTL	,D1
	WHATNO	,D2
	COMPCT	,D4
	CRMNOA	,A6
	CUSNOA	,A6
	DECMAL	,D18
	TAX	,D6
	MSCTAX	,D6
	BLANKS	,A10
	ALPHA	,A8
	ABORT	,D1
	SYSTEM	,D1
	COL2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
PROC
	DUMCTL = ARTCTL
	XCALL ENVRN(SYSTEM)
	XCALL OUTPT (1,1,2,'\',V)
	LOKCTL = 1
	XCALL IO (5,ARACCT,1,READ,LOKCTL)
	BSEND = ORG007
	TCRMHD = CRMHDR
	CRMNOA = CCRMNO, MASK3
	CUSNOA = CCUSNO, MASK3
	XCALL CR3(CRMNOA,CUSNOA,CCUSNM,CMLMSC,CMLTAX,CMLFRT,V)
	CTL = '02,25,08'
	DECMAL = CSALAM
	CALL DSPDLR
	ROW = 3
	DECMAL = TAXTOT
	CALL DSPDLR
DISPL3,
	CNGCTL =
	CFRGHT =
	CMISC =
	CTAX =
	CMSACT = CDEFMS
	CTXACT = CDEFTX
	CFRACT = CDEFRT
FRGHT,
	CTL = '04,22,06,00,$ '
	CALL INPUT
	GO TO (DISPL3), INXCTL
	CFRGHT = ENTRY
	DEFACT = CFRACT
	IF (CMLFRT.NE.'Y'.OR.CFRGHT.EQ.0) GO TO FRGHT1
	CALL GETACT
	GO TO (DISPL3), INXCTL
FRGHT1,
	CFRACT = DEFACT
	GO TO (CRMTOT), CNGCTL
MISC,
	CTL = '05,22,06,00,$ '
	CALL INPUT
	GOTO (DISPL3), INXCTL
	CMISC = ENTRY
	DEFACT = CMSACT
	IF (CMLMSC.NE.'Y'.OR.CMISC.EQ.0) GO TO MISC1
	CALL GETACT
	GO TO (DISPL3), INXCTL
MISC1,
	CMSACT = DEFACT
	MSCTAX =
	IF (CSALAM.EQ.0) GOTO NOMTAX
	MSCTAX = ((TAXTOT*CMISC*10)/CSALAM) #1
NOMTAX,
	CTL = '06,25,06'
	DECMAL = MSCTAX
	CALL DSPDLR
	CTL = '07,22,07,00,$ '
;	IF (CNGCTL.EQ.1) GOTO CRMTOT
TAX,
	ARTCDE =
	LOKCTL = 1 
	XCALL IO (10,ARTCTL,1,READ,LOKCTL)
	CTL = '07,22,07,00,$-'
	IF (TXFLG.EQ.'   ') GO TO ASK		; NO  VALID TAX RATE OR NON-TAXABLE

	SRCCTL = 2
	XCALL SERCH (10,ARTCDE,TXFLG,1,3,ORG169,BSMID,SRCCTL,4,4,9,0,0,0,0)
	GO TO (ASK), SRCCTL		;TAX CODE NOT FOUND

	CTAX(1) = ((MSCTAX + TAXTOT) * ARTPRT(1) / 100) # 3
	CTAX(2) = ((MSCTAX + TAXTOT) * ARTPRT(2) / 100) # 3
	CTAX(3) = ((MSCTAX + TAXTOT) * ARTPRT(3) / 100) # 3
	TAX = CTAX(1) + CTAX(2) + CTAX(3)
	XCALL DSPLY (MAX,ROW,COL,TAX,3,V)	;Display default tax amount
	CALL ACCEPT
	IF (TCHAR.EQ.13) GO TO OK	
ASK,
	CALL INPUT
	GO TO (DISPL3), INXCTL
	CTAX(1) = ENTRY (1,7)
	CTAX(2) =
	CTAX(3) =
	TAX = CTAX(1)
OK,
	CTXACT(1) = ARTGLN(1)
	CTXACT(2) = ARTGLN(2)
	CTXACT(3) = ARTGLN(3)
	DEFACT    = ARTGLN(1)

TAX0,
	DEFACT = CTXACT
	IF (CMLTAX.NE.'Y'.OR.TAX.EQ.0) GO TO TAX1
	CALL GETACT
	GO TO (DISPL3), INXCTL
TAX1,
	CTXACT(1) = DEFACT

CRMTOT,
	CTL = '08,22,10'
	DECMAL = CSALAM + CFRGHT + CMISC + TAX
	CALL DSPDLR
	GO TO (ANYCN3), CNGCTL
COMMIS,
	CALL CLR45
	IF (CNGCTL.EQ.0) WHATNO =
	GO TO (COMAMT,COMPCT), WHATNO - 3
COMAMT,
	CTL = '09,22,07,00,$ '
	CALL INPUT
	GO TO (DISPL3), INXCTL
	IF (ENTRY.EQ.BLANKS) GO TO COMPCT
	CCOMDU = ENTRY
	GO TO ANYCN3
COMPCT,
	CTL = '10,22,04,00,# '
	CALL INPUT
	GO TO (DISPL3), INXCTL
	COMPCT = ENTRY(1,4)
	CCOMDU = (-1)*COMPCT
	CTL = '10,22,04'
	DECMAL = COMPCT
	CALL DSPDLR
	GO TO ANYCN3
GETACT,
	COL = 40
	SRCHFL = 1
	CALL SERCH
	SRCHFL =
	COL = 39
	CALL ACCEPT
	IF (TCHAR.EQ.13) RETURN
GETAC1,
	COL = 40
	XCALL OUTPT (ROW,40,1,'\',V)
	CALL ENTACT
	RETURN
ENTACT,
	XCALL OUTPT (ROW,COL,1,'\',V)
	XCALL GETAC (ROW,COL,DEFACT,INXCTL,V)
	IF (INXCTL.EQ.1) RETURN
	CALL SERCH
	IF (SRCCTL.EQ.0) RETURN
	GO TO ENTACT
ACCEPT,
	XCALL FLAGS (00010000,1)		;DISABLE CHARACTER ECHOING
	XCALL OUTPT (ROW,COL,0,'\',V)
	ACCEPT (15,TCHAR)
	IF (SYSTEM.NE.1.AND.TCHAR.EQ.13) ACCEPT (15,DCHAR)
	XCALL FLAGS (00010000,0)		;RE-ENABLE CHARACTER ECHOING
	RETURN
SERCH,
	KEY = DEFACT, MASK
	ALPHA(1,8) = DEFACT, MASK2
	XCALL SERCH (5,ARACCT,KEY,1,7,BSEND,BSMID,SRCCTL,4,32,37,0,0,0,0)
	IF (SRCCTL.EQ.1) CALL BADACT
	IF (SRCCTL.EQ.0) CALL DSPACT
	RETURN
BADACT,
	IF (SRCHFL.EQ.1) ARACDS = 'ACCOUNT NOT ON FILE'
	IF (SRCHFL.EQ.0) XCALL MESAG('ACCOUNT NOT ON FILE',1)
	IF (SRCHFL.EQ.1) SRCCTL = 0
	RETURN
DSPACT,
	XCALL OUTPT (ROW,40,0,ALPHA(1,8),V)
	XCALL OUTPT (ROW,50,0,ARACDS,V)
	RETURN
CLR45,
	XCALL OUTPT (9,22,1,'\',V)
	XCALL OUTPT (10,22,1,'\',V)
	RETURN
CNGBR3,
	GO TO (FRGHT,MISC,TAX,COMMIS,COMMIS), WHATNO
	XCALL MESAG (' ',6)
	GOTO ANYCN3
PRCES3,
	CRMHDR = TCRMHD
	RETURN
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCN3,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PRCES3,CNGBR3,DISPL3), CNGCTL+1
DSPNUM,
	OPTION = 1
	GOTO CALDSP
DSPDLR,
	OPTION = 3
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,V)
	RETURN
END
