;  CRMEDT / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PGM TO PRINT CREDIT MEMO EDIT LIST
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CRMHDR		
		.INCLUDE 'DEF:RD046A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD046B.DEF'
RECORD CRMLIN		
		.INCLUDE 'DEF:RD047A.DEF'
RECORD CUSMAS		
		.INCLUDE 'DEF:RD001A.DEF'
RECORD	,X		
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD COPCTL
		.INCLUDE 'DEF:RD060A.DEF'
RECORD TITLE
		,A21,'CREDIT MEMO EDIT LIST'
RECORD SUBHDR
		,A3
		,A11,'ITEM NUMBER'
		,A4
		,A16,'ITEM DESCRIPTION'
		,A17
		,A3,'QTY'
		,A2
		,A10,'UNIT-PRICE'
		,A1
		,A3,'UOM'
		,A2
		,A9,'EXT-PRICE'
		,A1
		,A4,'DISC'
		,A2
		,A9,'NET-PRICE'
		,A4
		,A14,'ADJ INVENTORY?'
RECORD PBUF
	PLINE	,A132
RECORD
	SOLDTO	,4A29
	SHIPTO	,4A29
	SAVQTY	,D4
	DECMAL	,D10
	TOTSAL	,D8
	LINCNT	,D2,60
	PGCNT	,D3
	MASK	,A10,'ZZZ,ZZZ.XX'
	MASK2	,A8,'XXXX-XXX'
	PAGENO	,D2
	PRTCTL	,D2
	BLANKS	,A25
	SPLFIL	,A14
	LPSW	,D2
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	SWITCH	,D1
	V	,D1
	KEY	,A6
	TAX	,D6
	BSMID	,D5
	BSEND	,D5
	SRCCTL	,D1
	LPONSW	,D1
	ADD4	,A29
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST	,D1
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'CREDIT MEMO EDIT LIST',V)
	SWITCH = 1
	XCALL FILES (10,'I',41,SWITCH)
	IF (SWITCH.EQ.9) XCALL PGCHN ('CP:CRMENT',1)	;FILE # 41 - ITMMAS
	LOKCTL =
	XCALL IO (10,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	XCALL FILES (10,'I',41,4)
	SWITCH = 1
	XCALL FILES(7,'I',47,SWITCH)		;FILE # 47 -- CRMLIN FILE
	IF (SWITCH.EQ.9) XCALL PGCHN ('CP:CRMENT',1)
	SWITCH = 1
	XCALL FILES(6,'I',46,SWITCH)		;FILE # 46 -- CRMHDR FILE
	IF (SWITCH.NE.9) GO TO OPEN1
	CALL CLOSE1
	XCALL PGCHN ('CP:CRMENT',1)
OPEN1,
	SWITCH = 1
	XCALL FILES(2,'I',02,SWITCH)			;FILE # 2 -- CUSIDX
	IF (SWITCH.NE.9) GO TO OPEN2
	CALL CLOSE2
	XCALL PGCHN ('CP:CRMENT',1)
OPEN2,
	SWITCH = 1
	XCALL FILES (1,'I',01,SWITCH)			;FILE # 1 -- CUSMAS
	IF (SWITCH.NE.9) GO TO OPEN3
	CALL CLOSE3
	XCALL PGCHN ('CP:CRMENT',1)
OPEN3,
	SWITCH = 1
	XCALL FILES (8,'I',60,SWITCH)			;FILE # 60 -- COPCTL
	IF (SWITCH.NE.9) GO TO BEGIN
	CALL CLOSE4
	XCALL PGCHN ('CP:CRMENT',1)
BEGIN,
	LOKCTL = 1
	XCALL IOS (6,CRMHDR,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDHDR
	LOKCTL = 1
	XCALL IO (8,COPCTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,CUSMAS,1,READ,LOKCTL)
	BSEND = ORG001
RDHDR,
	LINCNT = 60
	LOKCTL = 1
	XCALL IOS (6,CRMHDR,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO ENDHDR
	IF (CCUSNM.EQ.']]]]]]') GOTO ENDHDR
	IF (CCUSNM.EQ.']]]CANCEL'.OR.CCRMDT.EQ.0) GO TO RDHDR
	KEY = CCUSNO,'XXXXXX'
	XCALL SERCH (2,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	GO TO (PRNTHD),SRCCTL
	IF(CCUSNO.EQ.999999) GO TO PRNTHD
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	ADD4(1,15) = CITY
	ADD4(17,18) = STATE
	ADD4(19,19) =
	ADD4(16,16) =
	ADD4(20,29) = ZIP
	PAGENO = 1
PRNTHD,
	CALL BUFFUP
	TOTSAL =
	CALL PRTHDR
	PBUF = SUBHDR
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	CLOSE 7
	SWITCH = 5
	XCALL FILES(7,'I',47,SWITCH)
	LOKCTL = 1
	XCALL IOS (7,CRMLIN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO CRMTOT
	GOTO ITEMS
PRTHDR,
	PLINE (1,14) = 'CREDIT MEMO #:'
	PLINE (16,21) = CCRMNO
	PLINE (32,39) = 'SOLD TO:'
	PLINE (41,69) = SOLDTO (1)
	PLINE (72,82) = 'SHIPPED TO:'
	PLINE (84,112) = SHIPTO (1)
	CALL PRINT
	PLINE (6,14) = 'APPLY TO:'
	PLINE (16,21) = CAPLTO
	IF (CAPLTO.EQ.-1) PLINE (16,21) = 'BAL-FW'
	IF (CAPLTO.EQ.0) PLINE (16,21) = 'OPN-CR'
	PLINE (41,69) = SOLDTO (2)
	PLINE (84,112) = SHIPTO (2)
	CALL PRINT
	PLINE (2,14) = 'CR MEMO DATE:'
	PLINE (16,23) = CCRMDT,'XX/XX/XX'
	PLINE (41,69) = SOLDTO (3)
	PLINE (84,112) = SHIPTO (3)
	CALL PRINT
	PLINE (41,69) = SOLDTO (4)
	PLINE (84,112) = SHIPTO (4)
	CALL PRINT
	XCALL LINFD (1)
	LINCNT = LINCNT + 4
	PLINE (1,7) = 'CUST #:'
	PLINE (10,15) = CCUSNO
	PLINE (19,27) = 'SALESMAN:'
	PLINE (29,30) = CSLMAN, MASK
	PLINE (35,46) = 'PURCH-ORD #:'
	PLINE (48,57) = CCUSPO
	PLINE (62,76) = 'MEMO DISCOUNT:'
	PLINE (77,78) = CDSCNT, 'ZX'
	PLINE (79,79) = '%'
	PLINE (83,91) = 'LOCATION:'
	PLINE (93,94) = CLOC
	IF (DSTFLG.NE.'Y') GO TO SKIP0
	PLINE (97,107) = 'A/R ACCT-#:'
	PLINE (109,116) = CARACT, MASK2
SKIP0,
	CALL PRINT
	XCALL LINFD (2)
	LINCNT = LINCNT + 2
	RETURN
BUFFUP,
	SOLDTO (1) = NAME
	SOLDTO (2) = ADD1
	SOLDTO (3) = ADD2
	SOLDTO (4) = ADD4
	SHIPTO (1) = CCUSNM
	SHIPTO (2) = CADD1
	SHIPTO (3) = CADD2
	SHIPTO (4) = CADD3
BUFSLD,
	IF (SOLDTO(2).NE.BLANKS) GOTO BUFSL2
	SOLDTO (2) = SOLDTO (3)
	SOLDTO (3) =
BUFSL2,
	IF (SOLDTO (3).NE.BLANKS) RETURN
	SOLDTO (3) = SOLDTO (4)
	SOLDTO (4) =
BUFSHP,
	IF (SHIPTO(2).NE.BLANKS) GOTO BUFSH2
	SHIPTO (2) = SHIPTO (3)
	SHIPTO (3) =
BUFSH2,
	IF (SHIPTO (3).NE.BLANKS) RETURN
	SHIPTO (3) = SHIPTO (4)
	SHIPTO (4) =
	RETURN
ITEMS,
	IF (CLITEM.EQ.']]]]]]') GOTO CRMTOT
	IF (CLCRNO.EQ.CCRMNO) CALL PRTLIN
	LOKCTL = 1
	XCALL IOS (7,CRMLIN,READ,LOKCTL)
	IF (LOKCTL.EQ.2) GO TO CRMTOT
	GOTO ITEMS
PRTLIN,
	IF (CLQTY.EQ.0) RETURN
	IF (CLDESC .EQ. ']]]CANCEL') RETURN
	SAVQTY = CLQTY
	IF (CLQTY.LT.0) SAVQTY = (-1)*SAVQTY
	PLINE (2,16) = CLITEM
	IF (JUST) XCALL LEFTJ (PLINE(2,16),15)
	PLINE (19,48) = CLDESC
	PLINE (50,54) = SAVQTY,'Z,ZZX'
	PLINE (57,65) = CLPRCE,MASK
	PLINE (68,69) = CLUOFM
	DECMAL = CLPRCE * SAVQTY
	PLINE (72,81) = DECMAL, MASK
	PLINE (84,85) = CLDISC
	DECMAL = DECMAL - ((DECMAL*CLDISC)/100)
	DECMAL = DECMAL - ((DECMAL*CDSCNT)/100)
	TOTSAL = TOTSAL + DECMAL
	PLINE (88,97) = DECMAL, MASK
	PLINE (108,108) = 'Y'
	IF (CLQTY.GT.0) PLINE (108,108) = 'N'
	CALL PRINT
	RETURN
;****************************************************************
PRINT,
	IF (LPONSW.EQ.0) CALL PRNTON
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,'NO HDR',
&		' ',' ','NO LEGEND',' ',' ',0,0,132,0,LPSW,RPTNUM,PRTTYP)
	RETURN
PRNTON,
	SPLFIL (5,6)  = 'ED'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO ENDHDR
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	LPONSW = 1
	RETURN
;****************************************************************
CRMTOT,
	LINCNT = LINCNT - 1
	XCALL LINFD (2)
	PLINE (67,85) = 'CREDIT MEMO AMOUNT:'
	PLINE (88,97) = TOTSAL, MASK
	CALL PRINT
	XCALL LINFD (1)
	PLINE (1,9) = 'COMMENTS:'
	PLINE (11,45) = CCOMNT(1)
	PLINE (73,85) = 'MISC CHARGES:'
	PLINE (90,97) = CMISC, MASK
	IF (DSTFLG.NE.'Y'.OR.CMISC.EQ.0) GO TO SKIP1
	PLINE (101,107) = 'ACCT-#:'
	PLINE (109,116) = CMSACT, MASK2
SKIP1,
	CALL PRINT
	PLINE (11,45) = CCOMNT(2)
	PLINE (78,85) = 'FREIGHT:'
	PLINE (90,97) = CFRGHT, MASK
	IF (DSTFLG.NE.'Y'.OR.CFRGHT.EQ.0) GO TO SKIP2
	PLINE (101,107) = 'ACCT-#:'
	PLINE (109,116) = CFRACT, MASK2
SKIP2,
	TAX = CTAX(1) + CTAX(2) + CTAX(3)
	CALL PRINT
	PLINE (76,85) = 'SALES TAX:'
	PLINE (89,97) = CTAX(1), MASK
	IF (DSTFLG.NE.'Y'.OR.CTAX(1).EQ.0) GO TO SKIP3
	PLINE (101,107) = 'ACCT-#:'
	PLINE (109,116) = CTXACT(1), MASK2
	CALL PRINT
	IF (CTAX(2).NE.0) PLINE (101,107) = 'ACCT-#:'
	IF (CTAX(2).NE.0) PLINE (89,97) = CTAX(2), MASK
	IF (CTAX(2).NE.0) PLINE (109,116) = CTXACT(2), MASK2
	CALL PRINT
	IF (CTAX(3).NE.0) PLINE (101,107) = 'ACCT-#:'
	IF (CTAX(3).NE.0) PLINE (89,97) = CTAX(3), MASK
	IF (CTAX(3).NE.0) PLINE (109,116) = CTXACT(3), MASK2
	CALL PRINT
	IF (CTAX(2).NE.0 .OR. CTAX(3).NE.0) XCALL LINFD(1)
	IF (CTAX(2).NE.0 .OR. CTAX(3).NE.0) INCR LINCNT
SKIP3,
	CALL PRINT
	PLINE (1,15) = 'COMMISSION AMT:'
	IF (CCOMDU.GE.0) PLINE (17,25) = CCOMDU, MASK
	PLINE (30,44) = 'COMMISSION PCT:'
	IF (CCOMDU.LT.0) PLINE (46,50) = CCOMDU*(-1), MASK
	CALL PRINT
	DECMAL = TOTSAL + CMISC + TAX + CFRGHT
	PLINE (80,85) = 'TOTAL:'
	PLINE (88,97) = DECMAL, MASK
	CALL PRINT
	XCALL LINFD (3)
	LINCNT = LINCNT + 3
	GO TO RDHDR
ENDHDR,
	XCALL LPOFF(LPSW,SPLFIL,PGCNT)
	CALL CLOSES
	XCALL PGCHN ('CP:CRMENT',1)
CLOSES,
	XCALL FILES (8,'I',60,4)
CLOSE4,
	XCALL FILES(1,'I',01,4)
CLOSE3,
	XCALL FILES(2,'I',02,4)
CLOSE2,
	XCALL FILES(6,'I',46,4)
CLOSE1,
	XCALL FILES(7,'I',47,4)
	RETURN
END
