;  SSUPD / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		POST DETAIL SALES HISTORY TO SALES SUMMARY FILE
;		UPDATE SLSSUM AND SLSIDX FILES
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SLSHST		
		.INCLUDE 'DEF:RD055A.DEF'
RECORD HSTCTL		
		.INCLUDE 'DEF:RD055B.DEF'
RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD BADMSG
		,A9,'INDEX IS '
	KEY	,A21
		,A12,' SUMMARY IS '
	SUMKEY	,A21
RECORD SNDMSG
		,A3,'CP:'
	PRGNM	,A6,'SSCNT '
	RCNT	,D5
	OCNT	,D5
		,D3,059
		,A3
RECORD NXTMSG
	MESSAG	,A6
RECORD RCVMSG
		,A6
RECORD
	WRTCNT	,D6
	FULLSW	,D1,0
	SWITCH	,D1
	V	,D1
	IDXCNT	,D5,00001
	IDXRD	,D5
	MASK	,A6,'XXXXXX'
	RECNO	,D5,00001
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	BLANKS	,A10
PROC
	XCALL TERID(V)
	XCALL OUTPT (2,1,1,'POST DETAIL TO SUMMARY FILE',1)
	SWITCH = 1
	XCALL SNMSG (RCVMSG,SWITCH)
	IF (SWITCH.EQ.9) GOTO GOON
	SWITCH = 3
	XCALL SNMSG (BLANKS,SWITCH)
GOON,
	NXTMSG = RCVMSG
	SWITCH = 5
	XCALL FILES(7,'U',55,SWITCH)		; FILE # 55 --SALES HISTORY FILE
	SWITCH = 2
	XCALL FILES(8,'U',58,SWITCH)		; FILE # 58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO INUSE
	SWITCH = 2
	XCALL FILES(9,'U',59,SWITCH)		; FILE # 59 -- SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	LOKCTL = 1
	XCALL IO (7,HSTCTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (8,SLSDUM,1,READ,LOKCTL)
	WRTCNT = REC058
;**********************************************************************
;		READ SALES HISTORY FILE & (IF NEEDED)
;		FIND CORRESPONDING INDEX RECORD
READ,
	INCR RECNO
	IF (RECNO.GT.MAX055) GOTO EOF
	LOKCTL = 1
	XCALL IO (7,SLSHST,RECNO,READ,LOKCTL)
	IF (HITMNO.EQ.']]]]]') GO TO EOF
	IF (POSTED.NE.1) GOTO READ
	KEY(1,6) = HCUSNO,MASK
	KEY(7,21) = HITMNO
CHECK,
	IF (KEY.EQ.SLSIDX(1,21)) GOTO UPDATE
	IF (KEY.LT.SLSIDX(1,21)) GO TO ADD
READIX,
	INCR IDXCNT
	IF (IDXCNT.GT.REC058) SLSIDX(1,21) = ']]]]]]]]]]]]]]]]]]]]]'
	IF (IDXCNT.GT.REC058) GO TO ADD
	LOKCTL = 1
	XCALL IO (9,SLSIDX,IDXCNT,READ,LOKCTL)
	IDXRD = IDXCNT
	IF (IRC058.EQ.0) GOTO READIX
	GOTO CHECK
;**********************************************************************
;		CREATE NEW RECORDS IN SLSSUM AND SLSIDX FILES
ADD,
	INCR WRTCNT
	IF (WRTCNT.GT.MAX058) GO TO FULL
	SLSSUM =
	SLSIDX =
	SSCUS = HCUSNO
	SSPROD = HITMNO
	SSSALE = HSALE
	SSUNIT = HQTY
	SSCOST = HCOST
	SICUS = HCUSNO
	SIPROD = HITMNO
	SICCD= HCUSCD
	CALL IDXUPD
	IRC058 = WRTCNT
	POSTED = 2
	LOKCTL = 1
	XCALL IO (7,SLSHST,RECNO,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO(8,SLSSUM,WRTCNT,WRITE,LOKCTL)
	LOKCTL = 1
	XCALL IO(9,SLSIDX,WRTCNT,WRITE,LOKCTL)
	IDXRD = WRTCNT
	IDXCNT = IDXCNT - 1
	GO TO READ
;**********************************************************************
;		UPDATE SALES TOTALS IN SLSSUM AND SLSIDX FILES
UPDATE,
	LOKCTL = 1
	XCALL IO (9,SLSIDX,IDXRD,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (8,SLSSUM,IRC058,READ,LOKCTL)
	SUMKEY(1,6) = SSCUS,MASK
	SUMKEY(7,21) = SSPROD
	IF (SUMKEY.NE.KEY) CALL BADFIL
	SSSALE = SSSALE + HSALE
	SSUNIT = SSUNIT + HQTY
	SSCOST = SSCOST + HCOST
	LOKCTL = 1
	XCALL IO (8,SLSSUM,IRC058,WRITE,LOKCTL)
	CALL IDXUPD
	LOKCTL = 1
	XCALL IO (9,SLSIDX,IDXRD,WRITE,LOKCTL)
	POSTED = 2
	LOKCTL = 1
	XCALL IO (7,SLSHST,RECNO,WRITE,LOKCTL)
	GO TO READ

;**********************************************************************
EOF,
	RCNT = WRTCNT
	OCNT = ORG058
	REC058 = WRTCNT
	LOKCTL = 1
	XCALL IO (8,SLSDUM,1,WRITE,LOKCTL)
	CLOSE 7			;CLOSE, BUT LEAVE SLSHST PROTECTED FOR PURGING
	CLOSE 8			;CLOSE, BUT LEAVE SLSSUM PROTECTED FOR UPDATING
				;	OF RECORD COUNTS
	CLOSE 9			;CLOSE, BUT LEAVE SLSIDX FILE PROTECTED FOR SORT
	SWITCH = 5
	XCALL SNMSG (SNDMSG,SWITCH)
	SWITCH = 2
	XCALL SNMSG (NXTMSG,SWITCH)
	XCALL PGCHN ('CP:SRTSIX',0)
FULL,
	REC058 = WRTCNT - 1
	LOKCTL = 1
	XCALL IO (8,SLSDUM,1,WRITE,LOKCTL)
	FULLSW = 1
	XCALL OUTPT (5,20,0,'THE SALES SUMMARY FILE MUST BE EXPANDED',V)
	XCALL OUTPT (6,20,0,'BEFORE POSTING CAN BE COMPLETED. PLEASE',V)
	XCALL OUTPT (7,20,0,'RUN THE "XPAND" PROGRAM AND EXPAND THE',V)
	XCALL OUTPT (8,20,0,'"SLSSUM" FILE AND THEN START THE POSTING',V)
	XCALL OUTPT (9,20,0,'OVER AGAIN. SALES HISTORY RECORDS ALREADY',V)
	XCALL OUTPT (10,20,0,'POSTED WILL NOT BE POSTED AGAIN.',V)
	XCALL MESAG(' ',2)
	XCALL FILES (9,'U',59,4)
INUSE2,
	XCALL FILES (8,'U',58,4)
INUSE,
	XCALL FILES (7,'U',55,4)
	IF (FULLSW.EQ.0) XCALL MESAG('FILES ARE IN USE - TRY LATER',1)
	XCALL PGCHN ('CP:SSMENU',1)
BADFIL,
	XCALL MESAG(BADMSG,2)
	RETURN
;**************************************
IDXUPD,
	SICCD = HCUSCD
	SICCD2 = HCUSC2
	SIPCD = HPRDCD
	SIPCD2 = HPRDC2
	SITERR = HTERR
	SISMAN = HSLSMN
	SILOC = HLOC
	RETURN
END
