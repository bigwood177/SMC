NO LONGER USED
;MISPRT.COP - PROGRAM TO PRINT OUT MISITM FILE
;
;
RECORD TITLE
		,A34	,'MISCELLANEOUS ITEMS FILE PRINT OUT'
;
RECORD HDR1
		,A15	,'CUSTOMER NUMBER'
		,A5	
		,A4	,'NAME'
		,A26
		,A12	,'ORDER NUMBER'
		,A2
		,A12	,'INVOICE DATE'
		,A5
		,A11	,'ORDER DATE'
RECORD HDR2
		,A10	,'ITEM DESC.'	
		,A20
		,A8	,'QTY ORD.'
		,A5
		,A5	,'PRICE'
		,A3
	HDR2A	,A10	,'LINE DISC.'
		,A3
	HDR2B	,A12	,'MISC. CHARGE'
		,A3
	HDR2C	,A14	,'FREIGHT CHARGE'
		,A3
	HDR2D	,A9	,'SALES TAX'
		,A3
	HDR2E	,A11	,'ORDER DISC.'
		,A3
		,A10	,'ITEM TOTAL'

RECORD MISITM
		.INCLUDE 'DEF:RD172A.DEF'
RECORD DUMMIS
		.INCLUDE 'DEF:RD172B.DEF'

RECORD
	V	,D1
	SWITCH	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	PGCNT	,D4
	CNT 	,D5
	PLINE	,A132
	LPSW	,D1
	SPLFIL	,A14
	RPTNUM	,D4
	PRTTYP	,D4
	LINCNT	,D4
	LNFEED	,D4
	PRNTSW	,D1
	PRTCTL	,D1
	EXPRCE	,D8
	TOTVAL	,D10
	INXCTL	,D1
	ENTRY	,A10
	FULDET	,D1,0

PROC
	XCALL TERID (V)
OPEN,
	SWITCH = 1
	XCALL FILES (2,'I',172,SWITCH)
	IF (SWITCH.EQ.9) GOTO EXIT
	LPSW = 1
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GOTO ABORT
	CNT =
	LINCNT = 66
	XCALL OUTPT ( 12,10,0,'PRINT FULL ORDER/CUSTOMER DETAIL <N> ?',1)
	XCALL INPUT ( 12,51,01,00,'A',ENTRY,INXCTL,1 )
	FULDET = 0
	IF ( ENTRY( 1,1 ).EQ.'Y' )
		BEGIN
			FULDET = 1
		END
	IF ( FULDET.EQ.0 )
		BEGIN
			HDR1 =
			HDR2A =
			HDR2B =
			HDR2C =
			HDR2D =
			HDR2E =
		END
	GOTO BEGIN

CLOSE,
	XCALL LINFD( 2 )
	PLINE =
	PLINE ( 80,114 ) = 'TOTAL VALUE OF MISCELLANEOUS ITEMS'
	PLINE ( 122,132 ) = TOTVAL,'ZZZ,ZZZ.ZX-'
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
ABORT,
	XCALL FILES (2,'I',172,4)
EXIT,
	XCALL PGCHN ('CP:CPMENU',1)
BEGIN,
	INCR CNT
	XCALL IOS (2,MISITM,READ,LOKCTL)
	IF ( CNT.EQ.1)GOTO BEGIN
	IF ( MISITM.EQ.']]]]')GOTO CLOSE
	IF ( MCOST.EQ.0 ) GOTO BEGIN
;	 Don't print items that are zero
;
	PLINE =
	PLINE (4,10) = MCUSNO
	PLINE (16,40) = MCUSNM
	PLINE (52,59) = MORDNO
	PLINE (66,74) = MINVDT,'XX/XX/XX'
	PLINE (82,90) = MORDDT,'XX/XX/XX'
	IF ( FULDET.EQ.1 ) CALL PRINT
		; Only print the full detail if they want to
	PLINE =
	PLINE (1,29) = MDESCR
	PLINE (31,36) = MQTYOR,'ZZZZZ-'
	PLINE (38,50) = MCOST,'ZZZ,ZZZ.ZZX'

	IF ( FULDET.EQ.1 )
		BEGIN
			PLINE (56,58) = MDISC,'ZX%'
			PLINE (67,75) = MMISC ,'Z,ZZZ.ZX-'
			PLINE (81,90) = MFRGHT,'Z,ZZZ.ZX-'
			PLINE (97,106) = MTAX,'ZZ,ZZZ.ZX-'
			PLINE (113,115) = MODISC,'ZX%'
		END
	EXPRCE = (((MQTYOR)*MCOST)#1)
&		-(((MQTYOR)*(MCOST*MDISC)#1)/100)
	PLINE (122,132) = EXPRCE,'ZZZ,ZZZ.ZX-'
	CALL PRINT
;	XCALL LINFD (1)
;	INCR LINCNT
	TOTVAL = TOTVAL + EXPRCE
	GOTO BEGIN
PRINT,
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,HDR2,'NO HDR',
&		'NO LEGEND',' ',' ',0,132,PRTCTL,0,LPSW,RPTNUM,PRTTYP)
	RETURN
END
