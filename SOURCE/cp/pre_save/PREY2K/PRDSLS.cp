;  PRDSLS / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SALES COMPARISON BY PRODUCT
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD INVMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX  		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD TITLE
		,A27,'SALES COMPARISON BY PRODUCT'
RECORD LEGEND
		,A48
		,A14,'************* '
	MONTH1	,A10
		,A13,'*************'
		,A9
		,A20,'********** YTD THRU '
	MONTH2	,A10
		,A7,' ******'
RECORD LGND2
		,A46
		,A7,'-------'
	LYR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	LYR2	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR2	,D4
		,A6,'------'
RECORD HDR1
		,A1
		,A9,'ITEM # / '
		,A1
		,A11,'DESCRIPTION'
		,A6
		,A8,'PROD CAT'
		,A21
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A1
RECORD PRDTOT
	PRDMLU	,D7
	PRDMU	,D7
	PRDYLU	,D7
	PRDYU	,D7
	PRDMLS	,D9
	PRDMS	,D9
	PRDYLS	,D9
	PRDYS	,D9
	PRDMLC	,D9
	PRDMC	,D9
	PRDYLC	,D9
	PRDYC	,D9
RECORD GRPTOT
	GRPMLU	,D7
	GRPMU	,D7
	GRPYLU	,D7
	GRPYU	,D7
	GRPMLS	,D9
	GRPMS	,D9
	GRPYLS	,D9
	GRPYS	,D9
	GRPMLC	,D9
	GRPMC	,D9
	GRPYLC	,D9
	GRPYC	,D9
RECORD GRDTOT
	GRDMLU	,D7
	GRDMU	,D7
	GRDYLU	,D7
	GRDYU	,D7
	GRDMLS	,D9
	GRDMS	,D9
	GRDYLS	,D9
	GRDYS	,D9
	GRDMLC	,D9
	GRDMC	,D9
	GRDYLC	,D9
	GRDYC	,D9
RECORD DASHES
		,A50,'--------------------------------------------------'
		,A50,'--------------------------------------------------'
		,A32,'--------------------------------'
RECORD
	RATIO1	,D5
	RATIO2	,D5
	RATIO3	,D5
	RATIO4	,D5
	PRATO1	,D5
	PRATO2	,D5
	PRATO3	,D5
	PRATO4	,D5
	GRATO1	,D5
	GRATO2	,D5
	GRATO3	,D5
	GRATO4	,D5
	OLDPRD	,A15
	OLDPCD	,A2
	LINCNT	,D2,60
	CNGCTL	,D1
	IDXCNT	,D5
	PGCNT	,D6
	PLINE	,A132
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	SPLFIL	,A14
	LPARG	,D1
	RPTNUM	,D3
	PRTTYP	,A1
	LSTSW	,D1
	FRSTSW	,D1
	KEY	,A15
	MASK	,A13,'Z,ZZZ,ZZZ.ZX-'
	MASK1	,A10,'Z,ZZZ,ZZX-'
	MASK2	,A7,'ZZZ.X%-'
	MASK3	,A14,'ZZ,ZZZ,ZZZ.ZX-'
	LPSW	,D2
	SWITCH	,D1
	PRTCTL	,D3
	ROW	,D2
	WHATNO	,D2
	V	,D1
	CTR1	,D4
	CTR2	,D4
	UNT	,A4,'UNT:'
	SLS	,A4,'SLS:'
	CST	,A4,'CST:'
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST	,D1
PROC
	XCALL TERID(V)
	XCALL OUTPT (2,1,2,'\',1)
	SPLFIL (5,6) = 'ER'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO INUSE2
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	SWITCH = 5
	XCALL FILES(7,'I',57,SWITCH)			; SORTED SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE
	SWITCH = 1
	XCALL FILES(8,'I',58,SWITCH)		; FILE # 58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	XCALL FILES(2,'I',42,SWITCH)		; FILE # 42 -- INVIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3
	XCALL FILES(1,'I',41,SWITCH)		; FILE # 41 -- INVMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE4
	LOKCTL = 1
	XCALL IO(1,DUMINV,1,READ,LOKCTL)
	JUST = JSTIFY
	BSEND = ORG041
	LOKCTL = 1
	XCALL IO(8,SLSDUM,1,READ,LOKCTL)
	IF (SSPER.EQ.0) GO TO NOPERS
	YR1 = SSYEAR
	YR2 = SSYEAR
	LYR1 = SSYEAR - 1
	LYR2 = SSYEAR - 1
	MONTH1 = SSDESC(SSPER)
	MONTH2 = SSDESC(SSPER)
;*********************************************************************************
;		READ SORTED SLSIDX FILE
	XCALL WATE(2,V)
READ,
	INCR IDXCNT
	LOKCTL = 1
	XCALL IO (7,SLSIDX,IDXCNT,READ,LOKCTL)
	IF (SIPROD.EQ.']]]]]') LSTSW = 1
	IF (LSTSW.EQ.1) GO TO PRDTOT
	IF (IRC058.EQ.0) GO TO READ
	IF (SIPROD.NE.OLDPRD.AND.FRSTSW) CALL PRDTOT
	IF (ITEMNO.EQ.SIPROD) GO TO SAMEP
	OLDPRD = SIPROD
	OLDPCD = SIPCD
	KEY = SIPROD
	XCALL SERCH (2,INVIDX,KEY,1,15,BSEND,BSMID,SRCCTL,4,16,20,0,0,0,0)
	GO TO (NOINV),SRCCTL
	LOKCTL = 1
	XCALL IO(1,INVMAS,IRC041,READ,LOKCTL)
;**********************************************************************************
;		CALCULATE SAME PRODUCT TOTALS
SAMEP,
	LOKCTL = 1
	XCALL IO (8,SLSSUM,IRC058,READ,LOKCTL)
	FRSTSW = 1
	IF (SSPER.EQ.1) CALL FIRSTP
	IF (SSPER.EQ.2) CALL SECNDP
	IF (SSPER.GT.2) CALL OTHERP
	GO TO READ
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO END
FIRSTP,
	PRDMLU = PRDMLU + SSUYTD(1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(1)
	PRDMLC = PRDMLC + SSCYTD(1)
	PRDMC = PRDMC + SSCOST
	PRDMS = PRDMS + SSSALE
	PRDYLU = PRDYLU + SSUYTD(1)
	PRDYU = PRDYU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(1)
	PRDYS = PRDYS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(1)
	PRDYC = PRDYC + SSCOST
	RETURN
SECNDP,
	PRDMLU = PRDMLU + SSUYTD(2) - SSUYTD(1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(2) - SSSYTD(1)
	PRDMS = PRDMS + SSSALE
	PRDMLC = PRDMLC + SSCYTD(2) - SSCYTD(1)
	PRDMC = PRDMC + SSCOST
	PRDYLU = PRDYLU + SSUYTD(2)
	PRDYU = PRDYU + SSLSTU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(2)
	PRDYS = PRDYS + SSLSTS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(2)
	PRDYC = PRDYC + SSLSTC + SSCOST
	RETURN
OTHERP,
	PRDMLU = PRDMLU + SSUYTD(SSPER) - SSUYTD(SSPER - 1)
	PRDMU = PRDMU + SSUNIT
	PRDMLS = PRDMLS + SSSYTD(SSPER) - SSSYTD(SSPER - 1)
	PRDMS = PRDMS + SSSALE
	PRDMLC = PRDMLC + SSCYTD(SSPER) - SSCYTD(SSPER - 1)
	PRDMC = PRDMC + SSCOST
	PRDYLU = PRDYLU + SSUYTD(SSPER)
	PRDYU = PRDYU + SSUYTD(SSPER - 2) + SSLSTU + SSUNIT
	PRDYLS = PRDYLS + SSSYTD(SSPER)
	PRDYS = PRDYS + SSSYTD(SSPER - 2) + SSLSTS + SSSALE
	PRDYLC = PRDYLC + SSCYTD(SSPER)
	PRDYC = PRDYC + SSCYTD(SSPER - 2) + SSLSTC + SSCOST
	RETURN
;*****************************************************************************
;		PRINT SAME PRODUCT TOTALS
PRDTOT,
	CALL PRINT
	PLINE (6,20) = SSPROD
	IF (JUST) XCALL LEFTJ (PLINE(6,20),15)
	PLINE (31,32) = PRDCAT
	PLINE (38,41) = UNT
	PLINE (43,52) = PRDMLU,MASK1
	PLINE(63,75) = PRDMU,MASK1
	PLINE(88,98) = PRDYLU,MASK1
	PLINE(112,121) = PRDYU,MASK1
	CALL PRINT
	PLINE (7,36) = DESCR
	PLINE (38,41) = SLS
	PLINE (43,55) = PRDMLS,MASK
	PLINE (66,78) = PRDMS,MASK
	PLINE (89,101) = PRDYLS,MASK
	PLINE (112,124) = PRDYS,MASK
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = PRDMLC,MASK
	RATIO1 =
	IF (PRDMLS.NE.0) RATIO1 = (PRDMLS - PRDMLC) * 1000 / PRDMLS
	PLINE (57,63) = RATIO1,MASK2
	PLINE (66,78) = PRDMC,MASK
	RATIO2 =
	IF (PRDMS.NE.0) RATIO2 = (PRDMS - PRDMC) * 1000 / PRDMS
	PLINE (80,86) = RATIO2,MASK2
	PLINE (89,101) = PRDYLC,MASK
	RATIO3 =
	IF (PRDYLS.NE.0) RATIO3 = (PRDYLS - PRDYLC) * 1000 / PRDYLS
	PLINE (103,109) = RATIO3,MASK2
	PLINE (112,124) = PRDYC,MASK
	RATIO4 =
	IF (PRDYS.NE.0) RATIO4 = (PRDYS - PRDYC) * 1000 / PRDYS
	PLINE (126,132) = RATIO4,MASK2
	CALL PRINT
;**********************************************************************************
;		CALCULATE PRODUCT CODE TOTALS
	INCR CTR1
	GRPMLU = GRPMLU + PRDMLU
	GRPMU = GRPMU + PRDMU
	GRPYLU = GRPYLU + PRDYLU
	GRPYU	= GRPYU + PRDYU
	GRPMLS = GRPMLS + PRDMLS
	GRPMS = GRPMS + PRDMS
	GRPYLS = GRPYLS + PRDYLS
	GRPYS = GRPYS + PRDYS
	GRPMLC = GRPMLC + PRDMLC
	GRPMC = GRPMC + PRDMC
	GRPYLC = GRPYLC + PRDYLC
	GRPYC = GRPYC + PRDYC
	PRDTOT =
	IF(LSTSW.EQ.1) GO TO GRPTOT
	IF(SIPCD.NE.OLDPCD) CALL GRPTOT
	RETURN
;**********************************************************************************
GRPTOT,
	CALL PRINT
	CALL PRINT
	PLINE(15,22) = 'PROD CAT'
	PLINE(24,25) = OLDPCD
	PLINE(27,33) = 'TOTALS'
	PLINE(38,41) = UNT
	PLINE(43,52) = GRPMLU,MASK1
	PLINE(63,75) = GRPMU,MASK1
	PLINE(89,98) = GRPYLU,MASK1
	PLINE(112,121) = GRPYU,MASK1
	CALL PRINT
	PLINE(38,41) = SLS
	PLINE(43,55) = GRPMLS,MASK
	PLINE(66,78) = GRPMS,MASK
	PLINE(89,101) = GRPYLS,MASK
	PLINE(112,124) = GRPYS,MASK
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = GRPMLC,MASK
	PRATO1 =
	IF (GRPMLS.NE.0) PRATO1 = (GRPMLS - GRPMLC) * 1000 / GRPMLS
	PLINE (57,63) = PRATO1,MASK2
	PLINE (66,78) = GRPMC,MASK
	PRATO2 =
	IF (GRPMS.NE.0) PRATO2 = (GRPMS - GRPMC) * 1000 / GRPMS
	PLINE (80,86) = PRATO2,MASK2
	PLINE (89,101) = GRPYLC,MASK
	PRATO3 =
	IF (GRPYLS.NE.0) PRATO3 = (GRPYLS - GRPYLC) * 1000 / GRPYLS
	PLINE (103,109) = PRATO3,MASK2
	PLINE (112,124) = GRPYC,MASK
	PRATO4 =
	IF (GRPYS.NE.0) PRATO4 = (GRPYS - GRPYC) * 1000 / GRPYS
	PLINE (126,132) = PRATO4,MASK2
	CALL PRINT
	PLINE = DASHES
	CALL PRINT
;******************************************************************************
;		CALCULATE GRAND TOTALS
	INCR CTR2
	GRDMLU = GRDMLU + GRPMLU
	GRDMU = GRDMU + GRPMU
	GRDYLU = GRDYLU + GRPYLU
	GRDYU = GRDYU + GRPYU
	GRDMLS = GRDMLS + GRPMLS
	GRDMS = GRDMS + GRPMS
	GRDYLS = GRDYLS + GRPYLS
	GRDYS = GRDYS + GRPYS
	GRDMLC = GRDMLC + GRPMLC
	GRDMC = GRDMC + GRPMC
	GRDYLC = GRDYLC + GRPYLC
	GRDYC = GRDYC + GRPYC
	GRPTOT =
	IF (LSTSW.EQ.1) GO TO GRDTOT
	RETURN
;****************************************************************************
;		PRINT GRAND TOTALS
GRDTOT,
	CALL PRINT
	CALL PRINT
	PLINE(21,33) = 'GRAND TOTALS:'
	PLINE (38,41) = UNT
	PLINE(43,52) = GRDMLU,MASK1
	PLINE(66,75) = GRDMU,MASK1
	PLINE(89,98) = GRDYLU,MASK1
	PLINE(112,121) = GRDYU,MASK1
	CALL PRINT
	PLINE(38,41) = SLS
	PLINE(43,55) = GRDMLS,MASK
	PLINE(66,78) = GRDMS,MASK
	PLINE(89,101) = GRDYLS,MASK
	PLINE(112,124) = GRDYS,MASK3
	CALL PRINT
	PLINE (38,41) = CST
	PLINE (43,55) = GRDMLC,MASK
	GRATO1 =
	IF (GRDMLS.NE.0) GRATO1 = (GRDMLS - GRDMLC) * 1000 / GRDMLS
	PLINE (57,63) = GRATO1,MASK2
	PLINE (66,78) = GRDMC,MASK
	GRATO2 =
	IF (GRDMS.NE.0) GRATO2 = (GRDMS - GRDMC) * 1000 / GRDMS
	PLINE (80,86) = GRATO2,MASK2
	PLINE (89,101) = GRDYLC,MASK
	GRATO3 =
	IF (GRDYLS.NE.0) GRATO3 = (GRDYLS - GRDYLC) * 1000 / GRDYLS
	PLINE (103,109) = GRATO3,MASK2
	PLINE (112,124) = GRDYC,MASK
	GRATO4 =
	IF (GRDYS.NE.0) GRATO4 = (GRDYS - GRDYC) * 1000 / GRDYS
	PLINE (126,132) = GRATO4,MASK2
	CALL PRINT
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
;********************************************************************************
END,
	XCALL WATE(3,V)
	XCALL FILES (1,'I',41,4)
	XCALL FILES (2,'I',42,4)
	XCALL FILES (8,'I',58,4)
	XCALL FILES (7,'I',57,4)
	XCALL PGCHN ('CP:SSMENU',1)
INUSE4,
	XCALL FILES(2,'I',42,4)
INUSE3,
	XCALL FILES(8,'I',58,4)
INUSE2,
	XCALL FILES(7,'I',57,4)
INUSE,
	XCALL PGCHN ('CP:SSMENU',1)
PRINT,
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',
&		'NO HDR',LEGEND,LGND2,'NO LEGEND',0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
NOINV,
	DESCR = '* ITEM NOT ON FILE *'
	GO TO SAMEP
END
