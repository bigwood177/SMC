;  PRCMNT / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		PROGRAM FOR PRICE CODE AND PRICE MAINTENANCE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'
RECORD ITMMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD ITMCTL ,X	
		.INCLUDE 'DEF:RD041B.DEF'
RECORD	ITMIDX		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD	CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	V	,D1
	SWITCH	,D1
	SELECT	,D1
	INXCTL	,D1
	ENTRY	,A16
	WROW	,D2
	WCOL	,D3
	ARRSIZ	,D2
	ARPNTR	,D2
	ARRCNT	,D2
	DUPLIC	,D1
	J	,D2
	CTR	,D2
	KEY	,A15
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	WHATNO	,D2
	CNGCTL	,D1
	PCOL	,D3
	BLANKS	,A18
	PRICEA	,A10
	WITMNO	,A15
	XROW	,D2
	HDCNTA	,A3
	DECMAL	,D10
	OPTION	,D1
	DTGTMG	,A2
	COL2	,D2
	ROW2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST	,D1
PROC
	XCALL TERID (V)
	SWITCH = 1
	XCALL FILES (2,'I',42,SWITCH)		;FILE #42 -- ITMIDX FILE
	IF (SWITCH.EQ.9) XCALL PGCHN ('CP:CPMENU',1)
	SWITCH = 5
	XCALL FILES (1,'U',41,SWITCH)		;FILE #41 -- ITMMAS FILE
	IF (SWITCH.EQ.9) GO TO END
	LOKCTL = 1
	XCALL IO(1,ITMCTL,1,READ,LOKCTL)
	ARRSIZ = NUMPRC
	JUST = JSTIFY
	IF (NUMPRC.LT.2) GO TO ONLY1
	BSEND = ORG041
MNUHDR,
	XCALL OUTPT (1,1,2,'PRICE MAINTENANCE',1)
	XCALL OUTPT (10,13,0,'PLEASE SELECT APPLICATION',1)
	XCALL OUTPT (12,18,0,'1. ENTER CUSTOMER TYPES & PRICES',1)
	XCALL OUTPT (14,18,0,'2. CHANGE/INQUIRE CUSTOMER TYPES & PRICES',1)
MNUSEL,
	CTL = '10,40,01,01,#E'
	CALL INPUT
	GO TO (MNUSEL,END), INXCTL
	SELECT = ENTRY (1,1)
	IF (SELECT.LT.1.OR.SELECT.GT.2) GO TO MNUSEL
	IF (SELECT.EQ.1) XCALL OUTPT (1,19,0,'- ADD              ',1)
	IF (SELECT.EQ.2) XCALL OUTPT (1,19,0,'- CHANGE/INQUIRE   ',1)
DISPLA,
	UNLOCK 1
	CNGCTL =
	CALL HEADER
ITEMNO,
	CTL = '04,15,15,01,AE'
	CALL INPUT
	GO TO (DISPLA,MNUHDR), INXCTL
	IF (JUST) XCALL FRMAT (ENTRY(1,15),15)
	XCALL OUTPT (4,15,0,ENTRY(1,15),1)
	WITMNO = ENTRY (1,15)
	KEY = WITMNO
	XCALL SERCH (2,ITMIDX,KEY,1,15,BSEND,BSMID,SRCCTL,1,16,20,0,0,0,0)
	GO TO (BADITM), SRCCTL
	CALL DISPLY
	IF (LOKCTL.EQ.1) GO TO DISPLA
	IF (SELECT.EQ.1.AND.PRICE(1).NE.00000000) GO TO ONFILE
	IF (SELECT.EQ.1) GO TO NEWADD
ANCHG1,
	XCALL ANYCN(CNGCTL,WHATNO)
	GO TO (PROCES,CNGBR), CNGCTL + 1
BADITM,
	XCALL MESAG ('ITEM NOT ON FILE',1)
	GO TO DISPLA
ONFILE,
	XCALL MESAG ('PRICES ALREADY ENTERED FOR THIS ITEM',1)
	GO TO DISPLA
TARGET,
	CTL = '06,22,02,00,# '
	CALL INPUT
	GO TO (DISPLA), INXCTL
	TGTMGN = ENTRY (1,2)
	GO TO ANCHG1
PROCES,
	LOKCTL = 1
	XCALL IO (1,ITMMAS,IRC041,WRITE,LOKCTL)
READ1,
	LOKCTL = 1
	XCALL IO (1,ITMCTL,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (1,ITMCTL,MAX041,READ,LOKCTL)
	GO TO DISPLA
HEADER,
	XCALL OUTPT (2,1,2,'\',1)
	XCALL OUTPT (4,4,0,'1. ITEM #',1)
	XCALL OUTPT (6,4,0,'2. TARGET MARGIN:',1)
	XCALL OUTPT (6,35,0,'LST-CST:',1)
	XCALL OUTPT (6,56,0,'AVG-CST:',1)
	XCALL OUTPT (8,7,0,'TYPE',1)
	XCALL OUTPT (8,16,0,'PRICE',1)
	XCALL OUTPT (8,35,0,'TYPE',1)
	XCALL OUTPT (8,44,0,'PRICE',1)
	XCALL OUTPT (8,63,0,'TYPE',1)
	XCALL OUTPT (8,72,0,'PRICE',1)
	CTR = 2
LOOP01,
	INCR CTR
	IF (CTR.GT.(ARRSIZ+2)) RETURN
	IF (CTR.GT.30) GO TO COLTRE
	IF (CTR.GT.16) GO TO COLTWO
COLONE,
	WCOL = 3
	WROW = CTR + 7
	GO TO DSPHDR
COLTWO,
	WCOL = 31
	WROW = CTR - 7
	GO TO DSPHDR
COLTRE,
	WCOL = 59
	WROW = CTR - 21
DSPHDR,
	HDCNTA = CTR,'ZX.'
	XCALL OUTPT (WROW,WCOL,0,HDCNTA,1)
	GO TO LOOP01
NEWADD,
	ARPNTR =
LOOP03,
	INCR ARPNTR
	IF (ARPNTR.GT.ARRSIZ) GO TO ANCHG1
	CTR = ARPNTR + 2
	GO TO ADDENT
CNGBR,
	IF (WHATNO.LT.2.OR.WHATNO.GT.(ARRSIZ + 2)) GO TO ANCHG1
	IF (WHATNO.EQ.2) GO TO TARGET
	IF (WHATNO.EQ.(ARRCNT+3)) INCR ARRCNT
	IF (WHATNO.GT.(ARRCNT+3)) CALL FIX
	CTR = WHATNO
	ARPNTR = WHATNO - 2
ADDENT,
	IF (CTR.GT.30) GO TO COLMTH
	IF (CTR.GT.16) GO TO COLMTW
COLMON,
	WCOL = 8
	WROW = CTR + 7
	GO TO INCHNG
COLMTW,
	WCOL = 36
	WROW = CTR - 7
	GO TO INCHNG
COLMTH,
	WCOL = 64
	WROW = CTR - 21
INCHNG,
	ROW = WROW
	COL = WCOL
	CTL (7,14) = '02,00,A '
	IF (ARPNTR.EQ.1) GO TO INCHG1
	CALL INPUT
	GO TO (DISPLA), INXCTL
	IF (ENTRY(1,2).EQ.'  '.AND.CNGCTL.EQ.0) GO TO ANCHG1
	IF (ENTRY(1,2).EQ.'  ') GO TO DELETE
	CALL VPRCCD
	IF (DUPLIC.EQ.1) GO TO DUPCD
	PRICCD (ARPNTR) = ENTRY (1,2)
INCHG1,
	WCOL = WCOL + 5
	COL = WCOL
	CTL (7,14) = '08,00,$ '
	CALL INPUT
	WCOL = WCOL - 5
	GO TO (DISPLA), INXCTL
	PRICE (ARPNTR) = ENTRY (1,8)
	IF (ENTRY(1,8).EQ.'        ') GO TO INCHG1
	PRICEA = PRICE (ARPNTR), 'ZZZ,ZZZ.XX'
	XCALL OUTPT (WROW,COL,0,PRICEA,1)
	IF (SELECT.EQ.1.AND.CNGCTL.EQ.0.AND.ARPNTR.GT.1) INCR ARRCNT
	IF (SELECT.EQ.1.AND.CNGCTL.EQ.0) GO TO LOOP03
	GO TO ANCHG1
DISPLY,
	LOKCTL = 0
	XCALL IO (1,ITMMAS,IRC041,READ,LOKCTL)
		;A CONDIION OF LOKCTL=1 IS HANDLED AFTER RETURNING
		;FROM THIS CALLED ROUTINE
	IF (LOKCTL.EQ.1) RETURN		;ABORT SELECTED
DSPITM,
	XCALL OUTPT (4,32,0,DESCR,1)
	DTGTMG = TGTMGN
	XCALL OUTPT (6,22,0,DTGTMG,1)
	DECMAL = LSTCST
	CTL = '06,44,07'
	CALL DSPDLR
	DECMAL = AVGCST
	CTL = '06,65,08'
	CALL DSPCST
ENTRDS,
	CTR =
	ARRCNT = 1
LOOP02,
	INCR CTR
	IF (CTR.GT.ARRSIZ) GO TO DSPYDN
	IF (PRICCD(CTR).NE.'  '.AND.PRICE(CTR).NE.0) INCR ARRCNT
	IF (CTR.GT.28) GO TO COLIII
	IF (CTR.GT.14) GO TO COLII
COLI,
	WCOL = 8
	WROW = CTR + 9
	GO TO DSPDIS
COLII,
	WCOL = 36
	WROW = CTR - 5
	GO TO DSPDIS
COLIII,
	WCOL = 64
	WROW = CTR - 19
DSPDIS,
	PRICEA = PRICE (CTR), 'ZZZ,ZZZ.XX'
	XCALL OUTPT (WROW,WCOL,0,PRICCD(CTR),1)
	PCOL = WCOL + 4
	XCALL OUTPT (WROW,PCOL,0,PRICEA,1)
	IF (PRICCD(CTR).EQ.'  '.AND.CTR.GT.1)
&	XCALL OUTPT (WROW,WCOL,0,BLANKS,1)
	GO TO LOOP02
DSPYDN,
	XCALL OUTPT (24,1,1,'\',1)
	RETURN
VPRCCD,
	J =
	DUPLIC =
LOOPA,
	INCR J
	IF (J.GT.ARRSIZ) RETURN
	IF (J.EQ.ARPNTR) GO TO LOOPA
	IF (PRICCD(J).EQ.ENTRY(1,2)) DUPLIC = 1
	GO TO LOOPA
DUPCD,
	XCALL MESAG ('DUPLICATE TYPE NOT ALLOWED',1)
	GO TO INCHNG
DELETE,
	IF (PRICCD(ARPNTR+1).EQ.'  '.OR.(ARPNTR+1).GT.ARRSIZ) GO TO CLRSPT
	PRICCD (ARPNTR) = PRICCD (ARPNTR+1)
	PRICE (ARPNTR) = PRICE (ARPNTR+1)
	INCR ARPNTR
	GO TO DELETE
CLRSPT,
	PRICCD (ARPNTR) =
	PRICE (ARPNTR) =
	CALL ENTRDS
	GO TO ANCHG1
FIX,
	INCR ARRCNT
	WHATNO = ARRCNT + 2
	RETURN
ONLY1,
	XCALL MESAG('ONLY 1 PRICE ALLOWED',2)
END,
	XCALL WATE(3,2)
	CLOSE 1
	XCALL FILES (2,'I',42,4)
	XCALL PGCHN ('CP:CPMENU',1)
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
DSPDLR,
	OPTION = 3
	GO TO CALDSP
DSPCST,
	OPTION = 4
CALDSP,
	XCALL DSPLY(MAX,ROW,COL,DECMAL,OPTION,1)
	RETURN
	END
