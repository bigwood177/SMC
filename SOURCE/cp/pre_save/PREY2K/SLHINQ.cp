; SLHINQ.COP 
;
;	SALES HISTORY INQUIRY PROGRAM
;
;
RECORD SLSHST
		.INCLUDE 'DEF:RD055A.DEF'
RECORD SLHCTL	,X
		.INCLUDE 'DEF:RD055B.DEF'
RECORD CUSMAS
		.INCLUDE 'DEF:RD001A.DEF'
RECORD CUSCTL	,X
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX
		.INCLUDE 'DEF:RD002A.DEF'
RECORD ITMMAS
		.INCLUDE 'DEF:RD041A.DEF'
RECORD ITMCTL	,X
		.INCLUDE 'DEF:RD041B.DEF'
RECORD ITMIDX
		.INCLUDE 'DEF:RD042A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
	
RECORD
	V	,D1
	SWITCH	,D1
	ITMKEY	,A15
	CATKEY	,A2
	CUSKEY	,D6
	ORG41	,D5
	ORG01	,D5
	ORG55	,D5
	RECNO	,D5
	INXCTL	,D1
	ENTRY	,A15
	I	,D2
	LOKCTL 	,D1
	READ	,D1,0
	SRCCTL	,D1
	BSMID	,D5
	KEY	,A21
	BLANKS	,A15	
	ALPHA	,A15
	DTMASK	,A8,	'XX/XX/XX'
	NUMASK	,A8,	'ZZZ,ZZX-'
	DLMASK	,A12,	'ZZZZ,ZZZ.XX-'
	HSTOT	,D9
	LSTCUS	,D6
PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'SALES HISTORY INQUIRY',1)
OPEN,
	SWITCH = 1
	XCALL FILES (2,'I',002,SWITCH)		;FILE 002 - CUSIDX
	IF (SWITCH.NE.9) GOTO OPEN2
	GOTO ABORT
OPEN2,
	XCALL FILES (1,'I',001,SWITCH)		;FILE 001 - CUSMAS
	IF (SWITCH.NE.9) GOTO OPEN3
	CALL CLOSE1
	GOTO ABORT
OPEN3,
	XCALL FILES (4,'I',042,SWITCH)		;FILE 042 - ITMIDX
	IF (SWITCH.NE.9) GOTO OPEN4
	CALL CLOSE2
	GOTO ABORT
OPEN4,
	XCALL FILES (3,'I',041,SWITCH)		;FILE 041 - ITMMAS
	IF (SWITCH.NE.9) GOTO OPEN5
	CALL CLOSE3
	GOTO ABORT
OPEN5,
	XCALL FILES (5,'I',055,SWITCH)		;FILE 055 - SLSHST
	IF (SWITCH.NE.9) GOTO RDHDR
	CALL CLOSE4
	GOTO ABORT
CLOSES,
	XCALL FILES (5,'I',055,4)
CLOSE4,
	XCALL FILES (3,'I',042,4)
CLOSE3,
	XCALL FILES (4,'I',041,4)
CLOSE2,
	XCALL FILES (1,'I',001,4)
CLOSE1,
	XCALL FILES (2,'I',002,4)
	RETURN
ENDOFF,
	CALL CLOSES
ABORT,
	XCALL PGCHN ('CP:CPMENU',1)
	STOP
RDHDR,
	LOKCTL = 1
	XCALL IO (1,CUSCTL,1,READ,LOKCTL)
	ORG01 = ORG001
	LOKCTL = 1
	XCALL IO (3,ITMCTL,1,READ,LOKCTL)
	ORG41 = ORG041
	LOKCTL = 1
	XCALL IO (5,SLHCTL,1,READ,LOKCTL)
	ORG55 = ORG055
DISPLA,
;;;	DISPLAY (15,27,'[01;24r')			;scroll region
	XCALL OUTPT (1,1,2,'SALES HISTORY INQUIRY',1)
	XCALL OUTPT (3,10,0,'CUSTOMER: ',1)
	XCALL OUTPT (4,10,0,'CATEGORY: ',1)
	XCALL OUTPT (5,10,0,'ITEM NO.: ',1)
	XCALL OUTPT (5,40,0,'(PARTIAL ITEM NUMBERS ALLOWED)',1)
	DISPLAY (15,27,'[7m')
	XCALL OUTPT (6,1,0,'--------------- ITEM -------------------',1)
	XCALL OUTPT (0,0,0,'------    LAST    ---- ACCUMULATED ---- ',1)
	XCALL OUTPT (7,1,0,'NUMBER          DESCRIPTION             ',1)
	XCALL OUTPT (0,0,0,'         SL DATE      QTY       SL $    ',1)
	DISPLAY (15,27,'[0m')
;
ASK1,
	CTL = '03,20,06,00,#E'
	CALL INPUT
	GOTO (DISPLA,ENDOFF), INXCTL
	CUSKEY = ENTRY
	IF (CUSKEY.EQ.0)
	BEGIN
	  XCALL OUTPT (ROW,COL,0,'ALL',1)
	  GOTO ASK2
	END
	CALL FNDCUS
	XCALL OUTPT (ROW,40,0,NAME,1)
ASK2,
	CTL = '04,20,02,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	CATKEY = ENTRY
	IF (CATKEY.NE.BLANKS) 
	BEGIN
	  ITMKEY =
	  XCALL OUTPT (ROW+1,COL,0,'ALL',1)
	  GOTO FNDHST
	END
	XCALL OUTPT (ROW,COL,0,'ALL',1)
ASK3,
	CTL = '05,20,15,00,A '
	CALL INPUT
	GOTO (DISPLA), INXCTL
	ITMKEY = ENTRY
	IF (ITMKEY.EQ.BLANKS) 
	BEGIN
	  XCALL OUTPT (ROW,COL,1,'ALL',1)
	  GOTO FNDHST
	END
	FOR I FROM 15 THRU 1 BY -1
	BEGIN
	  IF (ITMKEY(I,I).NE.' ') GOTO E3
	END
E3,
	XCALL OUTPT (ROW,COL,1,ITMKEY(1,I),1)
	CALL FNDITM
	XCALL OUTPT (ROW,40,0,DESCR,1)

FNDHST,
;;;	DISPLAY (15,27,'[08;22r')	;scroll region
	HSTOT =
	ROW = 7
	RECNO = 1
	LSTCUS = -1
	IF (CUSKEY.EQ.0) GOTO HSTLP
	IF (ITMKEY.EQ.BLANKS) THEN CALL FNDHS1 ELSE CALL FNDHS2
	IF (SRCCTL) GOTO NOSLH
	RECNO = BSMID
	GOTO DSPHRC
HSTLP,
	LOKCTL = 1
	INCR RECNO
	XCALL IO (5,SLSHST,RECNO,READ,LOKCTL)
	IF (SLSHST.EQ.']]]]]]]]]]') GOTO ENDHST
DSPHRC,
	IF (CUSKEY.NE.0.AND.HCUSNO.NE.CUSKEY) GOTO ENDHST
	IF (CATKEY.NE.BLANKS.AND.CATKEY.NE.HPRDCD) GOTO HSTLP
	IF (ITMKEY.NE.BLANKS) IF (ITMKEY(1,I).NE.HITMNO(1,I)) GOTO HSTLP
	INCR ROW
	IF (ROW.GT.22) 
	BEGIN
	  CALL NXTPAG
	  IF (INXCTL.EQ.2) GOTO DISPLA
	END
	IF (CUSKEY.EQ.0.AND.HCUSNO.NE.LSTCUS)
	BEGIN
	  KEY(1,6) = HCUSNO,'XXXXXX'
	  CALL FNDCU2
	  IF (ROW+1.GE.22) CALL NXTPAG
	  IF (INXCTL.EQ.2) GOTO DISPLA
	  ALPHA(1,9) = HCUSNO,'ZZZZZX   '
	  DISPLAY (15,27,'[7m')
	  XCALL OUTPT (ROW,1,0,ALPHA(1,9),1)
	  XCALL OUTPT (ROW,10,0,NAME,1)
	  DISPLAY (15,27,'[0m')
	  INCR ROW
	  LSTCUS = HCUSNO
	END
	XCALL OUTPT (ROW,1,0,HITMNO,1)
	KEY(7,21) = HITMNO
	CALL FNDIT2
	XCALL OUTPT (ROW,17,0,DESCR,1)
	ALPHA(1,8) = HINVDT,DTMASK
	XCALL OUTPT (ROW,49,0,ALPHA(1,8),1)
	ALPHA(1,8) = HQTY,NUMASK
	XCALL OUTPT (ROW,59,0,ALPHA(1,8),1)
	ALPHA(1,12) = HSALE,DLMASK
	XCALL OUTPT (ROW,69,0,ALPHA(1,12),1)
	HSTOT = HSTOT + HSALE
	XCALL OUTPT (23,62,0,'TOTAL: ',1)
	ALPHA(1,12) = HSTOT,DLMASK
	XCALL OUTPT (23,69,0,ALPHA(1,12),1)
	GOTO HSTLP
NXTPAG,
	ROW = 08
	XCALL OUTPT (24,1,1,'<RETURN> FOR NEXT PAGE / <BS> TO ESCAPE',1)
	XCALL INPUT (24,42,01,00,'#E',ENTRY, INXCTL,1)
	XCALL OUTPT (ROW,1,2,'\',1)
	IF (INXCTL.EQ.2) RETURN
	RETURN
ENDHST,
	XCALL MESAG ('NO MORE RECORDS TO FIT THESE PARAMETERS',2)
	GOTO DISPLA
FNDHS1,
	XCALL SERCH (5,SLSHST,KEY(1,6),1,6,ORG55,BSMID,SRCCTL,1,1,6,0,0,0,0)
	RETURN
FNDHS2,
	XCALL SERCH (5,SLSHST,KEY(1,6+I),1,6+I,ORG55,BSMID,SRCCTL,1,1,6,0,0,0,0)
	RETURN
NOSLH,
	XCALL MESAG ('NO SALES HISTORY RECORDS FIT THESE PARAMETERS',1)
	GOTO DISPLA
FNDCUS,
	KEY(1,6) = CUSKEY,'XXXXXX'
FNDCU2,
	SRCCTL =
	IF (CUSIDX(1,6).EQ.KEY(1,6)) RETURN
	XCALL SERCH (2,CUSIDX,KEY(1,6),1,6,ORG01,BSMID,SRCCTL,1,7,11,0,0,0,0)
	IF (SRCCTL)
	BEGIN
	  CUSMAS =
	  NAME = 'CUSTOMER NOT ON FILE'
	  RETURN
	END
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
	RETURN
FNDITM,
	KEY(7,21) = ITMKEY
FNDIT2,
	SRCCTL =
	IF (ITMIDX(1,15).EQ.KEY(7,21)) RETURN
	XCALL SERCH (4,ITMIDX,KEY(7,21),1,15,ORG41,BSMID,SRCCTL,1,16,20,0,0,0,0)
	IF (SRCCTL)
	BEGIN
	  ITMMAS = 
	  DESCR = 'NOT FOUND OR PARTIAL ITEM NO.'
	  RETURN
	END
	LOKCTL = 1
	XCALL IO (3,ITMMAS,IRC041,READ,LOKCTL)
	RETURN
INPUT,
	XCALL INPUT (ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,1)
	RETURN
END
