; SSIUPD / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		THIS PROGRAM READS THE SALES SUMMARY INDEX FILE, AND
;		UPDATES THE NON-KEY FIELDS FROM THE CORRESPONDING CUSTOMER
;		AND PRODUCT MASTER FILES. THIS INSURES CURRENT AND CONSISTENT
;		FIELDS FOR SELECTION
;
;		YOU MUST HAVE VALID CUSTOMER MASTER AND ITEM MASTER
;			FILES TO RUN THIS PROGRAM.  BE SURE TO
;			CHECK FILE LENGTHS!
;
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD CUSMAS   	
		.INCLUDE 'DEF:RD001A.DEF'
RECORD DUMCUS		
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX  		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD INVMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV		
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX  		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD MESSAG
		,A6
RECORD
	RECIDX	,D5
	BSMID	,D5
	SRCCTL	,D1
	SWITCH	,D1
	V	,D1
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	BLANKS	,A10
PROC
	XCALL TERID (V)
	XCALL OUTPT (2,1,1,'UPDATE INDEX FIELDS',1)
	SWITCH = 1
	XCALL SNMSG (MESSAG,SWITCH)
	IF (SWITCH.EQ.9) GOTO GOON
	SWITCH = 3
	XCALL SNMSG (BLANKS,SWITCH)
GOON,
	CALL OPENS
	RECIDX = 1

RDSSI,
	INCR RECIDX
	LOKCTL = 1
	XCALL IO (3,SLSIDX,RECIDX,READ,LOKCTL)
	IF (SLSIDX(1,5).EQ.']]]]]') GOTO END
	CALL UPDCUS
	CALL UPDINV
	LOKCTL = 1
	XCALL IO (3,SLSIDX,RECIDX,WRITE,LOKCTL)
	GOTO RDSSI
END,
	XCALL FILES (1,'I',01,4)
	XCALL FILES (2,'I',02,4)
	XCALL FILES (3,'U',59,4)
	XCALL FILES (4,'I',41,4)
	XCALL FILES (5,'I',42,4)
	SWITCH = 2
	XCALL SNMSG (MESSAG,SWITCH)
	XCALL PGCHN ('CP:PRGSLH',1)
;***************************************
UPDCUS,
	IF (SICUS.EQ.CUSNO) GOTO CUSUPD
	XCALL SERCH(2,CUSIDX,SICUS,1,6,ORG001,BSMID,SRCCTL,4,7,11,0,0,0,0)
	IF (SRCCTL.NE.0) RETURN
	IF (IRC001.EQ.0) RETURN
	LOKCTL = 1
	XCALL IO (1,CUSMAS,IRC001,READ,LOKCTL)
CUSUPD,
	SICCD = CUSCD
	SICCD2 = CUSCD2
	SITERR = TERR
	SISMAN = SLSMAN
	SILOC = LOC
	SISTAT = STATE
	RETURN
;***************************************
UPDINV,
	IF (SIPROD.EQ.IITMNO) GOTO INVUPD
	XCALL SERCH (5,INVIDX,SIPROD,1,15,ORG041,BSMID,SRCCTL,4,16,20,0,0,0,0)
	IF (SRCCTL.NE.0) RETURN
	IF (IRC041.EQ.0) RETURN
	LOKCTL = 1
	XCALL IO (4,INVMAS,IRC041,READ,LOKCTL)
INVUPD,
	SIPCD = IPRCAT
	SIPCD2 = USRDEF
	RETURN
;***************************************
OPENS,
	SWITCH = 1
	XCALL FILES (1,'I',01,SWITCH)		;FILE # 01 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE1
	SWITCH = 1
	XCALL FILES (2,'I',02,SWITCH)		;FILE # 02 -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	SWITCH = 5
	XCALL FILES (3,'U',59,SWITCH)		;FILE # 59 -- SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE3
	SWITCH = 1
	XCALL FILES (4,'I',41,SWITCH)		;FILE # 41 -- INVMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE4
	SWITCH = 1
	XCALL FILES (5,'I',42,SWITCH)		;FILE # 42 -- INVIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE5
	LOKCTL = 1
	XCALL IO (1,DUMCUS,1,READ,LOKCTL)
	LOKCTL = 1
	XCALL IO (4,DUMINV,1,READ,LOKCTL)
	RETURN
INUSE5,	XCALL FILES (4,'I',41,4)
INUSE4,	XCALL FILES (3,'U',59,4)
INUSE3,	XCALL FILES (2,'I',02,4)
INUSE2,	XCALL FILES (1,'I',01,4)
INUSE1,	XCALL MESAG('FILES IN USE, NO UPDATE TO INDEX FIELDS',1)
	SWITCH = 2
	XCALL SNMSG (MESSAG,SWITCH)
	XCALL PGCHN ('CP:PRGSLH',1)
	END
