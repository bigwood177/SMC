;  CUSSLS / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		SALES COMPARISON BY CUSTOMER
;
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD CUSMAS  		
		.INCLUDE 'DEF:RD001A.DEF'
RECORD DUMMAS		
		.INCLUDE 'DEF:RD001B.DEF'
RECORD CUSIDX  		
		.INCLUDE 'DEF:RD002A.DEF'
RECORD SLSSUM		
		.INCLUDE 'DEF:RD058A.DEF'
RECORD SLSDUM		
		.INCLUDE 'DEF:RD058B.DEF'
RECORD SLSIDX		
		.INCLUDE 'DEF:RD059A.DEF'
RECORD TITLE
		,A28,'SALES COMPARISON BY CUSTOMER'
RECORD LEGEND
		,A48
		,A14,'************* '
	MONTH1	,A10
		,A13,'*************'
		,A9
		,A20,'********** YTD THRU '
	MONTH2	,A10
		,A7,' ******'
RECORD LGND2
		,A46
		,A7,'-------'
	LYR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR1	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	LYR2	,D4
		,A7,'-------'
		,A5
		,A7,'-------'
	YR2	,D4
		,A6,'------'
RECORD HDR1
		,A3
		,A8,'CUST #  '
		,A13,'CUSTOMER NAME'
		,A33
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A18
		,A5,'% MRG'
		,A1
RECORD CUSSUB
	CUSMLS	,D9
	CUSMS	,D9
	CUSMLU	,D9
	CUSMU	,D9
	CUSMLC	,D9
	CUSMC	,D9
	CUSYLS	,D9
	CUSYS	,D9
	CUSYLU	,D9
	CUSYU	,D9
	CUSYLC	,D9
	CUSYC	,D9
RECORD GRSTOT
	GRSMLS	,D9
	GRSMS	,D9
	GRSMLU	,D9
	GRSMU	,D9
	GRSMLC	,D9
	GRSMC	,D9
	GRSYLS	,D9
	GRSYS	,D9
	GRSYLU	,D9
	GRSYU	,D9
	GRSYLC	,D9
	GRSYC	,D9
RECORD
	RATIO1	,D5
	RATIO2	,D5
	RATIO3	,D5
	RATIO4	,D5
	GRATO1	,D5
	GRATO2	,D5
	GRATO3	,D5
	GRATO4	,D5
	OLDCUS	,D6
	OLDCCD	,A2
	LINCNT	,D2,60
	STRTNA	,A6
	ENDNA	,A6
	STRTNO	,D6
	ENDNO	,D6
	CNGCTL	,D1
	LPARG	,D1
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	IDXCNT	,D5
	PGCNT	,D6
	PLINE	,A132
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	LSTSW	,D1
	FRSTSW	,D1
	KEY	,A6
	MASK	,A13,'Z,ZZZ,ZZX.XX-'
 	MASK1	,A10,'Z,ZZZ,ZZX-'
	MASK2	,A7,'ZZZ.X%-'
	MASK3	,A14,'ZZ,ZZZ,ZZZ.ZX-'
	LPSW	,D2
	SWITCH	,D1,1
	PRTCTL	,D3
	STXCTL	,D1
	ROW	,D2
	WHATNO	,D2
	V	,D1
	N	,D4
	UNT	,A4,'UNT:'
	SLS	,A4,'SLS:'
	CST	,A4,'CST:'
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	PRNTSW	,D1,	0
PROC
	XCALL TERID(V)
	XCALL OUTPT (1,1,2,'PRINT SALES COMPARISON BY CUSTOMER',V)
	XCALL WATE (4,V)
	SWITCH = 5
	XCALL FILES(9,'I',59,SWITCH)		; FILE #59 -- SLSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	SWITCH = 1
	XCALL FILES(8,'I',58,SWITCH)		; FILE #58 -- SLSSUM FILE
	IF (SWITCH.EQ.9) GO TO INUSE2
	SWITCH = 1
	XCALL FILES(1,'I',1,SWITCH)		; FILE #1 -- CUSMAS FILE
	IF (SWITCH.EQ.9) GO TO INUSE3
	SWITCH = 1
	XCALL FILES(2,'I',2,SWITCH)		; FILE #2 -- CUSIDX FILE
	IF (SWITCH.EQ.9) GO TO INUSE4
	LOKCTL = 1
	XCALL IO(1,DUMMAS,1,READ,LOKCTL)
	BSEND = ORG001
	LOKCTL = 1
	XCALL IO(8,SLSDUM,1,READ,LOKCTL)
	IF (SSPER.EQ.0) GO TO NOPERS
	YR1 = SSYEAR
	YR2 = SSYEAR
	LYR1 = SSYEAR - 1
	LYR2 = SSYEAR - 1
	MONTH1 = SSDESC(SSPER)
	MONTH2 = SSDESC(SSPER)
;*************************************************************************************
SELECT,
	STXCTL = 1
	XCALL STENO (STRTNA,ENDNA,'CUSTOMER #',6,STXCTL,V)
	GO TO (END),STXCTL
	XCALL WATE (3,V)
	IF (ENDNA.EQ.'[[[') STRTNA =
	IF (ENDNA.EQ.'[[[') ENDNA = '999999'
	ENDNO = ENDNA
	STRTNO = STRTNA
	LOKCTL = 1
	XCALL IOS (9,SLSIDX,READ,LOKCTL)	;READ FIRST BLANK RECORD
READ,
	LOKCTL = 1
	XCALL IOS (9,SLSIDX,READ,LOKCTL)
	IF (LOKCTL.EQ.2 .OR. SLSIDX.EQ.']]]]]]]]]]') GO TO CUSTOT
	IF(SICUS.LT.STRTNO) GO TO READ
	IF(SICUS.GT.ENDNO) GO TO READ
	IF(IRC058.EQ.0) GO TO READ
	IF(SICUS.NE.OLDCUS.AND.FRSTSW) IDXCNT = 1
	IF(SICUS.NE.OLDCUS.AND.FRSTSW) CALL CUSTOT
	IDXCNT = 0
	IF (CUSNO.EQ.SICUS) GO TO SAMEC
	OLDCUS = SICUS
	KEY = SICUS,'XXXXXX'
	XCALL SERCH (2,CUSIDX,KEY,1,6,BSEND,BSMID,SRCCTL,4,7,11,0,0,0,0)
	GO TO (NOCUS),SRCCTL
	LOKCTL = 1
	XCALL IO(1,CUSMAS,IRC001,READ,LOKCTL)
;*****************************************************************************
;		TOTAL CUSTOMER SALES
SAMEC,
	LOKCTL = 1
	XCALL IO (8,SLSSUM,IRC058,READ,LOKCTL)
	FRSTSW = 1
	IF (SSPER.EQ.1) CALL FIRSTC
	IF (SSPER.EQ.2) CALL SECNDC
	IF (SSPER.GT.2) CALL OTHERC
	GO TO READ
NOPERS,
	XCALL MESAG('SALES SUMMARY PERIOD DESCRIPTIONS NOT SET',1)
	GO TO END
;******************************************************************************
;		TOTAL BY PERIODS
FIRSTC,
	CUSMLS = CUSMLS + SSSYTD(1)
	CUSMS = CUSMS + SSSALE
	CUSMLU = CUSMLU + SSUYTD(1)
	CUSMU = CUSMU + SSUNIT
	CUSMLC = CUSMLC + SSCYTD(1)
	CUSMC = CUSMC + SSCOST
	CUSYLS = CUSMLS
	CUSYS = CUSMS
	CUSYLU = CUSMLU
	CUSYU = CUSMU
	CUSYLC = CUSMLC
	CUSYC = CUSMC
	RETURN
SECNDC,
	CUSMLS =CUSMLS + SSSYTD(2) - SSSYTD(1)
	CUSMS = CUSMS + SSSALE
	CUSMLU = CUSMLU + SSUYTD(2) - SSUYTD(1)
	CUSMU = CUSMU + SSUNIT
	CUSMLC = CUSMLC + SSCYTD(2) - SSCYTD(1)
	CUSMC = CUSMC + SSCOST
	CUSYLS = CUSYLS + SSSYTD(2)
	CUSYS = CUSYS + SSLSTS + SSSALE
	CUSYLU = CUSYLU + SSUYTD(2)
	CUSYU = CUSYU + SSLSTU + SSUNIT
	CUSYLC = CUSYLC + SSCYTD(2)
	CUSYC = CUSYC + SSLSTC + SSCOST
	RETURN
OTHERC,
	CUSMLS = CUSMLS + SSSYTD(SSPER) - SSSYTD(SSPER - 1)
	CUSMS = CUSMS + SSSALE
	CUSMLU = CUSMLU + SSUYTD(SSPER) - SSUYTD(SSPER - 1)
	CUSMU = CUSMU + SSUNIT
	CUSMLC = CUSMLC + SSCYTD(SSPER) - SSCYTD(SSPER - 1)
	CUSMC = CUSMC + SSCOST
	CUSYLS = CUSYLS + SSSYTD(SSPER)
	CUSYS = CUSYS + SSSYTD(SSPER - 2) + SSLSTS + SSSALE
	CUSYLU = CUSYLU + SSUYTD(SSPER)
	CUSYU = CUSYU + SSUYTD(SSPER - 2) + SSLSTU + SSUNIT
	CUSYLC = CUSYLC + SSCYTD(SSPER)
	CUSYC = CUSYC + SSCYTD(SSPER - 2) + SSLSTC + SSCOST
	RETURN
;******************************************************************************
;		PRINT CUSTOMER TOTALS
CUSTOT,
	IF (PRNTSW.NE.1) CALL PRNTON
	CALL PRINT
	PLINE (4,9) = SSCUS
	PLINE (12,36) = NAME
	PLINE (38,41) = UNT
	PLINE (43,52) = CUSMLU,MASK1
	PLINE (66,75) = CUSMU,MASK1
	PLINE (89,98) = CUSYLU,MASK1
	PLINE (112,121) = CUSYU,MASK1
	CALL PRINT		;PRINT UNITS
	PLINE (38,41) = SLS
	PLINE (43,55) = CUSMLS,MASK
	PLINE (66,78) = CUSMS,MASK
	PLINE (89,101) = CUSYLS,MASK
	PLINE (112,124) = CUSYS,MASK
	CALL PRINT		;PRINT SALES
	PLINE (38,41) = CST
	PLINE (43,55) = CUSMLC,MASK
	RATIO1 =
	IF (CUSMLS.NE.0) RATIO1 = (CUSMLS - CUSMLC) * 1000 / CUSMLS
	PLINE (57,63) = RATIO1,MASK2
	PLINE (66,78) = CUSMC,MASK
	RATIO2 =
	IF (CUSMS.NE.0) RATIO2 = (CUSMS - CUSMC) * 1000 / CUSMS
	PLINE (80,86) = RATIO2,MASK2
	PLINE (89,101) = CUSYLC,MASK
	RATIO3 =
	IF (CUSYLS.NE.0) RATIO3 = (CUSYLS - CUSYLC) * 1000 / CUSYLS
	PLINE (103,109) = RATIO3,MASK2
	PLINE (112,124) = CUSYC,MASK
	RATIO4 =
	IF (CUSYS.NE.0) RATIO4 = (CUSYS - CUSYC) * 1000 / CUSYS
	PLINE (126,132) = RATIO4,MASK2
	CALL PRINT
	INCR N
	GRSMLS = GRSMLS + CUSMLS
	GRSMS = GRSMS + CUSMS
	GRSYLS = GRSYLS + CUSYLS
	GRSYS = GRSYS + CUSYS
	GRSMLU = GRSMLU + CUSMLU
	GRSMU = GRSMU + CUSMU
	GRSYLU = GRSYLU + CUSYLU
	GRSYU = GRSYU + CUSYU
	GRSMLC = GRSMLC + CUSMLC
	GRSMC = GRSMC + CUSMC
	GRSYLC = GRSYLC + CUSYLC
	GRSYC = GRSYC + CUSYC
	CUSSUB =
	IF (IDXCNT.EQ.1) RETURN
;*******************************************************************************
;		PRINT GRAND TOTALS
GRSTOT,
	CALL PRINT
	CALL PRINT
	PLINE (24,36) = 'GRAND TOTALS:'
	PLINE (38,41) = UNT
	PLINE (43,52) = GRSMLU,MASK1
	PLINE (66,75) = GRSMU,MASK1
	PLINE (89,98) = GRSYLU,MASK1
	PLINE (112,121) = GRSYU,MASK1
	CALL PRINT		;PRINT GRAND UNITS
	PLINE (38,41) = SLS
	PLINE (43,55) = GRSMLS,MASK
	PLINE (66,78) = GRSMS,MASK
	PLINE (89,101) = GRSYLS,MASK
	PLINE (112,124) = GRSYS,MASK
	CALL PRINT		;PRINT GRAND SALES
	PLINE (38,41) = CST
	PLINE (43,55) = GRSMLC,MASK
	GRATO1 =
	IF (GRSMLS.NE.0) GRATO1 = (GRSMLS - GRSMLC) * 1000 / GRSMLS
	PLINE (57,63) = GRATO1,MASK2
	PLINE (66,78) = GRSMC,MASK
	GRATO2 =
	IF (GRSMS.NE.0) GRATO2 = (GRSMS - GRSMC) * 1000 / GRSMS
	PLINE (80,86) = GRATO2,MASK2
	PLINE (89,101) = GRSYLC,MASK
	GRATO3 =
	IF (GRSYLS.NE.0) GRATO3 = (GRSYLS - GRSYLC) * 1000 / GRSYLS
	PLINE (103,109) = GRATO3,MASK2
	PLINE (112,124) = GRSYC,MASK
	GRATO4 =
	IF (GRSYS.NE.0) GRATO4 = (GRSYS - GRSYC) * 1000 / GRSYS
	PLINE (126,132) = GRATO4,MASK2
	CALL PRINT		;PRINT GRAND COSTS AND RATIOS

	XCALL LPOFF(LPSW,SPLFIL,PGCNT)
END,
	XCALL WATE(3,V)
	XCALL FILES(9,'I',59,4)
	XCALL FILES(8,'I',58,4)
	XCALL FILES(1,'I',1,4)
	XCALL FILES(2,'I',2,4)
	XCALL PGCHN ('CP:SSMENU',1)

;*******************************************************************************

PRNTON,
	SPLFIL (5,6) = 'EP'
	LPSW = 1	; PRINT, SPOOL OR DISPLAY
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO END
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
	PRNTSW = 1
	RETURN

;*******************************************************************************
INUSE4,
	XCALL FILES(1,'I',1,4)
INUSE3,
	XCALL FILES(8,'I',58,4)
INUSE2,
	XCALL FILES(9,'I',59,4)
INUSE,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:SSMENU',1)
PRINT,
	XCALL LPOUT(LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',
&		'NO HDR',LEGEND,LGND2,'NO LEGEND',0,0,132,1,LPSW,RPTNUM,PRTTYP)
	RETURN
NOCUS,
	NAME = '* CUSTOMER NOT ON FILE *'
	GO TO SAMEC
END
