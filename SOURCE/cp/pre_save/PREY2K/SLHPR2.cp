; SLHPR2.COP - PRINT SALES HISTORY CATEGORY ANALYSIS SPREADSHEET
;
;
RECORD SLSSUM
	SYY	,D2
	SMM	,D2
	SCAT	,A2
		,A10
		,A10
		,A10
	SDOLR	,D10	;XX,XXX,XXX.XX
RECORD SLHCTL
		,A28
	ORG058	,D5
	REC058	,D5
	MAX058	,D5
	DEL058	,D3
RECORD PLINE
	ACAT	,A33
		,A1
	AMM	,A3
		,A1
	ABUCK	,6A15		; XX,XXX,XXX.XX-
		,A4
RECORD HDR1
		,A33,	'CATEGORY'
		,A1
		,A3,	'MON'
		,A1
	HBUCK	,6A15
RECORD LEG1
		,A18,'DATA REPORTED FOR '
	NUMYY	,D2
		,A25,' YEAR(S) STARTING WITH 19'
	STYY	,D2

RECORD
 	CATS ,20A33,
&		'1  SALES CONNECTORS              ',
&		'2  SALES CUT TO LENGTH - CONN    ',
&		'3  SALES - ELBOWS & ANGLES       ',
&		'33 GORELOCK ELBOWS & ANGLES      ',
&		'4  SALES - COLLARS, SLEEVES, DAMP',
&		'44 HIGH EFFICIENCY TAKEOFFS      ',
&		'5  SALES - OTHER FITTINGS        ',
&		'55 16GA SPIRAL PIPE HANGERS      ',
&		'6  SALES - PIPE                  ',
&		'7  SALES - NON-MFG               ',
&		'8  SALES - SPIRAL PIPE           ',
&		'88 OVAL PIPE                     ',
&		'9  SALES - NEXUS                 ',
&		'10 SALES - WELDED FITTINGS       ',
&		'11 DUCTWORK & ACCESSORIES        ',
&		'12 SALES - PLASMA CUT FITTINGS   ',
&		'13 SALES - R-ANGLE               ',
&		'14 EZ RAIL II MACHINES           ',
&		'15 WELDED DIEFORM ELBOWS & ANGLES',
&		'** CATEGORY NOT RECOGNIZED       '
	MAXCAT	,D2,	20
	MON	,12A3,
&			'JAN','FEB','MAR','APR','MAY','JUN',
&			'JUL','AUG','SEP','OCT','NOV','DEC'

RECORD
	TODAY	,D6
	V	,D1
	DLMASK	,A15,	' ZZ,ZZZ,ZZZ.XX-'
	DASHES	,A15,	' ------------- '
	LSTCAT	,A2,	'-1'
	LSTMM	,D2,	-1
	BUCK	,D2
	RECNO	,D5,	1
	LPARG	,D1
	LINCNT	,D2,	62
	PGCNT	,D3
	SPLFIL	,A14
	RPTNUM	,D3
	PRTTYP	,A1
	TITLE	,A34,	'SALES CATEGORY SUMMARY SPREADSHEET'
	LPSW	,D1
	SWITCH	,D1
	PRTCTL	,D1
	READ	,D1,	0
	LOKCTL	,D1
	ENTRY	,A5
	INXCTL	,D1
	YRTOT	,7D10
	CATTOT	,7D10
	FILNAM	,A14
	I	,D2	
	BLANKS	,A33
RECORD	,X
		,A4
	TODYY	,D2

PROC
	XCALL TERID (V)
	XCALL OUTPT (1,1,2,'PRINT SALES SUMMARY CATEGORY SPREADSHEET',1)
	XCALL RDATE (TODAY)
	XCALL FFILE (58,FILNAM,SWITCH)
DISPLA,
	XCALL OUTPT (6,20,0,'SELECT REPORT STARTING YEAR',1)
	XCALL OUTPT (8,20,0,'SELECT NUMBER OF YEARS TO PRINT',1)
	XCALL INPUT (6,52,02,02,'#E',ENTRY,INXCTL,1)
	GOTO (DISPLA,ENDOFF), INXCTL
	STYY = ENTRY
	IF (STYY.LT.89) GOTO DISPLA
	IF (STYY.GT.TODYY) GOTO DISPLA
	XCALL INPUT (8,52,02,01,'# ',ENTRY,INXCTL,1)
	NUMYY = ENTRY
	IF (NUMYY.GT.6) GOTO DISPLA
	FOR I FROM 1 THRU NUMYY HBUCK(I) = 1900+STYY+I-1,'ZZZZZZZZXXXX '
OPENS,
	SWITCH = 1
	XCALL FILES (8,'I',58,SWITCH)		;FILE 58 - SLSSUM
	IF (SWITCH.EQ.9) GOTO ENDOFF
	LPSW = 1
	SPLFIL =
	XCALL LPON (LPSW,SPLFIL)
	IF (LPSW.EQ.0) GO TO CLOSE1
	LPARG = 2
	IF (LPSW.EQ.2) LPARG = 4
	XCALL WATE (LPARG,V)
;;;
;	SORT SLSSUM BY CAT / MON / YEAR
;;;
	XCALL OUTPT (2,1,1,'SORTING',1)
	CLOSE 8
	SORT (INPUT=FILNAM,RECORD=SLSSUM/F,KEY=(SCAT,SMM,SYY),OUTPUT=FILNAM)
	OPEN (8,I,FILNAM)
	XCALL OUTPT (2,1,1,'\',1)
RDLOOP,
	INCR RECNO
	LOKCTL = 1
	XCALL IO (8,SLSSUM,RECNO,READ,LOKCTL)
	IF (SLSSUM.EQ.']]]]]]]]]]') GOTO EOF
	IF (SYY.LT.STYY) GOTO RDLOOP
	IF (SYY.GE.STYY+NUMYY) GOTO RDLOOP
	IF (SMM.NE.LSTMM) CALL MMTOT
	IF (SCAT.NE.LSTCAT) CALL CATTOT
	BUCK = SYY - STYY + 1
	ABUCK(BUCK) = SDOLR, DLMASK
	YRTOT(BUCK) = YRTOT(BUCK) + SDOLR
	CATTOT(BUCK) = CATTOT(BUCK) + SDOLR
	GOTO RDLOOP
MMTOT,
	IF (LSTMM.EQ.-1) GOTO MMTOT2
	AMM = MON(LSTMM)
	CALL PRINT
MMTOT2,
	LSTMM = SMM
	RETURN
CATTOT,
	IF (LSTCAT.EQ.'-1') GOTO CATTO2
	CALL PRINT
	FOR I FROM 1 THRU NUMYY ABUCK(I) = DASHES
	CALL PRINT
	ACAT = '             CATEGORY SUBTOTALS: '
	FOR I FROM 1 THRU NUMYY ABUCK(I) = CATTOT(I),DLMASK
	CALL PRINT
	XCALL LINFD (1)
	INCR LINCNT
	IF (LINCNT+15.GT.60) LINCNT = 62
CATTO2,
	FOR I FROM 1 THRU NUMYY CATTOT(I) =
	FOR I FROM 1 THRU MAXCAT-1  IF (CATS(I).EQ.SCAT) PLINE (1,33) = CATS(I)
	IF (PLINE.EQ.BLANKS) PLINE (1,33) = CATS(MAXCAT)
	LSTCAT = SCAT
	RETURN

PRINT,	
	XCALL LPOUT (LINCNT,PGCNT,PLINE,TITLE,HDR1,'NO HDR',' ',
&		LEG1,'NO LEGEND',' ',0,132,PRTCTL,1,LPSW,RPTNUM,PRTTYP)
	RETURN

EOF,
	CALL MMTOT
	CALL CATTOT

	LINCNT = 62
	PLINE =
	FOR I FROM 1 THRU NUMYY ABUCK(I) = DASHES
	CALL PRINT
	ACAT = '                  YEARLY TOTALS: '
	FOR I FROM 1 THRU NUMYY ABUCK(I) = YRTOT(I),DLMASK
	CALL PRINT

CLOSE2,
	XCALL LPOFF (LPSW,SPLFIL,PGCNT)
CLOSE1,
	XCALL FILES (8,'I',58,4)
	SORT (INPUT=FILNAM,RECORD=SLSSUM/F,KEY=(SYY,SMM,SCAT),OUTPUT=FILNAM)
ENDOFF,
	XCALL PGCHN ('CP:SPCFUN',1)
	STOP
END

