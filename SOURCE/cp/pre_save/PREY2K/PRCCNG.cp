;  PRCCNG / COP 
;
;
;		::PCPYCOP.DEF::
;*********************************************************************
;		CUSTOMER ORDER PROCESSING 
;		
;		RELEASED: AUGUST 1, 1984 (d70s10)
;***********************************************************************
;
;		PROPRIETARY RIGHTS NOTICE:  All rights reserved.  This
;		material contains the valuable properties and trade secrets
;		of MCBA, Glendale, California, USA embodying substantial
;		creative effort and confidential information and ideas, no
;		part of which may be used and/or disclosed without MCBA's
;		duly authorized license agreement and/or written permission.
;
;		COPYRIGHT NOTICE:  Copyright <C> 1978, 1981, 1982, 1983, 1984
;		MCBA, AN UNPUBLISHED WORK.  ALL RIGHTS RESERVED.
;
;
;		COMPILE & LINK PER INSTALLATION OR TECHNICAL NOTES.
;
;:
;
;		MASS PRICE CHANGE
;
RECORD
	,A64,'THIS MATERIAL CONTAINS THE VALUABLE PROPERTIES AND TRADE SECRETS'
	,A62,'OF MCBA, EMBODYING CONFIDENTIAL INFORMATION AND IDEAS, NO PART'
	,A56,'OF WHICH MAY BE USED AND/OR DISCLOSED WITHOUT MCBAs DULY'
	,A55,'AUTHORIZED LICENSE AGREEMENT AND/OR WRITTEN PERMISSION.'
	,A61,'COPYRIGHT (C) MCBA, AN UNPUBLISHED WORK. ALL RIGHTS RESERVED.'

RECORD INVMAS		
		.INCLUDE 'DEF:RD041A.DEF'
RECORD DUMINV ,X	
		.INCLUDE 'DEF:RD041B.DEF'
RECORD INVIDX		
		.INCLUDE 'DEF:RD042A.DEF'
RECORD CTL
	ROW	,D2
		,A1
	COL	,D2
		,A1
	MAX	,D2
		,A1
	MIN	,D2
		,A1
	TYPE	,A2
RECORD
	PCTCNG	,D4
	TPRCAT	,A3
	CATLIT	,A4
	STRTNO	,A15
	START	,A15
	ENDNO	,A15
	END	,A15
	MASK	,A6	,'ZZ.XX-'
	STXCTL	,D1
	ENTRY	,A16
	INXCTL	,D1
	SWITCH	,D1
	CNGCTL	,D1
	WHATNO	,D1
	RECCTR	,D5, 00001
	TOTREC	,D5
	BLANKS	,A15
	CTR	,D2
	PRICES	,D2
	XROW	,D2
	V	,D1
	KEY	,A15
	BSEND	,D5
	BSMID	,D5
	SRCCTL	,D1
	COL2	,D2
	ROW2	,D2
	READ	,D1	,0
	WRITE	,D1	,1
	LOKCTL	,D1
	JUST 	,D1
PROC
	XCALL TERID (V)
	SWITCH = 3
	XCALL FILES (2,'U',41,SWITCH)		;FILE # 41 - INVMAS FILE
	IF (SWITCH.EQ.9) GOTO END1
	SWITCH = 5
	XCALL FILES(2,'U',41,SWITCH)
	IF (SWITCH.EQ.9) GO TO END2
	SWITCH = 3
	XCALL FILES (3,'I',42,SWITCH)
	IF (SWITCH.EQ.9) GOTO END2
	SWITCH = 5
	XCALL FILES(3,'I',42,SWITCH)
	IF (SWITCH.EQ.9) GO TO END3
	LOKCTL = 1
	XCALL IO (2,INVMAS,1,READ,LOKCTL)
	JUST = JSTIFY
	BSEND = ORG041
	TOTREC = REC041
	PRICES = NUMPRC
BEGIN,
	CNGCTL =
	XCALL OUTPT (1,1,2,'MASS PRICE CHANGE',V)
	XCALL OUTPT (5,26,0,'1. PERCENT CHANGE',V)
	XCALL OUTPT (6,26,0,'2. PRODUCT CATEGORY',V)
	XCALL OUTPT (7,26,0,'3. STARTING ITEM #',V)
	XCALL OUTPT (8,26,0,'4. ENDING ITEM #',V)
	XCALL OUTPT (10,26,0,'(SCREEN FIELD 2 ACCEPTS "END")',V)
PERCNT,
	CTL = '05,47,04,00,$-'
	CALL INPUT
	GOTO (BEGIN), INXCTL
	PCTCNG = ENTRY
	XCALL OUTPT (5,53,0,'%',V)
	GOTO (ANYCNG), CNGCTL
PRODCA,
	CTL = '06,47,02,00,AE'
	CALL INPUT
	GOTO (BEGIN,END3), INXCTL
	IF (ENTRY.EQ.BLANKS) ENTRY = 'ALL'
	TPRCAT = ENTRY
	XCALL OUTPT (6,47,0,TPRCAT,V)
	CALL SETLIT
	GOTO (ANYCNG), CNGCTL
STARTN,
	CTL = '07,47,15,00,AE'
	CALL INPUT
	GOTO (BEGIN,END3), INXCTL
	IF (ENTRY(1,15).EQ.BLANKS) GOTO DFLTIT
	IF (JUST) XCALL FRMAT (ENTRY(1,15),15)
	XCALL OUTPT (7,47,0,ENTRY(1,15),V)
	STRTNO = ENTRY(1,15)
	START = ENTRY(1,15)
ENDNUM,
	IF (START.EQ.'ALL   ') GO TO ANYCNG
	CTL = '08,47,15,00,AE'
	CALL INPUT
	GOTO (BEGIN,END3), INXCTL
	IF (ENTRY(1,15).EQ.BLANKS) ENTRY(1,15) = STRTNO
	IF (JUST) XCALL FRMAT (ENTRY(1,15),15)
	XCALL OUTPT (8,47,0,ENTRY(1,15),V)
	IF (ENTRY.LT.STRTNO) GOTO ENDNUM
	ENDNO = ENTRY(1,15)
	END = ENTRY(1,15)
	GOTO ANYCNG
CNGPC,
	GOTO (PERCNT,PRODCA,STARTN,ENDNUM), WHATNO
	GOTO ANYCNG
PROCES,
	IF (ENDNO.EQ.BLANKS .OR. ENDNO.EQ.'[[[[') END = START
	XCALL OUTPT (3,20,2,'THIS PROGRAM WILL PASS THROUGH THE ENTIRE',V)
	XCALL OUTPT (4,20,0,'ITEM FILE AND CHANGE ALL PRICES ON ITEMS IN',V)
	XCALL OUTPT (5,20,0,CATLIT,V)
	XCALL OUTPT (0,0,0,' PRODUCT CATEGORY(S) AND IN THE RANGE OF ITEMS',V)
	IF (START.NE.'ALL   ') GO TO RANGE
	XCALL OUTPT (6,25,0,'*** ALL ITEMS ***.',V)
	GO TO PCT
RANGE,
	XCALL OUTPT (6,20,0,'FROM ',V)
	XCALL OUTPT (0,0,0,START,V)
	XCALL OUTPT (0,0,0,' TO ',V)
	XCALL OUTPT (0,0,0,END,V)
	XCALL OUTPT (0,0,1,'.',V)
PCT,
	ENTRY(1,6) = PCTCNG, MASK
	XCALL OUTPT (8,20,0,'THE PERCENT CHANGE WILL BE ',V)
	XCALL OUTPT (0,0,0,ENTRY(1,6),V)
	XCALL OUTPT (0,0,0,'.',V)
	XCALL OUTPT (10,20,0,'ARE YOU SURE YOU WANT TO RUN THIS PROGRAM ?',V)
	XCALL OUTPT (7,0,-1,'CNTRL',V)
	XCALL OUTPT (0,0,0,'\',V)
	CTL = '10,65,01,01,YE'
	CALL INPUT
	GOTO (END3), INXCTL - 1
	XCALL WATE(3,V)
	IF (STRTNO.EQ.ENDNO) GOTO SERCH
	RECCTR = 1
GETITM,
	INCR RECCTR
	IF (RECCTR.GT.TOTREC) GOTO BEGIN
READIT,
	LOKCTL = 1
	XCALL IO (3,INVIDX,RECCTR,READ,LOKCTL)
	IF (IRC041.EQ. 0) GOTO GETITM
	IF (IITMNO.LT.STRTNO) GOTO GETITM
	IF (IITMNO.GT.ENDNO) GOTO GETITM
	IF (TPRCAT.NE.'ALL'.AND.IPRCAT.NE.TPRCAT) GOTO GETITM
REDMAS,
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC041,READ,LOKCTL)
	CTR =
PRICES,
	INCR CTR
	IF (CTR.GT.PRICES) GOTO RITE
	IF (CTR.GT.1.AND.PRICCD(CTR).EQ.'  ') GOTO RITE
	PRICE(CTR) = PRICE(CTR) + (((PRICE(CTR)*PCTCNG)/1000)#1)
	GOTO PRICES
RITE,
	LOKCTL = 1
	XCALL IO (2,INVMAS,IRC041,WRITE,LOKCTL)
	IF (STRTNO.EQ.ENDNO) GOTO BEGIN
	GOTO GETITM
SERCH,
	KEY = STRTNO
	XCALL SERCH (3,INVIDX,KEY,1,15,BSEND,BSMID,SRCCTL,4,1,6,0,0,0,0)
	IF (SRCCTL.EQ.1) GOTO NOTFND
	IF (TPRCAT.NE.'ALL'.AND.IPRCAT.NE.TPRCAT) GOTO INVPC
	GOTO REDMAS
NOTFND,
	XCALL MESAG ('ITEM # NOT FOUND',1)
	GOTO BEGIN
INVPC,
	XCALL MESAG ('PRODUCT CATEGORY DOES NOT MATCH',1)
	GOTO BEGIN
END3,
	XCALL WATE(3,V)
	XCALL FILES (3,'I',42,4)
END2,
	XCALL FILES (2,'U',41,4)
END1,
	XCALL WATE(3,V)
	XCALL PGCHN ('CP:CPMENU',1)
;
SETLIT,
	CATLIT (1,1) = '"'
	CATLIT (4,4) = '"'
	CATLIT (2,3) = ENTRY (1,2)
	IF (ENTRY.EQ.'ALL') CATLIT = 'ALL'
	RETURN
DFLTIT,
	START = 'ALL'
	END =
	STRTNO =
	ENDNO = '[[[['
	XCALL OUTPT (ROW,COL,0,'ALL            ',V)
	XROW=ROW+1
	XCALL OUTPT (XROW,COL,1,'\',V)
	GOTO ANYCNG
INPUT,
	XCALL INPUT(ROW,COL,MAX,MIN,TYPE,ENTRY,INXCTL,V)
	RETURN
ANYCNG,
	XCALL ANYCN(CNGCTL,WHATNO)
	GOTO (PROCES,CNGPC,BEGIN), CNGCTL + 1
END
